# [\#3372 Issue](https://github.com/ethereum/solidity/issues/3372) `closed`: Suspected problem in optimizer
**Labels**: `documentation :book:`


#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) opened issue at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372):

I have a purely stateless contract with 3 functions - `log`, `floorLog` and `exp`.
I am testing the gas-estimate of function `log`, which calls function `floorLog`.
Function `exp` is just a dummy function which returns a constant value.

When optimization is enabled, the estimated gas of function `log` changes dramatically, depending on this value.

For example, running `log(123456, 25000)`:
- When the constant value is 0x080000000000000000000000000000000:
  - transaction cost = 52385 gas without optimizer, 51119 gas with optimizer
  - execution cost = 30537 gas without optimizer, 29271 gas with optimizer
- When the constant value is anything other than 0x080000000000000000000000000000000:
  - transaction cost = 52385 gas without optimizer, 42866 gas with optimizer
  - execution cost = 30537 gas without optimizer, 21018 gas with optimizer

Note that 0x080000000000000000000000000000000 is (also) the value of the global constant variable `FIXED_1`.
This variable is used in function `log`, and I'm suspecting that the optimizer messes things up because of that.

I have ran into the problem while testing via Remix, as well as via Web3 + TestRPC.
For the latter, I have used:
- Ganache CLI v6.0.3 (ganache-core: 2.0.2)
- solcjs v0.4.19 (commit.c4cbbb05.Emscripten.clang)

The contract is provided hereby:

```
pragma solidity ^0.4.18;

contract PriceCalculator {
    uint256 private constant ONE = 1;
    uint8 private constant PRECISION = 127;
    uint256 private constant FIXED_1 = 0x080000000000000000000000000000000;
    uint256 private constant FIXED_2 = 0x100000000000000000000000000000000;
    uint256 private constant MAX_NUM = 0x1ffffffffffffffffffffffffffffffff;

    function log(uint256 numerator, uint256 denominator) public pure returns (uint256) {
        uint256 res = 0;

        assert(numerator <= MAX_NUM);
        uint256 x = numerator * FIXED_1 / denominator;

        // If x >= 2, then we compute the integer part of log2(x), which is larger than 0.
        if (x >= FIXED_2) {
            uint8 count = floorLog(x / FIXED_1);
            x >>= count; // now x < 2
            res = count * FIXED_1;
        }

        // If x > 1, then we compute the fraction part of log2(x), which is larger than 0.
        if (x > FIXED_1) {
            for (uint8 i = PRECISION; i > 0; --i) {
                x = (x * x) / FIXED_1; // now 1 < x < 4
                if (x >= FIXED_2) {
                    x >>= 1; // now 1 < x < 2
                    res += ONE << (i - 1);
                }
            }
        }

        return res;
    }

    function floorLog(uint256 n) public pure returns (uint8) {
        uint8 res = 0;

        if (n < 256) {
            // At most 8 iterations
            while (n > 1) {
                n >>= 1;
                res += 1;
            }
        }
        else {
            // Exactly 8 iterations
            for (uint8 s = 128; s > 0; s >>= 1) {
                if (n >= (ONE << s)) {
                    n >>= s;
                    res |= s;
                }
            }
        }

        return res;
    }

    function exp(uint256 x) public pure returns (uint256) {
        return 0x080000000000000000000000000000000;
    }
}
```

Thanks.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355016595):

I'm not sure I understand. Is the issue that the performance of the optimizer differs on the actual constants used? Which gas costs are you referring to exactly, the deployment costs or the execution costs? Also, which optimizer setting did you use (the "runs" parameter)?

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355018197):

The gas-cost of the `log` function changes dramatically when I change the constant value returned by the `exp` (dummy) function.
But it changes only when optimization is enabled, which leads me to the conclusion that the optimizer is "incompetent" with respect to the given contract.

Optimizer settings:
- With Remix, I simply marked the `Enable Optimization` option under the `Settings` tab.
- With Web3 + TestRPC, I used the output of `solcjs --bin --abi --optimize`.

Thanks.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355247593):

OK, thanks for the explanation! I think this is an effect of the tradeoff between deployment and runtime costs of constants. This step is only activated if there are at least four references to the same number (this is why you see the effect depending on the actual value used inside the `exp` function). The optimizer is not able to detect any loops, it just assumes that each constant will be used exactly `<optimizer-runs>` times during the lifetime of the contract and adjusts the tradeoff accordingly. Since you use one constant inside a loop, this estimate is of course dramatically off.

You might get better results by just using `--optimize-runs 5000000` or something like that.

In one version, the optimizer uses the value as a literal, in the other it computes the value using the `exp` opcode. The former method has lower runtime costs, the latter has lower deployment costs.

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355249014):

Thank you very much!!!
I will now give it a try.
BTW, `--optimize-runs` does not show when I run `solcjs --help`, so it might be useful to add it (or any other "hidden" flag for that matter) there.
Thanks again!

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355250376):

OK, adding `--optimize-runs 5000000` to my build command-line does not seem to yield different `bin` or `abi` files. Any other suggestions?
P.S.: In reality, function `exp` is anything but "dummy", and it contains several references to the `FIXED_1` constant variable.
Thank you.

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355264616):

> BTW, --optimize-runs does not show when I run solcjs --help, so it might be useful to add it (or any other "hidden" flag for that matter) there.

There are no hidden flags. `solcjs` simply doesn't support it, hence it is not listed. It should have thrown an error for using an unsupported option though.

Try using `solc` (from the releases page here). It should be much faster than the Javascript version on the commandline.

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355265287):

> from the releases page here

Where?

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355265470):

https://github.com/ethereum/solidity/releases

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355270367):

Wow, not only is it faster, but it also generates each pair of bin/abi files with the same name as the input sol file (in opposed to `solcjs`, which adds a prefix based on the relative path of the input sol file (at least in the Windows version of this utility), which you are then obliged to remove).
Thank you very much!!!!!

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-355270507):

And with regards to the actual problem at hand - `--optimize-runs 5000000` seems to resolve it!!!

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-359997711):

@barakman would you be interested in documenting this tradeoff? Others may run into the same issue too.

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-409635092):

@ChrisChinchilla this about documenting what tradeoff the "runs" parameter is setting. It may also be properly documented.

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-456470389):

@chriseth:

This question is following your suggestion to use `--optimize-runs 5000000` ([above](https://github.com/ethereum/solidity/issues/3372#issuecomment-355247593)).

We are now at a point close to release.

One of the comments that we received in an audit-report was:

> Even though the community has started to experiments with such aggressive optimizations settings, itâ€™s relatively new and not as battle-tested as the default 200 optimizer runs.
> Our recommendation is to avoid such an aggressive optimization, for the time being, and prefer the default settings instead.

Would you be able to say if a solid and thorough verification has been conducted, to ensure that the `optimize-runs` parameter is safe for use with any value? Any major projects that have used it with any value other than the default? How safe can we feel ignoring the audit-comment above?

I've conducted extensive benchmark tests, showing that using 5000000 runs saves around 3% - 5% gas, so I'd hate to lose this cost-reduction. Then again, this upside is not worth even the slightest chance of erroneous execution due to a bug in the optimizer.

Thank you very much for your help :)

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-456477186):

If you have 100% test coverage, I think you should be safe. The tradeoff parameter should only affect constants and literals. If they are compiled incorrectly, you should notice quickly.

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-456478772):

@chriseth:
Thank you for the very quick response.

Can you please explain `The tradeoff parameter should only affect constants and literals`?

I am finding it quite hard to understand.

Assuming that by "tradeoff parameter" you refer to the `optimize-runs` parameter, it makes very little sense to me that the value of this parameter "should only affect constants and literals" (I mean, how exactly would any optimization be achieved this way?).

Thanks

Update:
My question does not refer specifically to the coding example provided at the top of this thread.
We're applying `--optimize-runs 5000000` on our entire system, hence the question is in general.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-456491249):

Please read about the parameter here: https://solidity.readthedocs.io/en/v0.5.3/using-the-compiler.html#using-the-commandline-compiler

It does not specify how often to invoke the optimizer - it will always perform as many optimizations until there is no efficiency gain in the binary anymore. It specifies whether you rather want a contract that is expensive to deploy but cheap to invoke or the opposite. The main component in the optimizer that makes use of that parameter is the one that encodes constants and string literals.

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-456498037):

@chriseth:

Well, the docs don't really go into details about this one. They just specify the "deployment cost vs runtime cost" tradeoff, but they don't explain how it works "under the hood" (including your notion about the encoding of constants and string literals).

I need something to persuade the auditer (and myself) that this is indeed safe to choose values higher than 200. Would you have a link which describes the optimizer in greater details?

Thanks

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-456719563):

@chriseth :

I am following the code in [ConstantOptimiser.cpp](https://github.com/ethereum/solidity/blob/develop/libevmasm/ConstantOptimiser.cpp).

Would that be sufficient for obtaining a good-enough perspective on the mechanism behind `optimize-runs`?

Side note: your repo is inconsistent in the spelling of 'optimize'; you might want to case-sensitively find/replace `ptimis` with `ptimiz`.

Thanks

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-456721741):

The tradeoff parameter is used in the constant optimizer and the [dispatch routine](https://github.com/ethereum/solidity/blob/develop/libsolidity/codegen/ContractCompiler.cpp#L277). If you have tests that access every constant once and call every function once, you should be pretty safe.

As far as the spelling is concerned, yes, we couldn't yet decide which dialect of English to follow. :)

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-467204468):

@chriseth should this issue be kept open? Do we need to improve documentation?

#### <img src="https://avatars.githubusercontent.com/u/7003246?v=4" width="50">[barakman](https://github.com/barakman) commented at [2018-01-03 13:48](https://github.com/ethereum/solidity/issues/3372#issuecomment-467442476):

@chriseth :
Thanks. I will use this thread (starting from [here](https://github.com/ethereum/solidity/issues/3372#issuecomment-456470389)) as a response for the audit's related comment.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
