# [\#4060 Issue](https://github.com/ethereum/solidity/issues/4060) `closed`: Public properties not providing a getter as expected

#### <img src="https://avatars.githubusercontent.com/u/683181?u=b2090e30956b23569a048e97fc478ba8ad272715&v=4" width="50">[saturnial](https://github.com/saturnial) opened issue at [2018-05-04 01:14](https://github.com/ethereum/solidity/issues/4060):

Below you will find a set of dummy contracts that illustrate the pattern I'm attempting to accomplish.

At a high level, I'm creating a registry contract `A` that holds a reference to contract `B`.  I then instantiate contract `C` with a reference to contract `A`, which in turn holds a reference to `B`. I expect that I should then be able to access functions on contract `B` from contract `C` through `A` given that contract `A` has a public property referencing contract `B`. Instead, I get the following error:

> TypeError: Member "test" not found or not visible after argument-dependent lookup in function () view external returns (contract B) `return a.b.test();`

The code is as follows:

```
pragma solidity 0.4.18;


// Registry contract
contract A {

    B public b;

    function A(
        address _b
    ) public {
        b = B(_b);
    }
}

// Contract in the registry
contract B {

    address public d;

    function B(
        address _d
    ) public {
        d = _d;
    }

    function test() public view returns (address) {
        return d;
    }
}


// Contract with access to the registry
contract C {

    A public a;

    function C(
        address _a
    ) public {
        a = A(_a);
    }

    function work() public view returns (address) {
        return a.b.test();
    }
}
```

Upon rewriting the contracts with my own getter `getB()` that returns the `B` instance, the code now compiles and works as expected. See below:

```
pragma solidity 0.4.18;


// Registry contract
contract A {

    B b;

    function A(
        address _b
    ) public {
        b = B(_b);
    }

    function getB() public view returns (B) {
        return b;
    }
}

// Contract in the registry
contract B {

    address public d;

    function B(
        address _d
    ) public {
        d = _d;
    }

    function test() public view returns (address) {
        return d;
    }
}


// Contract with access to the registry
contract C {

    A public a;

    function C(
        address _a
    ) public {
        a = A(_a);
    }

    function work() public view returns (address) {
        return a.getB().test();
    }
}
```

This is counter-intuitive as I would assume a public property would expose a getter identical to the one I wrote out in the second code snippet.

Is this a bug or am I misconstruing `public`?

Any clarification would be much appreciated.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-05-04 01:14](https://github.com/ethereum/solidity/issues/4060#issuecomment-386563494):

The getter is an actual function, so you have to call it: `return a.b().test();`

I created an issue to improve the error message: https://github.com/ethereum/solidity/issues/4066

#### <img src="https://avatars.githubusercontent.com/u/31821845?u=9b393f6490b021c8b5fe69e6dcf02bd95e6e39b0&v=4" width="50">[ivan-homoliak-sutd](https://github.com/ivan-homoliak-sutd) commented at [2018-05-04 01:14](https://github.com/ethereum/solidity/issues/4060#issuecomment-390385598):

I've got the same issue when accessing external public structs or their members, even though I am using () to invoke getters. To me, it looks that autogenerated getters do not work with structs, which is a pity.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
