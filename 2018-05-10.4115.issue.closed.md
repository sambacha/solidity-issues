# [\#4115 Issue](https://github.com/ethereum/solidity/issues/4115) `closed`: A Struct which contains Array of Structs can't be initialized

#### <img src="https://avatars.githubusercontent.com/u/17618905?u=de6a39a3890d53984a2d9ebdc4ce1553169d19ec&v=4" width="50">[carbogninalberto](https://github.com/carbogninalberto) opened issue at [2018-05-10 08:56](https://github.com/ethereum/solidity/issues/4115):

# Is there anyway to initialized a Struct which contains an Array of Structs?
### Let's suppose we have this contract:

```solidity
contract ChallengeManager is Owned {
        Challenge[] public challenges;
        struct Participant {
                address participantAddress;
		bool participate;
		uint goal;
	}
	struct Challenge {
		string name;
		uint256 timestamp;
		address owner;
		bytes32 challengeHash;
		Participant[] participants;
	}
}
```

Now, i can't initialized the Participant[] array inside Challenge.
Using this line when initializing struct
```solidity
new Participant[](0)
```
i get the following error:
> UnimplementedFeatureError: Copying of type struct ChallengeManager.Participant memory[] memory to storage not yet supported.

A less elegant solution i thought is deleting Participant struct and modify Challenge like this:

```solidity
struct Challenge {
	string name;
	uint256 timestamp;
	address owner;
	bytes32 challengeHash;
	address[] participantAddress;
	bool[] participate;
	uint[] goal;
}
```

But this _**is not a great solution**_ in my opinion.
### Do you have any suggestion?


#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-05-10 08:56](https://github.com/ethereum/solidity/issues/4115#issuecomment-388411697):

We will soon have `challenges.pushEmpty()` or something like that. Until then, please use `challenges.length++;` and then initialize using `Challenge storage c = challenges[challenges.length - 1]; c.name = ...`

#### <img src="https://avatars.githubusercontent.com/u/25003254?u=ee4560418a40e0d4fda713dd7553ddda37eaf09f&v=4" width="50">[egealpay](https://github.com/egealpay) commented at [2018-05-10 08:56](https://github.com/ethereum/solidity/issues/4115#issuecomment-609464550):

Any update on this?

#### <img src="https://avatars.githubusercontent.com/u/49253502?u=1fa82568ec89383b11faad9bc7876157959ebb2f&v=4" width="50">[ferdrian06](https://github.com/ferdrian06) commented at [2018-05-10 08:56](https://github.com/ethereum/solidity/issues/4115#issuecomment-609530643):

> # Is there anyway to initialized a Struct which contains an Array of Structs?
> ### Let's suppose we have this contract:
> ```solidity
> contract ChallengeManager is Owned {
>         Challenge[] public challenges;
>         struct Participant {
>                 address participantAddress;
> 		bool participate;
> 		uint goal;
> 	}
> 	struct Challenge {
> 		string name;
> 		uint256 timestamp;
> 		address owner;
> 		bytes32 challengeHash;
> 		Participant[] participants;
> 	}
> }
> ```
> 
> Now, i can't initialized the Participant[] array inside Challenge.
> Using this line when initializing struct
> 
> ```solidity
> new Participant[](0)
> ```
> 
> i get the following error:
> 
> > UnimplementedFeatureError: Copying of type struct ChallengeManager.Participant memory[] memory to storage not yet supported.
> 
> A less elegant solution i thought is deleting Participant struct and modify Challenge like this:
> 
> ```solidity
> struct Challenge {
> 	string name;
> 	uint256 timestamp;
> 	address owner;
> 	bytes32 challengeHash;
> 	address[] participantAddress;
> 	bool[] participate;
> 	uint[] goal;
> }
> ```
> 
> But this _**is not a great solution**_ in my opinion.
> 
> ### Do you have any suggestion?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-05-10 08:56](https://github.com/ethereum/solidity/issues/4115#issuecomment-609915614):

There is `.push()` now.

#### <img src="https://avatars.githubusercontent.com/u/6491112?u=eee27d774752202954a1575a88a21864fdbd59d6&v=4" width="50">[naddison36](https://github.com/naddison36) commented at [2018-05-10 08:56](https://github.com/ethereum/solidity/issues/4115#issuecomment-612255375):

@chriseth I can see how you can `.push()` a new `Challenge` to the `challenges` array. But how do you initialise an empty `participants` array on the `Challenge` struct?

The below is failing with
`UnimplementedFeatureError: Copying of type struct ChallengeManager.Participant memory[] memory to storage not yet supported.`

```Solidity
contract ChallengeManager {
    struct Participant {
        address participantAddress;
		bool participate;
	}
	struct Challenge {
		bytes32 challengeHash;
		uint256 timestamp;
		address owner;
		Participant[] participants;
	}
	Challenge[] public challenges;
	
	function addChallenge(bytes32 _challengeHash) public {
	    challenges.push(Challenge({
	        challengeHash: _challengeHash,
	        timestamp: now,
	        owner: msg.sender,
	        participants: new Participant[](0)
	    }));
	}
}
```

I've also tried the below but this fails with `This variable is of storage pointer type and can be accessed without prior assignment, which would lead to undefined behaviour.`

```Solidity
function addChallenge(bytes32 _challengeHash) public {
    Challenge storage newChallange;
    newChallange.hash = _challengeHash;
    newChallange.timestamp = now;
    newChallange.owner = msg.sender;
    challenges.push(newChallange);
}
```

#### <img src="https://avatars.githubusercontent.com/u/6491112?u=eee27d774752202954a1575a88a21864fdbd59d6&v=4" width="50">[naddison36](https://github.com/naddison36) commented at [2018-05-10 08:56](https://github.com/ethereum/solidity/issues/4115#issuecomment-612309066):

I worked out how to do the above. You can push an empty struct to the storage array and then update the properties you want. Any properties you don't update will have a default value. In the below case, `participants` on the `Challenge` struct will be an empty array of `Participant` structs.

```Solidity
pragma solidity ^0.6.6;

contract ChallengeManager {
    struct Participant {
        address participantAddress;
		bool participate;
	}
	struct Challenge {
		bytes32 hash;
		uint256 timestamp;
		address owner;
		Participant[] participants;
	}
	Challenge[] public challenges;
	
	function addChallenge(bytes32 _challengeHash) public {
	    challenges.push();
	    uint256 newIndex = challenges.length - 1;
	    challenges[newIndex].hash = _challengeHash;
        challenges[newIndex].timestamp = now;
        challenges[newIndex].owner = msg.sender;
	}
}
```


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
