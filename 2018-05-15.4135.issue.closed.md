# [\#4135 Issue](https://github.com/ethereum/solidity/issues/4135) `closed`: ABIEncoderV2 nested array of structs support

#### <img src="https://avatars.githubusercontent.com/u/6491112?u=eee27d774752202954a1575a88a21864fdbd59d6&v=4" width="50">[naddison36](https://github.com/naddison36) opened issue at [2018-05-15 08:32](https://github.com/ethereum/solidity/issues/4135):

I'd like to have a struct that contains an array of other structs and can be set using ABIEncoderV2.

Here's an example where I have a struct `TestStruct` that contains an array of `NestedStruct`'s that is set by the `setTestStruct` function
```
pragma solidity ^0.4.23;
pragma experimental ABIEncoderV2;

contract TestContract
{
    struct TestStruct {
        string name;
        bool init;
        NestedStruct[] nestedStructs;
    }

    struct NestedStruct {
        string name;
        bool init;
    }

    TestStruct testStruct;

    function setTestStruct(TestStruct _testStruct) public
    {
        testStruct = _testStruct;
    }
}
```

This fails with the following compile time error
```
Compiling using default compiler, solidity version: 0.4.23+commit.124ca40d.Emscripten.clang
UnimplementedFeatureError: Copying of type struct TestContract.NestedStruct memory[] memory to storage not yet supported.
```

Is there a way to make this work?

Thanks
Nick


#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-05-15 08:32](https://github.com/ethereum/solidity/issues/4135#issuecomment-389092838):

This is not about the ABI encoder, since your `_testStruct` is properly created. There are some memory-to-storage copy loops which are not implemented and which we might even disallow in the future, because they hide gigantic gas usages. The following code should do it:

```solidity
pragma solidity ^0.4.23;
pragma experimental ABIEncoderV2;

contract TestContract
{
    struct TestStruct {
        string name;
        bool init;
        NestedStruct[] nestedStructs;
    }

    struct NestedStruct {
        string name;
        bool init;
    }

    TestStruct testStruct;

    function setTestStruct(TestStruct _testStruct) public
    {
        testStruct.name = _testStruct.name;
        testStruct.init = _testStruct.init;
        for (uint i = 0; i < _testStruct.nestedStructs.length; i ++)
        {
            testStruct.nestedStructs.push(_testStruct.nestedStructs[i]);
        }
    }
}
```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-05-15 08:32](https://github.com/ethereum/solidity/issues/4135#issuecomment-389093452):

So you basically have to manually copy arrays of structs. Please reopen if you need further help.

#### <img src="https://avatars.githubusercontent.com/u/6491112?u=eee27d774752202954a1575a88a21864fdbd59d6&v=4" width="50">[naddison36](https://github.com/naddison36) commented at [2018-05-15 08:32](https://github.com/ethereum/solidity/issues/4135#issuecomment-389094620):

Beautiful. Thanks @chriseth


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
