# [\#4315 Issue](https://github.com/ethereum/solidity/issues/4315) `closed`: Invalid struct initialization leads to unexpected behavior

#### <img src="https://avatars.githubusercontent.com/u/25631?v=4" width="50">[phiferd](https://github.com/phiferd) opened issue at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315):

@mustafa92348 (attacker) posted a contract with the following code to the gitter tutorials channel:

SCAM

```
pragma solidity ^0.4.24;

// To play, call the play() method with the guessed number.  Bet price: 0.1 ether

contract CryptoRoulette {

    uint256 public secretNumber;
    uint256 public lastPlayed;
    uint256 public betPrice = 0.1 ether;
    address public ownerAddr;

    struct Game {
        address player;
        uint256 number;
    }
    Game[] public gamesPlayed;

    function CryptoRoulette() public {
        ownerAddr = msg.sender;
        generateNewRandom();
    }

    function generateNewRandom() internal {
        // initialize secretNumber with a value between 0 and 15
        secretNumber = uint8(sha3(now, block.blockhash(block.number-1))) % 16;
    }

    function play(uint256 number) payable public {
        require(msg.value >= betPrice && number < 16);

        Game game;
        game.player = msg.sender;  // <-- this assignment changes secretNumber
        game.number = number;
        gamesPlayed.push(game);

        if (number == secretNumber) {
            // win!
            if(msg.value*15>this.balance){
                msg.sender.transfer(this.balance);
            }
            else{
                msg.sender.transfer(msg.value*15);
            }
        }

        generateNewRandom();
        lastPlayed = now;
    }

    function kill() public {
        if (msg.sender == ownerAddr && now > lastPlayed + 1 days) {
            suicide(msg.sender);
        }
    }

    function() public payable { }
}
```

The code appears to be a naive attempt at a lottery contract.  The obvious issue is that the "secretNumber" field is visible to everyone. The contract is initialized with 1 ETH, and is intended to entice people into playing under the assumption that they can make some easy ETH (or help the the newbie by rescuing his ETH).  However, they cannot win.

@MicahZoltu noticed that the assignment on line 32 is what causes the trouble:

```
Game game;
game.player = msg.sender;
```
The effect of this statement is to *change the secretNumber* stored in the first slot of the contract, making it impossible to win the game.

The issue appears to be that initializing game as `Game game;` creates an object that shares ~memory~ storage with the contract itself.  So, assigning `game.player = msg.sender` overwrites the `secretNumber` field in the CryptoRoulette contract.  This should not be possible.

I think this is the right place to open this issue, but feel free to redirect me if it belongs somewhere else.


#### <img src="https://avatars.githubusercontent.com/u/886059?u=408de357d90aae9b9ffc956970b8fd4eec642060&v=4" width="50">[MicahZoltu](https://github.com/MicahZoltu) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398293761):

Solidity does give a compiler warning, but such warnings do not show up in etherscan.  One option would be to make etherscan present compiler warnings, though in this case it feels like the compiler shouldn't allow assignment to uninitialized local storage variables.

#### <img src="https://avatars.githubusercontent.com/u/25631?v=4" width="50">[phiferd](https://github.com/phiferd) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398297377):

Even showing the warning probably wouldn't help much. Is there any use case where accessing an uninitialized storage pointer makes sense?

#### <img src="https://avatars.githubusercontent.com/u/886059?u=408de357d90aae9b9ffc956970b8fd4eec642060&v=4" width="50">[MicahZoltu](https://github.com/MicahZoltu) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398307466):

No, but it can be difficult to statically prove that the variable is initialized before being used in all code paths.  I think for Solidity, security should be king so if the analyzer cannot prove that the variable _will_ be initialized before being used then it should error instead of warn.  Curious to hear if someone feels that is too extreme.

#### <img src="https://avatars.githubusercontent.com/u/25631?v=4" width="50">[phiferd](https://github.com/phiferd) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398316781):

If it can be handled at compile time, that would be great.  However, I think the code above should throw an exception at runtime, which should be easier to detect.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398466651):

I think it would be a good idea for etherscan to highlight any contract that compiles with warnings as potentially malicious. Even if people use external libraries that are not up to date with recent security best practices. If they want random users to use their code and trust that it operates correctly, they have to put the additional effort into that and update the library's code.

Concerning the issue at hand: The warning will turn into an error with the next version, so I consider this issue solved.

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398471730):

The warning in question is:
```
4315.sol:31:9: Warning: Uninitialized storage pointer. Did you mean '<type> memory game'?
        Game game;
        ^-------^
```

#### <img src="https://avatars.githubusercontent.com/u/25631?v=4" width="50">[phiferd](https://github.com/phiferd) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398579929):

Thanks @chriseth .  Can you clarify, would the following compile and have the same effect?

```
Game storage game;
game.player = msg.sender;  // <-- this assignment changes secretNumber
```

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-06-19 06:25](https://github.com/ethereum/solidity/issues/4315#issuecomment-398580870):

That is still an uninitialised pointer reporting the same warning. Also one can test this in Remix IDE with latest release (should receive warnings for the above). Soon one of the nightlies will report it as an error though.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
