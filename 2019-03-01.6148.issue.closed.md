# [\#6148 Issue](https://github.com/ethereum/solidity/issues/6148) `closed`: An ERC20 contract was identified as an ERC721 contract.

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) opened issue at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148):

I found in an ERC20-token contract, the Transfer() event would impact the contract type of ecah token transcation, for example.

Case1, when i used, `event Transfer(address indexed _from, address indexed _to, uint256 indexed _value);` the transcation was ERC721.
pls see https://kovan.etherscan.io/tx/0xce25abd0ded4566717010048e59fa9ce5601b6d3f612b0676ae754ac8e9313f0
Case2, when i used `event Transfer(address indexed _from, address indexed _to, uint256 _value);` the transcation was ERC721.
pls see https://kovan.etherscan.io/tx/0xeed92be7c6a1d09cd1fe197f9998db699ba25359f269fba6a55c174d7e3e9e55

up to now, as I know, the keyword `indexed` is just used to filter the log. Why it did make the contract type of token transcation on Kovan?

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-468629127):

I'm not sure I understand, can you elaborate a bit more?

#### <img src="https://avatars.githubusercontent.com/u/27282380?u=c3aa940fa56a7641bbb85c767b743ce4888c6ea7&v=4" width="50">[Skyge](https://github.com/Skyge) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469122386):

@YannickXiong I am not sure how does it come out, but I think the event `Transfer()` in the ERC20-token contract only can impact itself rather than other contract, such as ERC721. Obviously, the event `Transfer()` exists both in the two type contract, but how does the event be triggered? This only happens when someone calls the special function, so when you call a function of the ERC20-token contract, it will only impact on itself, if not, it is really a big problem. so in order to remove all hidden dangers, could you please give more details about how do you operate it.

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469135086):

I could reproduce the problem. Below was the details.

# the source code.
```
pragma solidity ^0.4.25;

/**
 * @title  : erc20 demo
 * @dev    : a erc20 token implement.
 * @author : yannick.xiong@aliyun.com
 */

library SafeMath {
    /**
     * @dev : Multiplies two unsigned integers, reverts on overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b);

        return c;
    }

    /**
     * @dev : Integer division of two unsigned integers truncating the quotient, reverts on division by zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        // Solidity only automatically asserts when dividing by 0
        require(b > 0);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    /**
     * @dev : Subtracts two unsigned integers, reverts on overflow (i.e. if subtrahend is greater than minuend).
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a);
        uint256 c = a - b;

        return c;
    }

    /**
     * @dev : Adds two unsigned integers, reverts on overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a);

        return c;
    }

    /**
     * @dev : Divides two unsigned integers and returns the remainder (unsigned integer modulo),
     * reverts when dividing by zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b != 0);
        return a % b;
    }
}

interface AuthorizeToken{
    /**
     * @dev : 
     * @param _authorizedFrom : 
     * @param _authorizedTo : 
     * @param _value : 
     * @param _extraData : 
     */
    function authorizeToken(address _authorizedFrom, address _authorizedTo, uint256 _value, bytes _extraData) external;
}


contract ERC20BaseToken{
    // using from
    using SafeMath for uint256;

    // state vars
    string public name;
    string public symbol;
    uint8 public decimals;

    uint256 public totalSupply;
    
    // balance of each address
    mapping(address => uint256) public balanceOf;

    // allowance[A][B] menas the balance of address B could used which authorized by address A
    mapping(address => mapping(address => uint256)) public allowance;

    // event Transfer(address indexed _from, address indexed _to, uint256 _value);
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _value);

    event Approval(address indexed _tokenOwner, address indexed _spender, uint256 indexed _value);
    event BurnEvent(address indexed _from, uint256 indexed _value);

    constructor(uint256 initialSupply, string tokenName, string tokenSymbol, uint8 tokenDecimals) 
    public {
        name = tokenName;
        symbol = tokenSymbol;
        decimals = tokenDecimals;

        totalSupply = initialSupply * 10 ** uint256(decimals);

        balanceOf[msg.sender] = totalSupply;
    }

    // methods

    /**
     * @dev : return totalSupply of a contract.
     */
    function totalSupply() public view returns(uint256){
        return totalSupply;
    }

    /**
     * @dev : return balance of a tokenOwner.
     */
    function balanceOf(address tokenOwner) public view returns(uint256){
        return balanceOf[tokenOwner];
    }

    /**
     * @dev    : 
     * @param _from : 
     * @param _to   : 
     * @param _value : 
     */
    // bug: https://github.com/ethereum/solidity/issues/6124
    function _transfer(address _from, address _to, uint256 _value) internal {

        require(_to != 0x0);
        require(balanceOf[_from] >= _value);
        require(balanceOf[_to] + _value >= balanceOf[_to]);
        
        uint256 preBalance = balanceOf[_from].add(balanceOf[_to]);
        balanceOf[_from] = balanceOf[_from].sub(_value);
        balanceOf[_to] = balanceOf[_to].add(_value);

        emit Transfer(_from, _to, _value);

        assert(balanceOf[_from].add(balanceOf[_to]) == preBalance);
    }
    
    /**
     * @dev    : 
     * @param _to   : 
     * @param _value : 
     */
    function transfer(address _to, uint256 _value) public {
        _transfer(msg.sender, _to, _value);
    }

    /**
     * @dev    : 
     * @param _from : 
     * @param _to   : 
     * @param _value : 
     */
    function transferFrom(address _from, address _to, uint256 _value) public returns(bool){
        require(_value <= allowance[_from][msg.sender]);

        allowance[_from][msg.sender] = allowance[_from][msg.sender].sub(_value);

        _transfer(_from, _to, _value);

        return true;
    }

    /**
     * @dev : 
     */
    function approve(address _spender, uint256 _value) public returns(bool) {
        require(balanceOf[msg.sender] >= _value);

        allowance[msg.sender][_spender] = _value;

        emit Approval(msg.sender, _spender, _value);

        return true;
    }

    /**
     * @dev : 
     * @param _tokenOwner : 
     * @param _spender : 
     * @return : 
     */
    function allowance(address _tokenOwner, address _spender) public view returns(uint256) {
        return allowance[_tokenOwner][_spender];
    }



    /**
     * @dev : 
     * @param _value : 
     */
    function burn(uint256 _value) public returns(bool) {
        require(balanceOf[msg.sender] >= _value);

        balanceOf[msg.sender] = balanceOf[msg.sender].sub(_value);
        totalSupply = totalSupply.sub(_value);

        emit BurnEvent(msg.sender, _value);

        return true;
    }

    /**
     * @dev : 
     * @param _from : 
     * @param _value : 
     */
    function burnFrom(address _from, uint256 _value) public returns(bool) {
        require(balanceOf[msg.sender] >= _value);
        require(allowance[_from][msg.sender] >= _value);

        balanceOf[msg.sender] = balanceOf[msg.sender].sub(_value);
        allowance[_from][msg.sender] = allowance[_from][msg.sender].sub(_value);
        totalSupply = totalSupply.sub(_value);

        emit BurnEvent(msg.sender, _value);

        return true;
    }

    /**
     *
     */
    function approveToContract(address _spender, uint256 _value, bytes _extraData) 
    public 
    returns(bool) {
        AuthorizeToken authorizedContractSpender = AuthorizeToken(_spender);

        if (approve(_spender, _value)) {
            authorizedContractSpender.authorizeToken(msg.sender, this, _value, _extraData); 
        }
    }
}

contract ThirdParty{

    event BeAuthorized
    (address _sender, 
    address _self, 
    address _authorizedFrom, 
    address _authorizedTo, 
    uint256 _value, bytes 
    _extraData);

    function authorizeToken(address _authorizedFrom, address _authorizedTo, uint256 _value, bytes _extraData) 
    public {
        emit BeAuthorized(msg.sender, this, _authorizedFrom, _authorizedTo, _value, _extraData);
    }

    
}

```

# testing steps.
## Common steps.
- Compiled the contract in remix-ide, version is `solidity ^0.4.25;`
- Deployed the contract to Kovan using MetaMask;
- Added the token to MetaMask;
- Transfer some token using MetaMask;

## Case 1, Token AT, using `event Transfer(address indexed _from, address indexed _to, uint256 _value);`
contract was created trx: https://kovan.etherscan.io/tx/0xb691876dd9537596f06e059e12e77f2f5685eb5db876da39dcd8ab37ff4297b2
contract address: 0xea0E52eFB8Bb2525B1388Efa412172162DfCbA1D
transfer 500 at token trx: https://kovan.etherscan.io/tx/0x63e667be8a08ef546060027b733217921a276bcec1704b4716558ea0e95cbb28
the transfer trx is ERC20.

## Case 2, Token BT, using ` event Transfer(address indexed _from, address indexed _to, uint256 indexed _value);`
### Case 2-1, transfer amount was 500;
contract was created trx: https://kovan.etherscan.io/tx/0x26aa7a8bee571e4c1d1835080d06f1250d35048e907d84981b6665e49b956942
contract address: 0xC40A315653A61de3082218fEcB0ec6a2d709Eee9
transfer 500 at token trx: https://kovan.etherscan.io/tx/0xb57e15b739e1d26ff48486e9594afc5f82f7a74a7e44671bf4dfe67baf5f37fe
**the transcation is still ERC20, which means could not reproduced the problem.**

### Case 2-2, transfer amount was 10;
Compred Case 2-1 to the situation when I met the problem first time, I found the transfer amout is 10. Maybe it was the trigger condition?
So I tried case2-2, still using token BT.
however, when the transfer amout is 10 token, the transcation is ERC721, see https://kovan.etherscan.io/tx/0x7a64d877c168f571784593a659fb760f6ea41dc1bb32690d6d208431d02ab5a4.

### Case 3, Token AT, using `event Transfer(address indexed _from, address indexed _to, uint256 _value);`, transfer 10 at token.
Compred case2-2 to case 2-1, I guessed that maybe it was none bussiness of the Transfer() event, the differ maybe it just the transfer.
So I tried case 3, which could be used as compare to Case 1.

transfer 10 AT token, the transcation is ERC20, so it was related to both the `Transfer()` event and `transfer amout`?

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469136022):

Sorry, there was a mistoken. In `case 2-1`, the trx was alse ERC-721, more precisel, when it was in pendding status, it was ERC20, and in confirmed status, it was ERC721.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469208290):

@YannickXiong could you please condense your report a little? What is the observed behaviour of the compiler, what is the expected behaviour of the compiler?

#### <img src="https://avatars.githubusercontent.com/u/27282380?u=c3aa940fa56a7641bbb85c767b743ce4888c6ea7&v=4" width="50">[Skyge](https://github.com/Skyge) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469238266):

@YannickXiong yeah, you can simplify the code by deleting the code you do not use in this example, just left necessary function.

#### <img src="https://avatars.githubusercontent.com/u/27282380?u=c3aa940fa56a7641bbb85c767b743ce4888c6ea7&v=4" width="50">[Skyge](https://github.com/Skyge) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469248931):

@YannickXiong  I find out what you are talking about, [look at here](https://snag.gy/D4mHnO.jpg), yeah, we deployed an ERC20 contract, but it really showed ERC-721 TokenID, I never noticed this before, I do not know what does that mean.

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469271355):

@Skyge Yeah, you got what I said.

@chriseth Sorry, I am a software tester, so I often use the control variable method to do different testing to locate the root cause of the problem, so my previous description may be a bit verbose.


the core code is shown as below in a ERC20 contract,
```
    // code A
    event Transfer(address indexed _from, address indexed _to, uint256 _value);

```
and 
```
    // code B
    event Transfer(address indexed _from, address indexed _to, uint256 indexed _value);
```

Simply put, my question is: using code B, in the same ERC20 smart contract, if the transfer amount is 500 token, it is an ERC20 contract, but when the transfer amount is 10 token, why it was an ERC721 contract (it does not implement the interface of ERC721). 

However, When code A is used, transfer both 500 token andm10 token, it is always ERC20.

My conclusion is that whether it is ERC721 or ERC20 depends on:
- the amount of transfer;
- the transfer event, `    event Transfer(address indexed _from, address indexed _to, uint256 _value);` or `    event Transfer(address indexed _from, address indexed _to, uint256 indexed _value);
`;

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469273518):

@YannickXiong ok, this gets us closer to something, I think :)

There is an open issue about whether or not a parameter is `indexed` in an event is not relevant in inheritance ( #4873 ).

I'm still not fully clear about what your issue is about.

What do you mean by "the contract is an ERC20 contract"? As far as the compiler is concerned, it is an ERC20 contract if and only if it inherits from a contract called `ERC20`. This is a compile-time feature, so I'm not sure I understand the "the amount of transfer" part - because that is a runtime fact.

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469279370):

@chriseth @axic @Skyge 

Sorry, my previous description is not accurate  because of  my poor english.

I writed an erc20 contract, using `event Transfer(address indexed _from, address indexed _to, uint256 indexed _value);`, and deployed it, of course the contract it was always erc20 contract. 

My issue is that, when I transfer some token, in pendding stauts, it was really showed as erc20, 
<img width="951" alt="pending" src="https://user-images.githubusercontent.com/33046810/53740748-d6898580-3ecf-11e9-9c38-808099884eec.png">


while in confirmed status, it was really showed as erc721 tokenid.
<img width="1146" alt="confiremed" src="https://user-images.githubusercontent.com/33046810/53740753-db4e3980-3ecf-11e9-8e8b-b4e1d11e3f90.png">

I just want to know why, mabye it's a bug or not?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469286005):

This seems to be an issue with either etherscan or the design of the token standards. Do you think it is related to the compiler or language?

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469491617):

@chriseth Thanks for your replay. Not sure yet. I searched `etherscan` in github, it seemed that there was no the  related projects. I was also not sure where could I report the issue to?

#### <img src="https://avatars.githubusercontent.com/u/27282380?u=c3aa940fa56a7641bbb85c767b743ce4888c6ea7&v=4" width="50">[Skyge](https://github.com/Skyge) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469512573):

@YannickXiong I did some search and asked for some nice guys, and at last, I think I got it. 
<img width="985" alt="2019-03-05 10 29 30" src="https://user-images.githubusercontent.com/27282380/53776713-cdc99b80-3f31-11e9-9b48-62baf31236ff.png">
<img width="991" alt="2019-03-05 10 30 23" src="https://user-images.githubusercontent.com/27282380/53776747-ee91f100-3f31-11e9-82f6-515a6812cfa0.png">
when you see above, you will find they make a standard such so, so everyone should follow them.So just like @chriseth said, there is a design of the token standards. As for why they do like so, maybe you can find more details on [Solidity docs on Events](https://solidity.readthedocs.io/en/develop/contracts.html#events).

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469521289):

@Skyge got it, thanks very much for your replying.

#### <img src="https://avatars.githubusercontent.com/u/27282380?u=c3aa940fa56a7641bbb85c767b743ce4888c6ea7&v=4" width="50">[Skyge](https://github.com/Skyge) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469534506):

@YannickXiong Do you have any other questions?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469591053):

@YannickXiong you could try here: https://etherscan.io/contactus or https://etherscancom.freshdesk.com/support/solutions

#### <img src="https://avatars.githubusercontent.com/u/33046810?u=5868f1f99c637b05e69388db798ea4a072c4531f&v=4" width="50">[YannickXiong](https://github.com/YannickXiong) commented at [2019-03-01 03:30](https://github.com/ethereum/solidity/issues/6148#issuecomment-469617849):

@chriseth @Skyge Thanks for your replying, I have no questions about this issue. 
Could you close this issue, thanks.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
