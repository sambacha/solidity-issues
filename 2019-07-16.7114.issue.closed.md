# [\#7114 Issue](https://github.com/ethereum/solidity/issues/7114) `closed`: abi.decode() results can occupy the same memory location
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/48803443?u=74b87b2f58d800f365c68b3a60f5beb7efbf7958&v=4" width="50">[dorothy-zbornak](https://github.com/dorothy-zbornak) opened issue at [2019-07-16 21:58](https://github.com/ethereum/solidity/issues/7114):

<!--## Prerequisites

- First, many thanks for taking part in the community. We really appreciate that.
- We realize there is a lot of information requested here. We ask only that you do your best to provide as much information as possible so we can better help you.
- Support questions are better asked in one of the following locations:
	- [Solidity chat](https://gitter.im/ethereum/solidity)
	- [Stack Overflow](https://ethereum.stackexchange.com/)
- Ensure the issue isn't already reported.
- The issue should be reproducible with the latest solidity version; however, this isn't a hard requirement and being reproducible with an older version is sufficient.
-->

## Description

<!--Please shortly describe the bug you have found, and what you expect instead.-->

If we provide `abi.decode(data)` compressed data, e.g., where two encoded dynamic arrays point to the same location, and depending on whether `data` resides in `calldata` or `memory`, you get different behaviors:

- If `data` is in `memory`, the results will share the same memory location.
- If `data` is in `calldata`, the results will *not* share the same memory location.

The latter should be the correct behavior. Otherwise, it is easy to enter a scenario where you are manipulating one array without realizing you are also manipulating the other. This is a potential attack vector.

It should be noted that this behavior is consistent and correct (always the second case) when `pragma experimental ABIEncoderV2` is enabled.

## Environment

- Compiler version: `0.5.10`
- Target EVM version (as per compiler settings): `constantinople`
- Framework/IDE (e.g. Truffle or Remix): Remix
- EVM execution environment / backend / blockchain client: Javascript VM
- Operating system: N/A

## Steps to Reproduce

Plug the following contract into Remix, deploy, and execute `runTests()`:

```solidity
pragma solidity ^0.5;
// Results are consistent with ABIEncoderV2.
// pragma experimental ABIEncoderV2;

contract DecodeTest {
    
    // Run `abiDecodeTestMemory()` and `abiDecodeTestCalldata()`
    function runTests() external view returns (bool memorySameMemory, bool calldataSameMemory) {
        memorySameMemory = abiDecodeTestMemory(encode());
        calldataSameMemory = this.abiDecodeTestCalldata(encode());
    }
    
    // With ABIEncoderV2 off, `arr1` and `arr2` point to the same memory when decoding from memory.
    function abiDecodeTestMemory(bytes memory buf) public pure returns (bool isSameLoc) {
        (uint256[] memory arr1, uint256[] memory arr2) = abi.decode(buf, (uint256[], uint256[]));
        isSameLoc = _isSameLoc(arr1, arr2);
    }
    
    // `arr1` and `arr2` always point to different memory locations when decoding from calldata.
    function abiDecodeTestCalldata(bytes calldata buf) external pure returns (bool isSameLoc) {
        (uint256[] memory arr1, uint256[] memory arr2) = abi.decode(buf, (uint256[], uint256[]));
        isSameLoc = _isSameLoc(arr1, arr2);
    }
    
    // ABI-encode two uint256[] arrays that point to the same location. 
    function encode() public pure returns (bytes memory) {
        bytes memory buf = new bytes(128);
        assembly {
            mstore(add(buf, 32), 64)
            mstore(add(buf, 64), 64)
            mstore(add(buf, 96), 1)
            mstore(add(buf, 128), 0xdeadbeef)
        }
        return buf;
    }
    
    function _isSameLoc(uint256[] memory arr1, uint256[] memory arr2) internal pure returns (bool same) {
        assembly {
            same := eq(arr1, arr2)
        }
    }
}
```

You should see these results, which indicate that the `abi.decode()` in `abiDecodeTestMemory()` returns two arrays that share the same memory location, but not the one in `abiDecodeTestCalldata()`:
```
0: bool: memorySameMemory true
1: bool: calldataSameMemory false
```

#### <img src="https://avatars.githubusercontent.com/u/18060168?u=119fee024861ba272356878a16540a534b61d88a&v=4" width="50">[abandeali1](https://github.com/abandeali1) commented at [2019-07-16 21:58](https://github.com/ethereum/solidity/issues/7114#issuecomment-512043100):

I want to provide an example of a pretty innocent looking function to emphasize the severity of this (this could have easily been code used in production):

```
/// @dev Transfers batch of ERC1155 assets. Either succeeds or throws.
/// @param assetData Byte array encoded with ERC1155 token address, array of ids, array of values, and callback data.
/// @param from Address to transfer assets from.
/// @param to Address to transfer assets to.
/// @param amount Amount that will be multiplied with each element of `assetData.values` to scale the
///        values that will be transferred.
function transferFrom(
    bytes calldata assetData,
    address from,
    address to,
    uint256 amount
)
    external
    onlyAuthorized
{
    // Decode params from `assetData`
    (
        address erc1155TokenAddress,
        uint256[] memory ids,
        uint256[] memory values,
        bytes memory data
    ) = abi.decode(
        assetData.sliceDestructive(4, assetData.length),
        (address, uint256[], uint256[], bytes)
    );

    // Scale values up by `amount`
    uint256 length = values.length;
    for (uint256 i = 0; i != length; i++) {
        values[i] = safeMul(values[i], amount);
    }

    // Execute `safeBatchTransferFrom` call
    // Either succeeds or throws
    IERC1155(erc1155TokenAddress).safeBatchTransferFrom(
        from,
        to,
        ids,
        values,
        data
    );
}
```

Now take an input where `values` and `ids` are both `[1, 2]`. The optimized calldata would look like:

```
// 0x         0000000000000000000000000b1ba0af832d7c05fd64161e0db78e85978e8082      // ERC1155 contract address
// 0x20       0000000000000000000000000000000000000000000000000000000000000080      // Offset to token IDs
// 0x40       0000000000000000000000000000000000000000000000000000000000000080      // Offset to token values (same as IDs)
// 0x60       00000000000000000000000000000000000000000000000000000000000000e0      // Offset to data
// 0x80       0000000000000000000000000000000000000000000000000000000000000002      // Length of token Ids / token values
// 0xA0       0000000000000000000000000000000000000000000000000000000000000001      // First Token ID / Token value
// 0xC0       0000000000000000000000000000000000000000000000000000000000000002      // Second Token ID / Token value
// 0xE0       0000000000000000000000000000000000000000000000000000000000000004      // Length of callback data
// 0x100      0102030400000000000000000000000000000000000000000000000000000000      // Callback data
```

In this case, `values[i] = safeMul(values[i], amount)` also ends up modifying `ids[i]` because they are stored in the same memory location. So if `amount` equals 2, we end up transferring 2 of id 2 and 4 of id 4 instead of 2 of id 1 and 4 of id 2!

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2019-07-16 21:58](https://github.com/ethereum/solidity/issues/7114#issuecomment-512205404):

@dorothy-zbornak I still don't know why the results from `abi.decode` point to the same location, but I'm not sure function `encode` does what the comment says (`// ABI-encode two uint256[] arrays that point to the same location`)

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-07-16 21:58](https://github.com/ethereum/solidity/issues/7114#issuecomment-512207530):

@leonardoalt I think the old decoder does not explicitly copy everything, because it was not meant to be used like that in the beginning.

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2019-07-16 21:58](https://github.com/ethereum/solidity/issues/7114#issuecomment-512207621):

As an example, I changed function `encode` to:
```
uint[] memory buf = new uint[](4);
buf[0] = 0xcafecafe;
uint[] memory buf2 = buf;
return abi.encode(buf, buf2);
```
Even though `buf` and `buf2` point to the same location, the data is duplicated in the resulting bytes.
In this case `runTests` returns
```
0: bool: memorySameMemory false
1: bool: calldataSameMemory false
```

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2019-07-16 21:58](https://github.com/ethereum/solidity/issues/7114#issuecomment-512209167):

Oh ok, my bad, I see it now.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
