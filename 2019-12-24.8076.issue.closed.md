# [\#8076 Issue](https://github.com/ethereum/solidity/issues/8076) `closed`: View function become input function after upgrade 0.5.1 to 0.6.0 ERC20

#### <img src="https://avatars.githubusercontent.com/u/53482087?u=83aa38054bf49bbe09df6bee3794587c0232d109&v=4" width="50">[codev911](https://github.com/codev911) opened issue at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076):

this is my source code after update from 0.5.1 to 0.6.0, i just clear any error appear at 0.6.0 but after deploy i got only input function, and my view function like view token name, view decimals, view supply and many more : 

```
pragma solidity >=0.5.0 <0.7.0;

library SafeMath {
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        // Solidity only automatically asserts when dividing by 0
        require(b > 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "SafeMath: modulo by zero");
    }
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
}

interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function symbol() external view returns (string memory);
    function name() external view returns (string memory);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

contract Context {
    // Empty internal constructor, to prevent people from mistakenly deploying
    // an instance of this contract, which should be used via inheritance.
    constructor () internal { }
    // solhint-disable-previous-line no-empty-blocks

    function _msgSender() internal view returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

abstract contract ApproveAndCallFallBack {
    function receiveApproval(address from, uint256 tokens, address token, bytes memory data) virtual public;
}

contract token is Context, IERC20{
    using SafeMath for uint256;
    address contractOwner;
    
    mapping (address => uint256) private _balances;
    mapping (address => mapping (address => uint256)) private _allowances;
    uint256 private _totalSupply;
    uint256 private _totalDistributed;
    string private _name;
    string private _symbol;
    uint8 private _decimals;

    constructor (string memory name, string memory symbol, uint8 decimals, uint256 totalSupply) public {
       contractOwner = msg.sender;
        _name = name;
        _symbol = symbol;
        _decimals = decimals;
        uint cvdecimal = decimals;
        _totalSupply = totalSupply.mul(10**cvdecimal);
    }
    
    modifier onlyOwner() {
        require(msg.sender == contractOwner);
        _;
    }

    function name() override external view returns (string memory) {
        return _name;
    }
    function symbol() override external view returns (string memory) {
        return _symbol;
    }
    function decimals() external view returns (uint8) {
        return _decimals;
    }
    function totalSupply() override public view returns (uint256) {
        return _totalSupply;
    }
    function totalDistributed() public view returns (uint256) {
        return _totalDistributed;
    }
    
    function balanceOf(address account) public override view returns (uint256) {
        return _balances[account];
    }
    function transfer(address recipient, uint256 amount) public override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }
    function allowance(address owner, address spender) public override view returns (uint256) {
        return _allowances[owner][spender];
    }
    function approve(address spender, uint256 amount) public override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }
    function transferFrom(address sender, address recipient, uint256 amount) public override returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(sender, _msgSender(), _allowances[sender][_msgSender()].sub(amount, "ERC20: transfer amount exceeds allowance"));
        return true;
    }
    function increaseAllowance(address spender, uint256 addedValue) public returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].add(addedValue));
        return true;
    }
    function decreaseAllowance(address spender, uint256 subtractedValue) public returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender].sub(subtractedValue, "ERC20: decreased allowance below zero"));
        return true;
    }

    function _transfer(address sender, address recipient, uint256 amount) internal {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _balances[sender] = _balances[sender].sub(amount, "ERC20: transfer amount exceeds balance");
        _balances[recipient] = _balances[recipient].add(amount);
        emit Transfer(sender, recipient, amount);
    }
    function _mint(address account, uint256 amount) internal {
        require(account != address(0), "ERC20: mint to the zero address");
        uint256 counter = _totalDistributed + amount;
        if(_totalDistributed <= _totalSupply && counter <= _totalSupply){
            _totalDistributed = _totalDistributed.add(amount);
            _balances[account] = _balances[account].add(amount);
            emit Transfer(address(0), account, amount);
        }else{
            revert();
        }
    }
    function _burn(address account, uint256 amount) internal {
        require(account != address(0), "ERC20: burn from the zero address");

        _balances[account] = _balances[account].sub(amount, "ERC20: burn amount exceeds balance");
        _totalSupply = _totalSupply.sub(amount);
        emit Transfer(account, address(0), amount);
    }
    function _approve(address owner, address spender, uint256 amount) internal {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }
    function approveAndCall(address spender, uint tokens, bytes memory data) public returns (bool success) {
        _allowances[msg.sender][spender] = tokens;
        _approve(msg.sender, spender, tokens);
        ApproveAndCallFallBack(spender).receiveApproval(msg.sender, tokens, address(this), data);
        return true;
    }
    function mint(address account, uint256 amount) public onlyOwner returns (bool) {
        _mint(account, amount);
        return true;
    }
}
```

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-568814348):

Can you please mention the tools you are using?

I believe this is a duplicate of #8065, which means it is not a problem in the compiler, but the tooling.

#### <img src="https://avatars.githubusercontent.com/u/53482087?u=83aa38054bf49bbe09df6bee3794587c0232d109&v=4" width="50">[codev911](https://github.com/codev911) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-568847473):

i just deploy at remix.ethereum.org

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-568921509):

@LianaHus is this fixed/deployed on Remix yet?

#### <img src="https://avatars.githubusercontent.com/u/13217082?u=bfc4794fbb23812200b4f6b584e008bddd7defd3&v=4" width="50">[BogdanVidrean](https://github.com/BogdanVidrean) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-569528765):

I also reproduced this problem after upgrading from 0.5.15 to 0.6.0. I used Remix IDE and now all my view functions initiate a transaction (they are not readonly anymore). I tested with a local node and also with the JavaScriptVM and it maintains the same behaviour. In order to reproduce create a new contract that contains a view function, and test it with the latest release of 0.15 and then switch to 0.6.0 and see that it creates a new transaction.

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-569544216):

Please note this is not a bug in the compiler. Those hints in the ABI JSON are nothing more, but hints. Tooling can decide how to interpret them.

The problem here is that tools seemingly didn't update since 2017, when the new fields were introduced.

#### <img src="https://avatars.githubusercontent.com/u/13217082?u=bfc4794fbb23812200b4f6b584e008bddd7defd3&v=4" width="50">[BogdanVidrean](https://github.com/BogdanVidrean) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-569734254):

Yeah, I was thinking at the exact same scenario. Most probably Remix uses those hints from the ABI JSON in order to determine whether a function is view, pure or it requires a transaction. Currently I also discovered this problem with Web3j's java class wrapper generation. They are also not up to date.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-574279170):

Closing this as out of scope. Please re-open if in error.

#### <img src="https://avatars.githubusercontent.com/u/9685356?u=7b16da115638a6b4dea66b3ea41a69106eaae630&v=4" width="50">[LianaHus](https://github.com/LianaHus) commented at [2019-12-24 13:55](https://github.com/ethereum/solidity/issues/8076#issuecomment-574648538):

> @LianaHus is this fixed/deployed on Remix yet?

this is fixed in remix-alpha. will be released soon


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
