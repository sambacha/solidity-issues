# [\#8141 Issue](https://github.com/ethereum/solidity/issues/8141) `closed`: False positive requirement for override?

#### <img src="https://avatars.githubusercontent.com/u/2530770?u=a2b81f85d207864b7db06415db53010c21633b33&v=4" width="50">[nventuro](https://github.com/nventuro) opened issue at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141):

I'm facing an issue which doesn't seem to be covered in the [inheritance documentation](https://solidity.readthedocs.io/en/v0.6.1/contracts.html#inheritance).

Consider the following scenario:

```solidity
pragma solidity ^0.6.0;

contract GrandParent {
    uint256 internal a;
    
    function foo() virtual public {
        a = 3;
    }
}

contract Parent is GrandParent {
    function foo() override public {
        a = 2;
    }
}

contract Child is Parent { }
```

This compiles with no errors. However, if we perform the following change:

```diff
-contract Child is Parent { }
+contract Child is Parent, GrandParent { }
```

The following compiler error is emitted:
```
TypeError: Derived contract must override function "foo". Two or more base classes define function with same name and parameter types.
contract Child is GrandParent, Parent {
^ (Relevant source part starts here and spans across multiple lines).
Definition in "GrandParent": 
    function foo() virtual public {
    ^ (Relevant source part starts here and spans across multiple lines).
Definition in "Parent": 
    function foo() override public {
    ^ (Relevant source part starts here and spans across multiple lines).
```

My general understanding is as follows. If a contract inherits from multiple sources that all declare the same virtual function, it must then _explicitly_ do an override and select what implementation it wants for it. This used to happen automatically in 0.5.x, with the final implementation being the one of the last contract in the linearized inheritance graph (indeed the code compiles fine under 0.5.16), but with explicit overrides in 0.6.x this behavior is no longer allowed.

However, the example shown above seems to be an edge case that should be allowed: the function _has_ been explicitly overridden: `Parent`'s `foo` is actually `GrandParent`'s `foo`, not a different one.

In particular, this causes issues when using the following pattern (which I've used extensively and found very useful):

```solidity
contract A { } // define some base behavior

contract B is A { } // use A's behavior

contract C is A { } // modify A's behavior via overrides

contract D is B, C {} // use B, with A's behavior modified by C
```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-574601811):

Thanks for taking a deep look into the new override mechanisms! First, I want to assure you that we have discussed this and other edge cases at length during the design phase :)

The crucial point here is that while `GrandParent`'s `foo` has been overridden by `Parent`'s `foo`, you are adding an additional inheritance path by explicitly inheritnig from `GrantParent` again. I think it is important to make this distinction. It might be too easy to introduce errors: If the someone explicitly inherits from `GrandParent` again, they might not have noticed that this is already a base through `Parent`.

This is also covered in the documentation:

"... you have to specify all base contracts that define the same function and have not yet been overridden by another base contract (on some path through the inheritance graph)"

Here again, "path through the inheritance graph" is the important wording as opposed to the linearized inheritance list.

You are saying "Parent's foo is actually GrandParent's foo, not a different on". While this might be obivous to you in this simple example, the fact that we inherit again from GrandParent explicitly might suggest that this is not obvious to the author of a more complex contract.

Concerning your pattern: Do you want to signal that `D` is a `B` to people who do not know that `C` is also a `B`? Can you give a more specific example?

#### <img src="https://avatars.githubusercontent.com/u/2530770?u=a2b81f85d207864b7db06415db53010c21633b33&v=4" width="50">[nventuro](https://github.com/nventuro) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-574683258):

>Concerning your pattern: Do you want to signal that D is a B to people who do not know that C is also a B? Can you give a more specific example?

Note that in the pattern C is not B, C is A.

A simple use case is testing, where a base contract (let's call it `Time`) provides wrappers for `block.timestamp` and `block.number`, and all contracts in the system use these wrappers instead of the native global variables. During testing, a second contract (`MockedTime`) overrides these getters for storage reads, and provides methods to change the value the getters return, letting you simulate time passing on the EVM. To test a contract, you create a mock contract that inherits from it and `MockedTime`.

Now, adding the explicit override might not be too bad for testing, but we also use it for a core feature of the [OpenZeppelin Contracts](https://github.com/OpenZeppelin/openzeppelin-contracts/): our [meta-transaction solution](https://docs.openzeppelin.com/contracts/2.x/gsn#receiving_a_relayed_call).

In meta-txs, the account that originates the transaction is not actually `msg.sender` but an account recovered from a signature. When writing a library however, it makes sense to have this complex sender recovery be opt-in: not every developer wants to use meta-transactions and this feature costs gas.

What we did was make every contract in the library inherit from a contract called [`Context`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/GSN/Context.sol#L19), which provides a trivial `msgSender()` function. All contracts call `msgSender()` instead of accessing `msg.sender`.

Meta-tx support (we call this the [GSN](https://docs.openzeppelin.com/learn/sending-gasless-transactions)) can then be enabled by inheriting from [`GSNRecipient`](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/GSN/GSNRecipient.sol#L88), which will override `msgSender()` in favor of this more complex behavior, both in the user's contracts _and_ in any other inherited contracts:


```solidity
contract Context {
    function msgSender() internal view virtual returns(address) { 
         return msg.sender; 
    }
}

contract ERC20 is Context {
    function transfer(address recipient, uint256 amount) public { 
        _transfer(msgSender(), recipient, amount);
    }
}

contract GSNRecipient is Context {
    function msgSender() internal view override returns(address) { 
        return computeGSNSender(); 
    }
}

contract MyToken is ERC20, GSNRecipient {
    // The ERC20 behavior is patched and now supports meta-txs
    ...
}
```

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-574867821):

Hm... this is quite an interesting use case... I'm not sure, if it's an option for you, but I find it noteworthy that the following alternative construction would be valid against our defined rules:

```
interface Context { // or "abstract contract Context"
    function msgSender() internal view returns(address);
}

contract ERC20 is Context {
    function transfer(address recipient, uint256 amount) public { 
        _transfer(msgSender(), recipient, amount);
    }
}

contract GSNRecipient is Context {
    function msgSender() internal view override returns(address) { 
        return computeGSNSender(); 
    }
}

contract RegularRecipient is Context {
    function msgSender() internal view override returns(address) { 
        return msg.sender; 
    }
}

contract MyToken is ERC20, GSNRecipient {
    // ...
}

contract MySecondToken is ERC20, RegularRecipient {
    // ...
}
```

There's not much room between "too strict" and "not strict enough" here - I'm not sure on which side this case is...

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-577859202):

@nventuro do you want to have a call about this?

#### <img src="https://avatars.githubusercontent.com/u/2530770?u=a2b81f85d207864b7db06415db53010c21633b33&v=4" width="50">[nventuro](https://github.com/nventuro) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-577864374):

I'd love to! We're discussing how to best handle migrating the OpenZeppelin Contracts library to 0.6 in https://github.com/OpenZeppelin/openzeppelin-contracts/pull/2063, and a key consideration is how to reduce the number of errors on diamond inheritance scenarios, and how to instruct users on what needs to be done when this happens.

I have a lot of availability these days, so just send one or two invites my way and we'll work from there.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-579342795):

We could not really find a solution for this during the call. The agreement was that we will be looking for other methods to share code apart from inheritance, since inheritance usually assumes an "is a" relationship, which is not always the case. Contract templates might be a solution here.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-581304449):

I'm not sure if you discussed and disregarded the idea during your call, but one solution I could imagine that could solve this issue while still being sufficiently explicit (at least as far as I'm concerned) would be to allow contract-level overriding, as in:
```
contract MySecondToken is ERC20, GSNRecipient override(Context) {
}
```
The effect of which could be defined as being equivalent to explicitly mentioning and overriding all overridable functions in ``Context`` while delegating to their implementations in ``GSNRecipient``. This would require ``GSNRecipient`` to implement *all* overridable functions in ``Context``. We could extend it further as ``override(Context in ERC20)``, i.e. require mentioning the direct base(s) containing the overridden functions. This is not perfect syntax-wise, but conceptually I wouldn't see a problem with it...

EDIT: it could also be done on a per-function basis like ``contract C is ERC20, GSNRecipient override (ERC20.msgSender)``

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-594120124):

What is the exact reason to have such powerful restrictions for `override` keyword? I am afraid it is over-protection for developers also breaking existing Solidity best practices and programming principles. I was pretty sure that language evolution should go hand in hand with leading libraries/solutions build using this language. Did you eve heard that a new C++ standard broke STL approach?

Maybe let's try first to figure out what exactly are cases we wish to avoid and make more explicit? What errors we are trying to prevent with the new syntax.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-594394574):

I'm not sure what "powerful restrictions for the ``override`` keyword" you are talking about. I'm also not sure what "best practices" the 0.6 changes are supposed to have broken.

The case for which we decided to require explicitness is the case where a contract inherits from multiple contracts that contain the same function, e.g. 
```
contract A { function f() public virtual {} }
contract B { function f() public virtual {} }
contract C is A, B { }
```
When calling ``C.f`` it is not immediately apparent, if it calls ``A.f`` or ``B.f``. Even if you are familiar with the C3-linearization and immediately assume it is ``B.f``, it can be worse: imagine ``B`` does not contain a function ``f`` initially and consequently you expect ``C.f`` to call ``A.f`` - but then you add a function ``f`` to ``B``, which silently changes what ``C.f`` does.
Note that in the initial proposals for reworking inheritance, it was even planned to disallow inheriting the same function from different contracts entirely. In order *not* to break things too much, we *do* still allow this, but require to make it explicit in ``C`` which base function you want (by requiring you to override and delegate).
In the scenario above, when you add ``f`` to ``B``, then ``C`` would fail to compile until you add an explicit override, preventing a silent change in behavior.

Now imagine ``C`` not only implicitly inherits ``f``, but overrides it.
```
contract A { function f() public virtual {} }
contract B { }
contract C is A, B { function f() public override {} }
```
If you now add a function ``f`` to ``B`` and ``B`` relies on it being properly implemented with respect to some requirements, this can easily result in the behavior of ``B`` to be broken in ``C``, since ``C`` so far only had to implement ``f`` to satisfy ``A``. Hence you need to explicitly vouch for your implementation of ``f`` in ``C`` to meet the requirements of ``A.f`` and of ``B.f``, which again prevents things from silently breaking.

In general, none of the stricter requirements for inheritance were introduced out of the blue - and there are no cases, in which something that could be done prior to 0.6 cannot still be done after 0.6, if you make explicit what you are doing.

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-594490145):

I believe this should produce a compilation error in the "new safer version of Solidity":
```
contract A { function f() public {} }
contract B { function f() public {} }
contract C is A, B { }
```

**Just allow `override` only `virtual` methods.** â€“ This would not break the existing things.

Trying to define _explicit overriding_ over _implicit linear hierarchy_ breaks, for example, ERC20 family of contracts in OZ library, which design was pretty convenient and safe.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-594500058):

Actually, I did forget the ``virtual`` keywords in my previous post. We *do* require a function to be ``virtual`` in order to override it.
I guess you're idea would be that:
```
contract A { function f() public {} }
contract B { function f() public {} }
contract C is A, B { }
```
should be an error due to ``A.f`` not being virtual and ``B.f`` overriding it. But in
```
contract A { function f() public virtual {} }
contract B { function f() public [virtual] {} }
contract C is A, B { }
```
The problems I tried to describe above still exist. Also you can't mark ``B.f`` override, since without the context in ``C``, it doesn't - however in ``C`` it then does.

I see the issue with the ERC20 family of contracts in OZ, but moving back to just allowing ambiguous inheritance that relies on the linearization is not a good way to solve the issue. What could be considered is allowing some kind of contract-level overriding like https://github.com/ethereum/solidity/issues/8141#issuecomment-581304449 in the future in solidity, if we can come up with a nice specification for that - or to employ a solution like https://github.com/ethereum/solidity/issues/8141#issuecomment-574867821 in OZ, which is perfectly possible.

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-594511730):

The following example should also fail:
```
contract A { function f() public virtual {} }
contract B { function f() public virtual {} }
contract C is A, B { }
```
Since both virtual methods are not abstract. To make it work you should derive both from some common ansector with `virtual` function `f()` to make sure it's the same API, for example we should be able to specify new implementation:
```
contract Z { function f() public virtual {} }
contract A is Z { function f() public override {} }
contract B is Z { function f() public override {} }
contract C is A, B { function f() public override { A.f(); B.f(); } }
```

Maybe it's better to decide in runtime, than on compile time?
This would allow things like this:
```
contract C is A, B {
    function f() public override returns(uint256) { return A.f() + B.f(); }
}
```

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-594525439):

Well...
```
contract A { function f() public virtual {} }
contract B { function f() public virtual {} }
contract C is A, B { }
```
currently *does* fail.

And the following *is* allowed:
```
contract Z { function f() public virtual {} }
contract A is Z { function f() public override virtual {} }
contract B is Z { function f() public override virtual {} }
contract C is A, B { function f() public override(A, B) { A.f(); B.f(); } }
```
You just have to make explicit that you're not missing a base, which I'd consider a minor inconvenience compared to the benefit. Having a common base contract/interface makes it *more likely* that nothing will go wrong, but it doesn't prevent it (even though both ``A``s ``f`` and ``B``s ``f`` implement ``Z.f``, they might still impose different requirements on their own ``f``s - after all they have overridden it).

Not sure what you mean by "decide in runtime". Runtime-type-information are generally infeasible due to their overhead, but I'm not sure how this relates to the issue.

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2020-01-15 00:41](https://github.com/ethereum/solidity/issues/8141#issuecomment-594727506):

@ekpyron I will return with a better description :)


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
