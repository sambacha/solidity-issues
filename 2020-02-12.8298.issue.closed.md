# [\#8298 Issue](https://github.com/ethereum/solidity/issues/8298) `closed`: abi.encode does not handle address arrays

#### <img src="https://avatars.githubusercontent.com/u/15959632?u=c2f8e2cf7f1bdb50cf51fa93132523b556708ca8&v=4" width="50">[thegostep](https://github.com/thegostep) opened issue at [2020-02-12 01:16](https://github.com/ethereum/solidity/issues/8298):

## Description

`abi.encode()`, `abi.encodeWithSelector`, and `abi.encodeWithSignature` perform incorrect encoding of address arrays. 

Expected behavior is to perform abi encoding of the array. Current behavior is to concatenate each item of the array.

## Environment

- Compiler version: 0.5.16
- Framework/IDE (e.g. Truffle or Remix): Remix

## Steps to Reproduce

```solidity
pragma solidity >=0.5.0 <0.6.0;

contract Temp {
    
    function init(address[] calldata list) external { }
    
    function getEncoding1() public view returns (bytes memory initCalldata) {
        Temp temp;
        bytes4 sig = temp.init.selector;
        return abi.encodeWithSelector(sig, [address(this)]);
    }
    
    
    function getEncoding2() public view returns (bytes memory initCalldata) {
        string memory sig = 'init(address[])';
        return abi.encodeWithSignature(sig, [[address(this)]]);
    }
    
    
    function getEncoding3() public view returns (bytes memory initCalldata) {
        return abi.encode([[address(this)]]);
    }
}
```


#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-12 01:16](https://github.com/ethereum/solidity/issues/8298#issuecomment-585129401):

`abi.encode` performs abi-encoding, not rlp encoding. Can you explain what encoding you would expect and how the compiler performs it for your example?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-12 01:16](https://github.com/ethereum/solidity/issues/8298#issuecomment-585287605):

From twitter I reckon that this is specific to inline arrays.

#### <img src="https://avatars.githubusercontent.com/u/15959632?u=c2f8e2cf7f1bdb50cf51fa93132523b556708ca8&v=4" width="50">[thegostep](https://github.com/thegostep) commented at [2020-02-12 01:16](https://github.com/ethereum/solidity/issues/8298#issuecomment-585353604):

I expect this to work. Added more expected cases @chriseth. You are right - I meant abi encoding

```solidity
pragma solidity >=0.5.0 <0.6.0;

contract Init {
    
    function init_array(address[] calldata list) external pure returns (bool) { 
        return true;
    }
    
    function init_single(address list) external pure returns (bool) { 
        return true;
    }
    
    function call_array_selector() public view returns (bytes memory initCalldata) {
        Init temp;
        bytes4 sig = temp.init_array.selector;
        return abi.encodeWithSelector(sig, [address(this)]);
    }
    
    function call_array_signature() public view returns (bytes memory initCalldata) {
        string memory sig = 'init_array(address[])';
        return abi.encodeWithSignature(sig, [address(this)]);
    }
    
    function call_single_selector() public view returns (bytes memory initCalldata) {
        Init temp;
        bytes4 sig = temp.init_single.selector;
        return abi.encodeWithSelector(sig, address(this));
    }
    
    function call_single_signature() public view returns (bytes memory initCalldata) {
        string memory sig = 'init_single(address)';
        return abi.encodeWithSignature(sig, address(this));
    }
    
    function call_single_selector_fail() public view returns (bytes memory initCalldata) {
        Init temp;
        bytes4 sig = temp.init_single.selector;
        return abi.encodeWithSelector(sig, [address(this)]);
    }
    
    function call_single_signature_fail() public view returns (bytes memory initCalldata) {
        string memory sig = 'init_single(address)';
        return abi.encodeWithSignature(sig, [address(this)]);
    }
}

contract Test is Init {
    
    function runTest() public view returns (bool[6] memory res) {
        // expect to succeed
        (res[0],) = address(this).staticcall(Init.call_array_selector());
        (res[1],) = address(this).staticcall(Init.call_array_signature());
        (res[2],) = address(this).staticcall(Init.call_single_selector());
        (res[3],) = address(this).staticcall(Init.call_single_signature());
        // expect to fail
        (res[4],) = address(this).staticcall(Init.call_single_selector_fail());
        (res[5],) = address(this).staticcall(Init.call_single_signature_fail());
    }
}
```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-12 01:16](https://github.com/ethereum/solidity/issues/8298#issuecomment-585705585):

@thegostep I think the problem is that the type of an inline array is a statically-sized array, but you are passing the encoded data to a decoder that expects a dynamically-sized array. If you want to encode a dynamicalli-sized array, you have to create a temporary memory array.

#### <img src="https://avatars.githubusercontent.com/u/15959632?u=c2f8e2cf7f1bdb50cf51fa93132523b556708ca8&v=4" width="50">[thegostep](https://github.com/thegostep) commented at [2020-02-12 01:16](https://github.com/ethereum/solidity/issues/8298#issuecomment-585735529):

> @thegostep I think the problem is that the type of an inline array is a statically-sized array, but you are passing the encoded data to a decoder that expects a dynamically-sized array. If you want to encode a dynamicalli-sized array, you have to create a temporary memory array.

Ah - spot on. The temporary array does the trick

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-12 01:16](https://github.com/ethereum/solidity/issues/8298#issuecomment-586211146):

I'm closing this. Please re-open if you think a change to the language or compiler is needed here.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
