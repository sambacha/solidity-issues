# [\#8354 Issue](https://github.com/ethereum/solidity/issues/8354) `open`: Arbitrary order of contracts in override specifier is confusing.
**Labels**: `breaking change :warning:`


#### <img src="https://avatars.githubusercontent.com/u/10974422?u=1e765626628dfa68f54edd04aa43dfe2a8d47e1a&v=4" width="50">[apguerrera](https://github.com/apguerrera) opened issue at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354):

Version: solidity ^0.6.3
Platform: http://remix.ethereum.org/#optimize=false&evmVersion=null&version=soljson-v0.6.3+commit.8dda9521.js

Issue: override(Two, One) doesn't seam to have the intended behavior. Instead, it retains the contract import inheritance.

Example: 

```
pragma solidity ^0.6.3;

contract One {
    uint256 public a;
    event Hah(string message, uint a, uint b);
    function foo() virtual public {
        a = 1;
        emit Hah("One::foo()", a, 0);
    }
}

contract Two {
    uint256 public b;
    event Hah(string message, uint a, uint b); 
    function foo() virtual public {
        b = 2;
        emit Hah("Two::foo()", 0, b);
    }
}

// Override contract One
contract OneAndTwo is One, Two {
    event Hah(string message, uint a, uint b);
    function foo() public override(One, Two) {
        super.foo();
        emit Hah("OneAndTwo::foo() after super.foo()", a, b);
    }
}
// Calls Two.foo()

// Swap override contracts
contract OneAndTwoReversed is One, Two {
    event Hah(string message, uint a, uint b);
    function foo() public override(Two, One) {        
        super.foo();
        emit Hah("OneAndTwoReversed::foo() after super.foo()", a, b); 
    }
}
// Still calls Two.foo()


// Swap import contracts
contract TwoAndOne is Two, One {
    event Hah(string message, uint a, uint b);
    function foo() public override(Two, One) {
        super.foo();
        emit Hah("TwoAndOne::foo() after super.foo()", a, b);
    }
}
// Now calls One.foo()
```

Thanks to BokkyPooBah for workshopping this and providing the code examples

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-589086419):

The override specifier has no semantic influence, it is a purely syntactic "clarifier". The order of contracts in the override specifier is arbitrary. Maybe we should require that the order is the same as the one deduced using linearization.

#### <img src="https://avatars.githubusercontent.com/u/10974422?u=1e765626628dfa68f54edd04aa43dfe2a8d47e1a&v=4" width="50">[apguerrera](https://github.com/apguerrera) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-591157800):

Shouldn't specifying the order in the override have an impact on super. is called in individual functions?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-591450835):

@apguerrera the specific meaning of `super` does not only depend the contract, this keyword is used in. It depends on the full inheritance hierarchy, including contracts that the current contract is not even aware of.

#### <img src="https://avatars.githubusercontent.com/u/10974422?u=1e765626628dfa68f54edd04aa43dfe2a8d47e1a&v=4" width="50">[apguerrera](https://github.com/apguerrera) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-591888368):

Could override(One,Two) indicate your preferred ordering within the hierarchy? 
Otherwise, if it is just arbitrary, why not simply leave it as override.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-591958444):

It is not about specifying an order but about specifying which contracts you inherit from also define that function. Since the order of inheritance cannot be changed on function basis, you specify it when specifying the bases: `contarct c is One, Two`

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-594000350):

What is the exact reason to add types into `override` keyword? I am afraid it is over-protection for developers also breaking existing Solidity best practices and programming principles. I was pretty sure that language evolution should go hand in hand with leading libraries/solutions build using this language. Did you eve heard that a new C++ standard broke STL approach?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-648768995):

The main problem with forcing an order to me is that it is still unclear if that order would be "base to derived" or "derived to base".

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-648824966):

@chriseth Why do we require specifying all the bases? I think the purpose of this feature is to make sure user knows which function gets overridden. Just specifying the base contract containing that function seems enough to achieve that goal. So it could work like this:

```solidity
// Good.
contract OneAndTwo is One, Two { function foo() public override(Two) { super.foo(); } }
contract TwoAndOne is Two, One { function foo() public override(One) { super.foo(); } }
```

```solidity
// Error. foo() does not override the function from the specified base.
contract OneAndTwo is One, Two { function foo() public override(One) { super.foo(); } }
contract TwoAndOne is Two, One { function foo() public override(Two) { super.foo(); } }
```

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-648836251):

~If we do want to keep the requirement to specify all the bases we could also make the order clear by requiring the user to mark the most derived one - since that's the only one whose placement could theoretically matter (unless the user expects to be able to reorder all the bases in the inheritance chain with `override()` but that would be ridiculous).~

~Some syntax ideas:~
```solidity
contract X is A, B, C, D { function foo() public override(A | D, B, C) { super.foo(); } }
```
``` solidity
contract X is A, B, C, D { function foo() public override(A -> (D, B, C)) { super.foo(); } }
```
``` solidity
contract X is A, B, C, D { function foo() public override(D, B, A*, C) { super.foo(); } }
```
``` solidity
contract X is A, B, C, D { function foo() public override(D, B, {A}, C) { super.foo(); } }
```
``` solidity
contract X is A, B, C, D { function foo() public override(D, B, A, C) through A { super.foo(); } }
```
``` solidity
contract X is A, B, C, D { function foo() public override(A) override(D, B, C) { super.foo(); } }
```

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-648939269):

Okay, so the reason why we want the user to see all the bases is the diamond inheritance. My idea would work fine for the example listed in the description of this issue but the case we're worried about is `A is B, C` + `C is E` + `D is E`. If we only list `override(B)` when defining `A.foo()`, user might not be aware that when there is a diamond in the hierarchy, `C.foo()` will get called as well. It's really sneaky because you can change whether it's called or not without changing anything in `A`. Modifying `C` not to inherit from `E` (both of which may be defined in a completely different file) is enough to disable the call to `C.foo()`. Cases like this make it harder to spot problems when auditing code.

#### <img src="https://avatars.githubusercontent.com/u/10974422?u=1e765626628dfa68f54edd04aa43dfe2a8d47e1a&v=4" width="50">[apguerrera](https://github.com/apguerrera) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-649006898):

If the contract order is arbitrary, it shouldn't need to be specified.  

// Good.
contract OneAndTwo is One, Two { function foo() public override { super.foo(); } }
contract TwoAndOne is Two, One { function foo() public override { super.foo(); } }

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-649545907):

Most of the comments on this issue are about something totally different than the order of contract names in the override specifier.

If the override specifier requires multiple contract names, then they have to be specified, in any order. They have to be specified to avoid situation where people accidentally override functions they did not mean to override:

```
contract A { function f() public virtual {} }
contract B { function f() public virtual {} }
contract C is A, B {
  // oops, I did not know that contract B also defines a function called `f`
  function f() public override {}
}
```

#### <img src="https://avatars.githubusercontent.com/u/10974422?u=1e765626628dfa68f54edd04aa43dfe2a8d47e1a&v=4" width="50">[apguerrera](https://github.com/apguerrera) commented at [2020-02-20 04:08](https://github.com/ethereum/solidity/issues/8354#issuecomment-649603103):

I would prefer that as it does not break contract composibility.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
