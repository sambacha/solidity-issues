# [\#9182 Issue](https://github.com/ethereum/solidity/issues/9182) `closed`: Unexpected values from abi.decode calldata bytes

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) opened issue at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182):

## Description

I get some unexpected values from `abi.decode(bytes calldata, (address,uint256))`.

Well maybe I should expect them and I just don't know. Maybe it has to do with the fact that `abi.decode` only works with abi encoded formats and `calldata` `bytes` are not in such format.

```solidity
function foo(bytes calldata _actionData, bytes calldata _inFlowData)
```

This seems to only affect the second `bytes calldata _inFlowData` param and not the first `bytes calldata _actionData` param.

Also the values that get decoded for `_inFlowData` 

```
sendToken:  0x0000000000000000000000000000000000000020
sendAmount:  64
```

look like they might be the `length` and maybe an offset for the `bytes calldata _inFlowData`.

As you can see from the various outputs I have included below, this clearly has something to do with the way the two `bytes calldata` params are formatted. They look different.

One notable difference between them.  `_actionData` is encoded off-chain with `ethers.js` abi-encoding with function selector. `_inFlowData` I get from another contract that emits it as `abi.encode(sendToken, sendAmount)` (address,uint).

## What I expect is the source of the issue

The source of the issue is probably this function invocation on 
```solidity
contract ConsumesData
```

```solidity
function foo(bytes calldata _offChainData, bytes calldata _contractEmitsData)
```

It expects 
```solidity
bytes calldata _contractEmitsData
```

However, during the call batching in a single tx, it actually get's `data` that we stored in `memory` from a previous call to `EmitsData.emitData()`.

So the formatting might be off.

However, since `ConsumesData.foo()` is invoked with another `low-level` `delegatecall`, I would have expected the `emittedData` we stored in `memory` to be placed in `calldata` for this second `delegatecall` ? But here my solidity knowledge is out of depth.


## Environment

- Compiler version:  solc-js 0.6.9
- Framework/IDE: `@nomiclabs/buidler`
- EVM execution environment / backend / blockchain client: buidlerevm

## Steps to Reproduce

This will be tricky because in our process we use a Smart Contract Wallet that batches calls.



But here are some rough guidelines.

1. Install `buidler` if you want the console.log output

2. Call `Batcher.delegatecallEmitsData` which `delegatecalls` `EmitsData.emitData()` first, then saves the emitted data to bytes memory, and then makes the next `delegatecall` to  ConsumesData.foo() with the off-chain encoded bytes `_offchainData` that can be any bytes array (that has an `address` at position `_offChainData[68:100]` and is encoded with any `function selector`) and with  `_contractEmitsData` that we stored in memory from the previous atomic `delegatecall` to `EmitsData.emitData()`. 

3. Look at the console.log output and see that `_actionData` was `decoded` as expected as `address destination` but `_inFlowData` wasn't decoded as `(address sendToken, uint256 sendAmount`).

```solidity
// "SPDX-License-Identifier: UNLICENSED"
pragma solidity ^0.6.9;

contract Batcher {
    // I tried to slim our actual code down here so might have overlooked something
     function delegatecallEmitsDataAndDelegatecallConsumesDataWithTheEmittedData(
          address _emitsData, 
          address _consumesData,
          bytes calldata _offChainData
     ) 
          external 
    {
           (bool success, bytes memory emittedData) = _emitsData.delegatecall();
           if (!success) revert();
           (bool success,) = _consumesData.delegatecall(_offChainData, emittedData);
           if (!success) revert();
    }
}

contract EmitsData {
      function emitData() external returns(bytes memory) {
          address sendToken = 0x0078371bdede8aac7debfff451b74c5edb385af7;
          uint256 sendAmount = 10000000000000000000;
          return abi.encode(sendToken, sendAmount);   
      }
}

contract ConsumesData {
    function foo(bytes calldata _offChainData, bytes calldata _contractEmitsData)
        external
        payable
    {
        // calldata decoding of first bytes: works
        console.log("ConsumesData.foo");
        console.log("_offChainData: calldata");
        console.logBytes(_offChainData);
        address destination = abi.decode(_offChainData[68:100], (address));
        console.log("destination", destination);  // this is as expected
        
        // calldata decoding of second bytes: does not work
        console.log("_contractEmitsData: calldata");
        console.logBytes(_contractEmitsData);
        (address sendToken, uint256 sendAmount) = abi.decode(_contractEmitsData, (address,uint256));
        console.log("sendToken: ", sendToken);  // this is unexpected
        console.log("sendAmount: ", sendAmount);  // this is unexpected
    }

}
```

This should be the console.log output:

```
ConsumesData.foo
_offChainData: calldata
0x42929d440000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000000000000000000000000000000000000000000000c783df8a850f42e7f7e57013759c285caa701eb6
destination 0xc783df8a850f42e7f7e57013759c285caa701eb6

_contractEmitsData: calldata
0x000000000000000000000000000000000000000000000000000000000000002000000000000000000000000000000000000000000000000000000000000000400000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000
sendToken:  0x0000000000000000000000000000000000000020
sendAmount:  64
```

However, when I decode the `calldata` `bytes` as `memory` `bytes` first and then decode the two values, it works!

```solidity
bytes memory contractEmitsData = abi.decode(contractEmitsData, (bytes));
```

Try this:

```solidity
// "SPDX-License-Identifier: UNLICENSED"
pragma solidity ^0.6.9;

contract ConsumesData {
    function foo(bytes calldata _offChainData, bytes calldata _contractEmitsData)
        external
        payable
    {
        // calldata decoding of first bytes: works
        console.log("ConsumesData.foo");
        console.log("_offChainData: calldata");
        console.logBytes(_offChainData);
        address destination = abi.decode(_offChainData[68:100], (address));
        console.log("destination", destination);  // this is as expected

        // calldata decoding of second bytes: does not work
        console.log("_contractEmitsData: calldata");
        console.logBytes(_contractEmitsData);
        (address sendToken, uint256 sendAmount) = abi.decode(_contractEmitsData, (address,uint256));
        console.log("sendToken: ", sendToken);  // this is unexpected
        console.log("sendAmount: ", sendAmount);  // this is unexpected

        // memory decoding of second bytes: works
        bytes memory contractEmitsData = abi.decode(_contractEmitsData, (bytes));
        console.log("contractEmitsData: memory");
        console.logBytes(contractEmitsData);
        (sendToken, sendAmount) = abi.decode(contractEmitsData, (address,uint256));
        console.log("sendToken: ", sendToken);  // as expected 
        console.log("sendAmount: ", sendAmount);  // as expected
    }
}
```

My output:

```
ConsumesData.foo
_offChainData
0x42929d440000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000000000000000000000000000000000000000000000c783df8a850f42e7f7e57013759c285caa701eb6
destination 0xc783df8a850f42e7f7e57013759c285caa701eb6

_contractEmitsData: calldata
0x000000000000000000000000000000000000000000000000000000000000002000000000000000000000000000000000000000000000000000000000000000400000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000
sendToken:  0x0000000000000000000000000000000000000020
sendAmount:  64

contractEmitsData: memory
0x0000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000
sendToken:  0x0078371bdede8aac7debfff451b74c5edb385af7
sendAmount:  10000000000000000000
```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642469203):

Thanks for opening this issue! Just to clarify: Does it work by changing the second function parameter from `calldata` to `bytes`?

My current suspicion is that the encoded calldata is wrong. If you say that first decoding it into `bytes` makes it work, this means that there is another "layer of indirection" inside the data - `abi.decode(_contractEmitsData, (bytes))` is not the same as `bytes memory c = _contractEmitsData` (the first follows another offset, the second does a plain conversion).

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642471727):

I just edited the original comment a bit for more clarity and a closer assimilation to our actual data flow in the Dapp (using the delegatecall Batcher proxy).

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642475706):

> Does it work by changing the second function parameter from calldata to bytes?

Do you mean from `calldata` to `memory` ? (or is the definition of `bytes calldata == calldata` and `bytes memory == bytes` ? )

If so. I just tested. It does not work for 

```solidity
function foo(bytes calldata _offChainData, bytes memory _contractEmitsData)
```

Result: Same unexpected output.

> My current suspicion is that the encoded calldata is wrong.

Yes, I assume this has to do with the fact that `Batcher` gets the `emittedData` as `bytes memory`  and then uses this data to call `ConsumesData.foo` which takes `bytes calldata` ?

Actually let me add this to `EmitsData.emitData()`:

```solidity
contract EmitsData {
      function emitData() external returns(bytes memory emittedData) {
          console.log("EmitsData.emitData");
          address sendToken = 0x0078371bdede8aac7debfff451b74c5edb385af7;
          uint256 sendAmount = 10000000000000000000;
          console.log("sendToken: ", sendToken);
          console.log("sendAmount: ", sendAmount);
          emittedData = abi.encode(sendToken, sendAmount);
          console.log("emittedData:");
          console.logBytes(emittedData);
      }
}
```

This yields this output:
```
EmitsData.emitData
sendToken:  10000000000000000000
sendAmount:  0x0078371bdede8aac7debfff451b74c5edb385af7
emittedData:
0x0000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000

ConsumesData.foo
_offChainData: calldata
0x42929d440000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000000000000000000000000000000000000000000000c783df8a850f42e7f7e57013759c285caa701eb6
destination 0xc783df8a850f42e7f7e57013759c285caa701eb6

_contractEmitsData: calldata
0x000000000000000000000000000000000000000000000000000000000000002000000000000000000000000000000000000000000000000000000000000000400000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000
sendToken:  0x0000000000000000000000000000000000000020
sendAmount:  64

contractEmitsData: memory
0x0000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000
sendToken:  0x0078371bdede8aac7debfff451b74c5edb385af7
sendAmount:  10000000000000000000
```

**Cleary, the emittedData is only in the correct format on ContractConsumesData, after it has been `abi.decoded` into bytes memory**

See:

EmitsData: emittedData == ConsumesData: contractEmitsData: memory

```
EmitsData.emitData
emittedData:
0x0000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000
```
```
ConsumesData.foo
contractEmitsData: memory
0x0000000000000000000000000078371bdede8aac7debfff451b74c5edb385af70000000000000000000000000000000000000000000000008ac7230489e80000
```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642492212):

Ok, I think the problem lies in the fact that you return a `bytes` variable from a function that is low-level called. The return value of that low-level call is of type `bytes`, but this is encoded once, so you have to use `abi.decode` to get the original `bytes` value.

I think you can fix it by changing `emitData` to directly return `(uint, address)` instead of abi-encoding these two values.

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642500872):

Ok, thanks for the explanation.

> I think you can fix it by changing emitData to directly return (uint, address) instead of abi-encoding these two values.

I will try, but there is one problem I see already. The actual functions we use that emit data `override` an interface and to be standardisable that interface always `returns (bytes memory)`.
So I am getting a `Overriding function return types differ` if I will try to return `(uint, address)`

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642502863):

If you cannot change the signature,then you have to run `abi.decode` one more time.

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642506762):

> I think you can fix it by changing emitData to directly return (uint, address) instead of abi-encoding these two values.

This worked! 

> If you cannot change the signature,then you have to run abi.decode one more time.

But yes, I cannot change the sig. So I'll have to resort to decoding the original `bytes` value first, before decoding the values encoded therein:

```solidity
bytes memory emittedData = abi.decode(_emittedData, (bytes))
(address sendToken, uint256 sendAmount) = abi.decode(emittedData, (address,uint256));
```

But that works ok, so all fine!

Thanks for helping out and clarifying what format to expect from such a data flow that involves low-level delegatecall returndata.

Actually, this makes total sense to me. I just got confused here at first because you have to `abi.decode()` `bytes` to `bytes`.  But actually this is valid of course. Just like you would `abi.decode` any other `bytes memory returndata` you get from a `low-level` `call` or `delegatecall` into their original `types` and `values`. Same goes for `bytes` that were returned  from a function, of course. Makes total sense !

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2020-06-11 06:24](https://github.com/ethereum/solidity/issues/9182#issuecomment-642529938):

Actually my final solution that works like a charm on all the contracts is just something like this on our delegatecall batcher contract:

```solidity
contract Batcher {
    // I tried to slim our actual code down here so might have overlooked something
     function delegatecallEmitsDataAndDelegatecallConsumesDataWithTheEmittedData(
          address _emitsData, 
          address _consumesData,
          bytes calldata _offChainData
     ) 
          external 
    {
           (bool success, bytes memory returndata) = _emitsData.delegatecall();
           if (!success) revert();
           
           // All OutFlow actions return (bytes memory). But the low-level
           // delegatecall encoded those bytes into returndata.
           // So we have to decode them again to obtain the original bytes value.
           returndata = abi.decode(returndata, (bytes));

           (bool success,) = _consumesData.delegatecall(_offChainData, returndata);
           if (!success) revert();
    }
}
```

So basically I already `decode` on the Batcher contract, so that all Contracts that consume the data from a previous contract that emitted it, already get the originally emitted `bytes` value and don't have to each `abi.decode` themselves. This is done centrally by the Batcher.

Great, thanks for helping out!


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
