# [\#11764 Issue](https://github.com/ethereum/solidity/issues/11764) `open`: What is the naming convention in terms of leading/trailing underscore?
**Labels**: `documentation :book:`, `style guide`


#### <img src="https://avatars.githubusercontent.com/u/5157350?u=3de8e717a2d1894f97068d33ac31e52a9ce79793&v=4" width="50">[Ahmed-Ali](https://github.com/Ahmed-Ali) opened issue at [2021-08-09 15:00](https://github.com/ethereum/solidity/issues/11764):

## Page

https://docs.soliditylang.org/en/latest/solidity-by-example.html

## Abstract
I am a bit confused about solidity naming convention when it comes to leading/trailing underscores.
Where and how they should be used?
I saw contract members with and without trailing underscore, parameters with and without trailing underscore, in other examples parameters with and without leading underscore.

Is this a consistency issue, or there is a convention that I just don't know yet?



#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2021-08-09 15:00](https://github.com/ethereum/solidity/issues/11764#issuecomment-895794014):

There is a style guide https://docs.soliditylang.org/en/latest/style-guide.html which doesn't seem to mention anything about leading underscores in the name.

To be fair, I think the leading underscore for function parameters comes from the style guide of the solidity compiler, written in c++. 

> I saw contract members with and without trailing underscore

Can you point out such an example?

#### <img src="https://avatars.githubusercontent.com/u/5157350?u=3de8e717a2d1894f97068d33ac31e52a9ce79793&v=4" width="50">[Ahmed-Ali](https://github.com/Ahmed-Ali) commented at [2021-08-09 15:00](https://github.com/ethereum/solidity/issues/11764#issuecomment-895805111):

> Can you point out such an example?

I am actually wrong. Seems like I mixed the constructor parameters (with leading underscore) and the member without trailing underscore.

However, the parameters themselves does have two variations without underscores, with leading underscore. Here are few examples

Without underscore
```
/// Create a new ballot to choose one of `proposalNames`.
    constructor(bytes32[] memory proposalNames) {
        chairperson = msg.sender;
        voters[chairperson].weight = 1;

        // For each of the provided proposal names,
        // create a new proposal object and add it
        // to the end of the array.
        for (uint i = 0; i < proposalNames.length; i++) {
            // `Proposal({...})` creates a temporary
            // Proposal object and `proposals.push(...)`
            // appends it to the end of `proposals`.
            proposals.push(Proposal({
                name: proposalNames[i],
                voteCount: 0
            }));
        }
    }

    // Give `voter` the right to vote on this ballot.
    // May only be called by `chairperson`.
    function giveRightToVote(address voter) public {
        // If the first argument of `require` evaluates
        // to `false`, execution terminates and all
        // changes to the state and to Ether balances
        // are reverted.
        // This used to consume all gas in old EVM versions, but
        // not anymore.
        // It is often a good idea to use `require` to check if
        // functions are called correctly.
        // As a second argument, you can also provide an
        // explanation about what went wrong.
        require(
            msg.sender == chairperson,
            "Only chairperson can give right to vote."
        );
        require(
            !voters[voter].voted,
            "The voter already voted."
        );
        require(voters[voter].weight == 0);
        voters[voter].weight = 1;
    }
```

With leading underscore
```
 function reveal(
        uint[] memory _values,
        bool[] memory _fake,
        bytes32[] memory _secret
    )
        public
        onlyAfter(biddingEnd)
        onlyBefore(revealEnd)
    {
        uint length = bids[msg.sender].length;
        require(_values.length == length);
        require(_fake.length == length);
        require(_secret.length == length);

        uint refund;
        for (uint i = 0; i < length; i++) {
            Bid storage bidToCheck = bids[msg.sender][i];
            (uint value, bool fake, bytes32 secret) =
                    (_values[i], _fake[i], _secret[i]);
            if (bidToCheck.blindedBid != keccak256(abi.encodePacked(value, fake, secret))) {
                // Bid was not actually revealed.
                // Do not refund deposit.
                continue;
            }
            refund += bidToCheck.deposit;
            if (!fake && bidToCheck.deposit >= value) {
                if (placeBid(msg.sender, value))
                    refund -= value;
            }
            // Make it impossible for the sender to re-claim
            // the same deposit.
            bidToCheck.blindedBid = bytes32(0);
        }
        payable(msg.sender).transfer(refund);
    }
```

Similarly for local scope variables. Sometime without underscore and sometime with trailing underscore

Here is a snippet with trailing underscore 
```
sender.voted = true;
        sender.delegate = to;
        Voter storage delegate_ = voters[to];
        if (delegate_.voted) {
            // If the delegate already voted,
            // directly add to the number of votes
            proposals[delegate_.vote].voteCount += sender.weight;
        } else {
            // If the delegate did not vote yet,
            // add to her weight.
            delegate_.weight += sender.weight;
        }
```

And here is a snippet where there is no trailing underscore
```
function reveal(
        uint[] memory _values,
        bool[] memory _fake,
        bytes32[] memory _secret
    )
        public
        onlyAfter(biddingEnd)
        onlyBefore(revealEnd)
    {
        uint length = bids[msg.sender].length;
        require(_values.length == length);
        require(_fake.length == length);
        require(_secret.length == length);

        uint refund;
        for (uint i = 0; i < length; i++) {
            Bid storage bidToCheck = bids[msg.sender][i];
            (uint value, bool fake, bytes32 secret) =
                    (_values[i], _fake[i], _secret[i]);
            if (bidToCheck.blindedBid != keccak256(abi.encodePacked(value, fake, secret))) {
                // Bid was not actually revealed.
                // Do not refund deposit.
                continue;
            }
            refund += bidToCheck.deposit;
            if (!fake && bidToCheck.deposit >= value) {
                if (placeBid(msg.sender, value))
                    refund -= value;
            }
            // Make it impossible for the sender to re-claim
            // the same deposit.
            bidToCheck.blindedBid = bytes32(0);
        }
        payable(msg.sender).transfer(refund);
    }
```

It seems to just make it difficult to stay consistent with all these variations.

So I guess the important question here is: are these the actual convention (I still need to go thoroughly through the style guide to see if it answers it) or this is just inconsistency in the referenced documentation?

#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2021-08-09 15:00](https://github.com/ethereum/solidity/issues/11764#issuecomment-895813808):

For `delegate_`, the trailing underscore is simply there to avaoid a shadowing warning (the name delegate is already taken in that context).

For
```solidity
function reveal(
        uint[] memory _values,
        bool[] memory _fake,
        bytes32[] memory _secret
    )
```

there is no problem removing `_`. Maybe we could even update that contract? Would you like to submit a PR for that?

#### <img src="https://avatars.githubusercontent.com/u/5157350?u=3de8e717a2d1894f97068d33ac31e52a9ce79793&v=4" width="50">[Ahmed-Ali](https://github.com/Ahmed-Ali) commented at [2021-08-09 15:00](https://github.com/ethereum/solidity/issues/11764#issuecomment-895815790):

> there is no problem removing _. Maybe we could even update that contract? Would you like to submit a PR for that?

Sure, happy to do so. Just to make sure I understand correctly so I can do the right change.
The underscore in the parameters or local variables isnâ€™t needed unless there is a reason (like shadowing a contract member), did I get that right?

#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2021-08-09 15:00](https://github.com/ethereum/solidity/issues/11764#issuecomment-895822032):

To summarize, one could say that the official style guide is https://docs.soliditylang.org/en/latest/style-guide.html. It doesn't talk about leading underscores. (It's still worthwhile to go through it to check if I missed something.)

I have seen some contracts naming internal functions starting with underscore. Perhaps to improve visibility. 

I think it would be useful to change `contract BlindAuction` https://docs.soliditylang.org/en/latest/style-guide.html, removing the argument names that start with `_`. Simply to make the contract more consistent with the rest of the examples. If you would like to fix this, then https://github.com/ethereum/solidity/blob/develop/docs/solidity-by-example.rst is the file that needs updating (I think the github preview is broken for that rst file)

#### <img src="https://avatars.githubusercontent.com/u/5157350?u=3de8e717a2d1894f97068d33ac31e52a9ce79793&v=4" width="50">[Ahmed-Ali](https://github.com/Ahmed-Ali) commented at [2021-08-09 15:00](https://github.com/ethereum/solidity/issues/11764#issuecomment-895822765):

Awesome, thanks for the pointers. Will go through the style guide and then do the suggested changes.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
