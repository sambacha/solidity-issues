# [\#349 Issue](https://github.com/ethereum/solidity/issues/349) `closed`: Docs: Blind Auction bug

#### <img src="https://avatars.githubusercontent.com/u/2582071?v=4" width="50">[jonasnick](https://github.com/jonasnick) opened issue at [2016-01-11 23:11](https://github.com/ethereum/solidity/issues/349):

In 'Solidity by Example' you show the following code as an implementation of a blind auction.
A participant bids by sending coins (`deposit`) and a hash of a `value` and a `secret` (`blindedBid`). The idea is that a `deposit` is provided up front, but the actual bid (`value`) can be less than `deposit` in order to preserve the blinding property. 

When revealing the value the user should then get a refund of `deposit - value`. However, the problem is that she can actually get `value` back, _even if she never deposited anything_ (as long as the contract has enough funds).

Changing the `refund += value;` line to `refund += bid.deposit;` would fix this bug.

Excerpt from the [blind auction code](https://solidity.readthedocs.org/en/latest/solidity-by-example.html#id2):

```
    function bid(bytes32 _blindedBid)
        onlyBefore(biddingEnd)
    {
        bids[msg.sender].push(Bid({
            blindedBid: _blindedBid,
            deposit: msg.value
        }));
    }

    /// Reveal your blinded bids. You will get a refund for all
    /// correctly blinded invalid bids and for all bids except for
    /// the totally highest.
    function reveal(uint[] _values, bool[] _fake,
                    bytes32[] _secret)
        onlyAfter(biddingEnd)
        onlyBefore(revealEnd)
    {
        uint length = bids[msg.sender].length;
        if (_values.length != length || _fake.length != length ||
                    _secret.length != length)
            throw;
        uint refund;
        for (uint i = 0; i < length; i++)
        {
            var bid = bids[msg.sender][i];
            var (value, fake, secret) =
                    (_values[i], _fake[i], _secret[i]);
            if (bid.blindedBid != sha3(value, fake, secret))
                // Bid was not actually revealed.
                // Do not refund deposit.
                continue;
            refund += value;
            if (!fake && bid.deposit >= value)
                if (placeBid(msg.sender, value))
                    refund -= value;
            // Make it impossible for the sender to re-claim
            // the same deposit.
            bid.blindedBid = 0;
        }
        msg.sender.send(refund);
    }
```





-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
