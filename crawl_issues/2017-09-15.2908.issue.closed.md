# [\#2908 Issue](https://github.com/ethereum/solidity/issues/2908) `closed`: Multiple `SSTORE` calls for the same storage location

#### <img src="https://avatars.githubusercontent.com/u/2864519?u=808f20ca62701a0b71027869276d6d3709e8c7eb&v=4" width="50">[ngotchac](https://github.com/ngotchac) opened issue at [2017-09-15 13:32](https://github.com/ethereum/solidity/issues/2908):

## Solidity version
I tested a couple of version from `0.4.0` onwards.

## Source code

```solidity
pragma solidity ^0.4.x;

contract TestSStore {
    struct TestStruct {
        uint128 a;
        uint128 b;
    }
    
    TestStruct s_data;
    
    function store (uint128 _a, uint128 _b) external {
      s_data = TestStruct({ a: _a, b: _b });
    }
}
```

## Steps to reproduce

Just deploy the contract, call `store` method with non-0 values, and look at the executed OPCODES

## Result

It executed two `SSTORE` OPCODES

## Expected Behaviour

As the Struct is composed of 2 `uint128`, it fits in one 32-bytes area. However, when calling `store`, it executes two `SSTORE` opcodes, thus increasing the gas usage. This increases the gas usage by 5,000 (since it just re-writes on the some storage location), which could be improved.

#### <img src="https://avatars.githubusercontent.com/u/2809499?u=85c557e8e011e3e40e6c011ee71bcf8785d1ac2b&v=4" width="50">[androlo](https://github.com/androlo) commented at [2017-10-02 19:52](https://github.com/ethereum/solidity/issues/2908#issuecomment-333645437):

User @adamskrodzki raised this issue on gitter as well, with an even more complex struct. It does 8 writes (1 full price + 7 discounted) for this:

```
    struct Bet {
        uint8 num1;
        uint8 num2;
        uint8 num3;
        uint8 num4;
        uint8 num5;
        uint8 num6;
        address gambler;
        uint32 betSum;
    }
```

No issues with packing though, they all end up in the same storage slot.

This i can confirm on latest stable (`0.4.17+commit.bdeb9e52.Emscripten.clang`) + nightly (`0.4.18-nightly.2017.10.2+commit.c6161030.Emscripten.clang`) of the emscripten version of solc, with optimizer enabled.

```
contract GameFundTest{

    struct Bet {
        uint8 num1;
        uint8 num2;
        uint8 num3;
        uint8 num4;
        uint8 num5;
        uint8 num6;
        address gambler;
        uint32 betSum;
    }
    
    Bet b;
    
    function checkAll() public {
        uint startGas = msg.gas;
        b = Bet(1, 2, 3, 4, 5, 6, 0x112233445566778899aa112233445566778899aa, 21);
        uint diff = startGas - msg.gas;
        assert(diff > 55000);
    }
    
}
```

#### <img src="https://avatars.githubusercontent.com/u/997681?v=4" width="50">[cdetrio](https://github.com/cdetrio) commented at [2017-11-13 05:10](https://github.com/ethereum/solidity/issues/2908#issuecomment-343815238):

This might be related to https://github.com/ethereum/solidity/issues/1137

#### <img src="https://avatars.githubusercontent.com/u/12047151?u=b3762462b156ead1cb83ceca77198d3f650baf2a&v=4" width="50">[greenGreengo](https://github.com/greenGreengo) commented at [2017-11-18 21:14](https://github.com/ethereum/solidity/issues/2908#issuecomment-345471720):

Yes it's the way the compiler works, it fits in one 32-bytes area but uses 2 `sstore`.
If you want to want to use just 1 `sstore` you have to enable `--optimize` flag when you compile.

`$ solc --bin --asm --optimize TestSStore.sol`

#### <img src="https://avatars.githubusercontent.com/u/2809499?u=85c557e8e011e3e40e6c011ee71bcf8785d1ac2b&v=4" width="50">[androlo](https://github.com/androlo) commented at [2017-11-20 08:49](https://github.com/ethereum/solidity/issues/2908#issuecomment-345628873):

@cdetrio They are different issues. This is not array related, and technically it is not a bug. At the time of this report, the way storage writes are done (in the case of one-word structs) is:

```
for all members M in storage struct S:
  read old S from storage to var X
  write M to X
  replace old S with X
```

Old S is read from storage, modified, and the modified result is written to storage, and this is repeated once for each member. We want it to be:

```
read old S from storage to var X

for all members M in storage struct S:
  write M to X

replace old S with X
```

This should also be extended to work for any size struct, which is probably not too difficult since the methods for manipulating packed storage are already in place, and seem to work perfectly fine.

```
for all storage partitions T of struct S:
  read T from storage to var X

  for all members M that goes into T:
    write M to X

  replace T with X
```

This should result in one `SLOAD` and `SSTORE` per word, rather then one per member variable per word.

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2017-11-20 14:01](https://github.com/ethereum/solidity/issues/2908#issuecomment-345703870):

@androlo that should be fairly safe to implement once storage-related code is written in the IR, but before that it may be a risky change.

#### <img src="https://avatars.githubusercontent.com/u/2809499?u=85c557e8e011e3e40e6c011ee71bcf8785d1ac2b&v=4" width="50">[androlo](https://github.com/androlo) commented at [2017-11-20 15:45](https://github.com/ethereum/solidity/issues/2908#issuecomment-345735429):

@ngotchac

#### <img src="https://avatars.githubusercontent.com/u/2864519?u=808f20ca62701a0b71027869276d6d3709e8c7eb&v=4" width="50">[ngotchac](https://github.com/ngotchac) commented at [2017-11-20 15:53](https://github.com/ethereum/solidity/issues/2908#issuecomment-345737891):

I wasn't sure if it was a bug or an enhancement. Seems to be an enhancement then!

#### <img src="https://avatars.githubusercontent.com/u/15745003?u=f176ecc43b6c4e853299886008482c5e734dd044&v=4" width="50">[artemii235](https://github.com/artemii235) commented at [2018-01-16 06:47](https://github.com/ethereum/solidity/issues/2908#issuecomment-357868310):

Discovered same using 0.4.19+commit.c4cbbb05.Emscripten.clang, as an example please take a look at this Smart Contract at Ropsten:
https://ropsten.etherscan.io/address/0xa66bc2b55ef5dff147edd7352a633fe8d510839c
It uses `Deal` struct which contains 7 fields.
When `initEthDeal` function is called new `Deal` is created and added to mapping: https://ropsten.etherscan.io/tx/0xb659eb6d17e75fe8e5518398a1d3871c4dece509d43ce38384f3b640422551c9
As we can see by vmtrace (https://ropsten.etherscan.io/vmtrace?txhash=0xb659eb6d17e75fe8e5518398a1d3871c4dece509d43ce38384f3b640422551c9) it causes 7 `sstore` calls which results in high gas usage for such simple method (over 140k).
In mainnet with gas price = 20 gwei this transaction will cost 3$ (Current ETH price ~1200$) for a caller which is pretty high.
So in my particular case this improvement will help to save a lot money of end users.
My idea is also to pack everything in `bytes` array and then parse it further. However it would be very nice to have solidity doing it internally without such workarounds.

#### <img src="https://avatars.githubusercontent.com/u/15745003?u=f176ecc43b6c4e853299886008482c5e734dd044&v=4" width="50">[artemii235](https://github.com/artemii235) commented at [2018-01-18 12:28](https://github.com/ethereum/solidity/issues/2908#issuecomment-358632101):

My bad, my struct size is much larger than 32 bytes and it's not related to this issue - it's just how EVM works (splitting everything to 32 bytes words and calling SSTORE for each).

#### <img src="https://avatars.githubusercontent.com/u/28908611?v=4" width="50">[beether](https://github.com/beether) commented at [2018-02-20 08:42](https://github.com/ethereum/solidity/issues/2908#issuecomment-366905750):

Not sure if this has yet been noted, but doing this reduces costs to what is expected:

```
// Compiler is dumb with converting `TestStruct memory` to storage.
// Avoid this.
// s_data = TestStruct({ a: _a, b: _b });

// Compiler is smart when modifying a `TestStruct storage`
// It will write all changes only once (and by 256-bit segment)
s_data.a = _a;
s_data.b = _b;
```

The optimizer seems smart enough to cache all changes to a `TestStruct storage` and write them out once. Not sure how it figures out there are no more changes (maybe it writes before any return), but it seems pretty robust -- even if you pass around the `TestStruct storage` it will still only write once per 256-bit block.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-03-13 16:44](https://github.com/ethereum/solidity/issues/2908#issuecomment-372734233):

This might be fixed by #3693 , but we should check.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-05-14 10:20](https://github.com/ethereum/solidity/issues/2908#issuecomment-388768739):

This has been fixed.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
