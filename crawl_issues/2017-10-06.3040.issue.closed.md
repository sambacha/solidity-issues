# [\#3040 Issue](https://github.com/ethereum/solidity/issues/3040) `closed`: Adding payable to constructor of contract causes deployment cost to skyrocket

#### <img src="https://avatars.githubusercontent.com/u/4350404?v=4" width="50">[dwalintukan](https://github.com/dwalintukan) opened issue at [2017-10-06 11:33](https://github.com/ethereum/solidity/issues/3040):

I think I've found a bug.

I have a working contract that deploys fine using Kovan testnet & Remix.
**Gas Deployment cost: 0.02820 ETH**
```
function TopicEvent(
        address _owner, 
        address _resultSetter, 
        bytes32 _name, 
        bytes32[] _resultNames, 
        uint256 _bettingEndBlock) 
    {
        require(_owner != 0);
        require(_resultSetter != 0);
        require(_name.length > 0);
        require(_resultNames.length > 1);
        require(_bettingEndBlock > block.number);

        owner = _owner;
        resultSetter = _resultSetter;
        name = _name;

        for (uint i = 0; i < _resultNames.length; i++) {
            results.push(Result({
            name: _resultNames[i],
            balance: 0
            }));
        }

        bettingEndBlock = _bettingEndBlock;

        TopicCreated(name);
    }
```

After adding the `payable` keyword to the constructor: 
**gas deployment costs ~100 ETH**.

I've tested this with two different contracts. Why is this happening?

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2017-10-06 11:46](https://github.com/ethereum/solidity/issues/3040#issuecomment-334732652):

Can you please copy in your entire contract triggering this and all the version of the tools used (Solidity, Remix/Geth, etc.)?

#### <img src="https://avatars.githubusercontent.com/u/4350404?v=4" width="50">[dwalintukan](https://github.com/dwalintukan) commented at [2017-10-06 18:08](https://github.com/ethereum/solidity/issues/3040#issuecomment-334829914):

**Solidity Version on Remix: 0.4.17+commit.bdeb9e52.Emscripten.clang**
**My Machine: 0.4.16+commit.d7661dd9.Emscripten.clang**
**TestRPC: 4.1.1**

**My exact steps to figuring this out:**

**Local Testing first**
On my local machine and using `TestRPC`, the following contract would compile, migrate, and pass all the tests. With the `payable` keyword, but without the `address _addressManager` param in the constructor (first param).

Once I added the `address _addressManager`, the tests would crash with an `Out of Gas exception`. I commented out the code that used this param and it still threw the OOG exception. Literally, just passing in the address would cause the OOG exception (even if I didn't do anything with the address). I have tested this thoroughly and have verified this (both having the address param and without).

Next I tried to increasing the block gas limit on TestRPC to 10,000,000 and sending the max limit of gas when creating the contract in my tests with the `address _addressManager` param. Still I received the OOG exception.

**Remix Testing**
This lead me to do some further testing on Remix, Parity, and deploying on the Kovan testnet. I just copied my working code (without the `address _addressManager` param) to Remix and tried deploying, but it was asking me for 100 ETH to deploy the contract. So I started removing constructor params and contract code to see if anything would make it go down. Until I realized it was the `payable` keyword in the constructor. 

I then re-copied all the contract code to Remix (with the `address _addressManager` param) and removed the `payable` keyword. Then deployed from Remix and Parity allowed me to deploy it with ~0.02 ETH.

**Conclusions**
So my findings were:
1. on my local machine, adding the `address _addressManager` param would cause my tests to fail with an OOG exception. But the `payable` was allowed.
2. on Remix/Kovan testnet, the `payable` keyword caused my deployment costs from ~0.02 ETH to jump to 100 ETH. But the `address _addressManager` logic worked fine.



**Oracle.sol**

```
pragma solidity ^0.4.15;

import "../libs/SafeMath.sol";
import "../addressmanager/IAddressManager.sol";

contract Oracle {
    using SafeMath for uint256;

    struct Participant {
        uint256 stakeContributed;
        bool didSetResult;
        uint8 resultIndex;
        bool didWithdrawEarnings;
    }

    struct Result {
        bytes32 name;
        uint256 votedBalance;
    }

    uint256 public constant nativeDecimals = 18; // Number of decimals of token used to create Oracle
    uint256 public constant botDecimals = 8; // Number of decimals for BOT
    uint256 public constant minBaseReward = 1 * (10**nativeDecimals); // Minimum amount needed to create Oracle
    uint256 public constant maxStakeContribution = 101 * (10**botDecimals); // Maximum amount of BOT staking contributions allowed

    bytes public eventName;
    uint256 public eventBettingEndBlock;
    uint256 public decisionEndBlock; // Block number when Oracle participants can no longer set a result
    uint256 public arbitrationOptionEndBlock; // Block number when Oracle participants can no longer start arbitration
    uint256 public totalStakeContributed;

    IAddressManager private addressManager;
    Result[] private eventResults;
    mapping(address => Participant) private participants;

    // Modifiers
    modifier validResultIndex(uint _resultIndex) {
        require(_resultIndex >= 0);
        require(_resultIndex <= eventResults.length - 1);
        _;
    }

    // Events
    event OracleCreated(bytes _eventName, bytes32[] _eventResultNames, uint256 _eventBettingEndBlock, 
        uint256 _decisionEndBlock, uint256 _arbitrationOptionEndBlock, uint256 _baseRewardAmount);
    event ParticipantVoted(address _participant, uint256 _stakeContributed, uint8 _resultIndex);
    event EarningsWithdrawn(uint256 _amountWithdrawn);

    /// @notice Creates new Oracle contract. Requires payment of the minBaseReward. 
    /// @param _eventName The name of the Event this Oracle will arbitrate.

    function Oracle(
        address _addressManager,
        bytes _eventName,
        bytes32[] _eventResultNames,
        uint256 _eventBettingEndBlock,
        uint256 _decisionEndBlock,
        uint8 _averageBlockTime,
        uint256 _arbitrationOptionMinutes) 
        public
        payable
    {
        require(_addressManager != address(0));
        require(msg.value >= minBaseReward);
        require(_eventName.length > 0);
        require(_eventResultNames.length > 1);
        require(_decisionEndBlock > _eventBettingEndBlock);
        require(_averageBlockTime > 0);
        require(_arbitrationOptionMinutes > 0);

        addressManager = IAddressManager(_addressManager);
        eventName = _eventName;

        for (uint i = 0; i < _eventResultNames.length; i++) {
            eventResults.push(Result({
                name: _eventResultNames[i],
                votedBalance: 0
            }));
        }

        eventBettingEndBlock = _eventBettingEndBlock;
        decisionEndBlock = _decisionEndBlock;

        uint256 arbitrationBlocks = getArbitrationOptionBlocks(_averageBlockTime, _arbitrationOptionMinutes);
        arbitrationOptionEndBlock = decisionEndBlock.add(arbitrationBlocks);
        assert(arbitrationOptionEndBlock > decisionEndBlock);

        OracleCreated(_eventName, _eventResultNames, _eventBettingEndBlock, _decisionEndBlock, 
            arbitrationOptionEndBlock, msg.value);
    }

    function() payable {
    }

    /// @notice Vote an Event result which requires BOT payment.
    /// @param _eventResultIndex The Event result which is being voted on.
    function voteResult(uint8 _eventResultIndex) 
        public 
        payable 
        validResultIndex(_eventResultIndex) 
    {
        require(msg.value > 0);
        require(block.number >= eventBettingEndBlock);
        require(block.number < decisionEndBlock);
        require(!participants[msg.sender].didSetResult);

        Participant storage participant = participants[msg.sender];
        participant.stakeContributed = participant.stakeContributed.add(msg.value);
        participant.resultIndex = _eventResultIndex;
        participant.didSetResult = true;

        eventResults[_eventResultIndex].votedBalance = eventResults[_eventResultIndex].votedBalance.add(msg.value);
        totalStakeContributed = totalStakeContributed.add(msg.value);

        ParticipantVoted(msg.sender, msg.value, _eventResultIndex);
    }

    /// @notice Withdraw earnings if you picked the correct result.
    function withdrawEarnings() public {
        require(block.number >= arbitrationOptionEndBlock);
        require(participants[msg.sender].stakeContributed > 0);
        require(totalStakeContributed > 0);
        require(!participants[msg.sender].didWithdrawEarnings);

        uint256 withdrawAmount = getEarningsAmount();
        participants[msg.sender].didWithdrawEarnings = true;

        require(withdrawAmount > 0);
        msg.sender.transfer(withdrawAmount);

        EarningsWithdrawn(withdrawAmount);
    }

    /// @notice Gets the Event result name given a valid index.
    /// @param _eventResultIndex The index of the wanted result name.
    /// @return The name of the Event result.
    function getEventResultName(uint8 _eventResultIndex) 
        public 
        validResultIndex(_eventResultIndex) 
        constant 
        returns (bytes32) 
    {
        return eventResults[_eventResultIndex].name;
    }

    /// @notice Gets the number of blocks allowed for arbitration.
    /// @param _averageBlockTime The current average mining block time.
    /// @param _arbitrationOptionMinutes The number of minutes allowed for initiating arbitration.
    function getArbitrationOptionBlocks(
        uint8 _averageBlockTime, 
        uint256 _arbitrationOptionMinutes) 
        public 
        constant 
        returns(uint256) 
    {
        return _arbitrationOptionMinutes.div(uint256(_averageBlockTime));
    }

    /// @notice Gets the stake contributed by the Oracle participant.
    /// @return The amount of stake contributed by the Oracle participant.
    function getStakeContributed() public constant returns(uint256) {
        return participants[msg.sender].stakeContributed;
    }

    /// @notice Shows if the Oracle participant has voted yet.
    /// @return Flag that shows if the Oracle participant has voted yet.
    function didSetResult() public constant returns(bool) {
        return participants[msg.sender].didSetResult;
    }

    /// @notice Gets the result index the Oracle participant previously voted on.
    /// @return The voted result index.
    function getVotedResultIndex() public constant returns(uint8) {
        require(participants[msg.sender].didSetResult);
        return participants[msg.sender].resultIndex;
    }

    /// @notice Gets the final result index set by the Oracle participants.
    /// @return The index of the final result set by Oracle participants.
    function getFinalResultIndex() public constant returns (uint8) {
        require(block.number >= decisionEndBlock);

        uint8 finalResultIndex = 0;
        uint256 winningIndexAmount = 0;
        for (uint8 i = 0; i < eventResults.length; i++) {
            uint256 votedBalance = eventResults[i].votedBalance;
            if (votedBalance > winningIndexAmount) {
                winningIndexAmount = votedBalance;
                finalResultIndex = i;
            }
        }

        return finalResultIndex;
    }

    /// @notice Gets the amount of earnings you can withdraw.
    /// @return The amount of earnings you can withdraw.
    function getEarningsAmount() public constant returns(uint256) {
        uint256 stakeContributed = participants[msg.sender].stakeContributed;
        if (stakeContributed == 0) {
            return 0;
        }

        if (!participants[msg.sender].didSetResult) {
            return 0;
        }

        if (participants[msg.sender].didWithdrawEarnings) {
            return 0;
        }

        uint8 finalResultIndex = getFinalResultIndex();
        if (participants[msg.sender].resultIndex != finalResultIndex) {
            return 0;
        }

        uint256 winningResultContributions = eventResults[finalResultIndex].votedBalance;
        uint256 losingResultContributions = totalStakeContributed.sub(winningResultContributions);
        return stakeContributed.mul(losingResultContributions).div(winningResultContributions).add(stakeContributed);
    }
}

```

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2017-11-29 00:09](https://github.com/ethereum/solidity/issues/3040#issuecomment-347707732):

@dwalintukan I couldn't reproduce it. Is it still an issue?

#### <img src="https://avatars.githubusercontent.com/u/4350404?v=4" width="50">[dwalintukan](https://github.com/dwalintukan) commented at [2017-12-02 11:47](https://github.com/ethereum/solidity/issues/3040#issuecomment-348686826):

@axic I actually just did a workaround and sent value after constructor was called. Going to close it for now.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
