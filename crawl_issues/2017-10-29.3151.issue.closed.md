# [\#3151 Issue](https://github.com/ethereum/solidity/issues/3151) `closed`: grammar.txt: missing rule for low level functions (assembly)
**Labels**: `bug :bug:`, `documentation :book:`


#### <img src="https://avatars.githubusercontent.com/u/1956149?u=f8593a6bb6acd2626bbb47dcd231360c8a9a2f6a&v=4" width="50">[walter-weinmann](https://github.com/walter-weinmann) opened issue at [2017-10-29 11:35](https://github.com/ethereum/solidity/issues/3151):

(I am currently trying to verify my Solidity LALR Parser project [syparse](https://github.com/walter-weinmann/syparse) with the examples in the [Solidity  documentation](https://github.com/ethereum/solidity/tree/develop/docs).)

The following example is taken from chapter `Inline Assembly (Functions)` in document `assembly.rst`:
```
    {
        function power(base, exponent) -> result {
            switch exponent
            case 0 { result := 1 }
            case 1 { result := base }
            default {
                result := power(mul(base, base), div(exponent, 2))
                switch mod(exponent, 2)
                    case 1 { result := mul(base, result) }
            }
        }
    }
```
This example is not covered in `grammar.txt`.

The following excerpt from the `assembly. rst` document has not been transferred completely to `grammar. txt`:

Parsing / Grammar
-----------------

The tasks of the parser are the following:

- Turn the byte stream into a token stream, discarding C++-style comments
  (a special comment exists for source references, but we will not explain it here).
- Turn the token stream into an AST according to the grammar below
- Register identifiers with the block they are defined in (annotation to the
  AST node) and note from which point on, variables can be accessed.

The assembly lexer follows the one defined by Solidity itself.

Whitespace is used to delimit tokens and it consists of the characters
Space, Tab and Linefeed. Comments are regular JavaScript/C++ comments and
are interpreted in the same way as Whitespace.

Grammar::

    AssemblyBlock = '{' AssemblyItem* '}'
    AssemblyItem =
        Identifier |
        AssemblyBlock |
        FunctionalAssemblyExpression |
        AssemblyLocalDefinition |
        FunctionalAssemblyAssignment |
        AssemblyAssignment |
        LabelDefinition |
        AssemblySwitch |
        AssemblyFunctionDefinition |
        AssemblyFor |
        'break' | 'continue' |
        SubAssembly | 'dataSize' '(' Identifier ')' |
        LinkerSymbol |
        'errorLabel' | 'bytecodeSize' |
        NumberLiteral | StringLiteral | HexLiteral
    Identifier = [a-zA-Z_$] [a-zA-Z_0-9]*
    FunctionalAssemblyExpression = Identifier '(' ( AssemblyItem ( ',' AssemblyItem )* )? ')'
    AssemblyLocalDefinition = 'let' IdentifierOrList ':=' FunctionalAssemblyExpression
    FunctionalAssemblyAssignment = IdentifierOrList ':=' FunctionalAssemblyExpression
    IdentifierOrList = Identifier | '(' IdentifierList ')'
    IdentifierList = Identifier ( ',' Identifier)*
    AssemblyAssignment = '=:' Identifier
    LabelDefinition = Identifier ':'
    AssemblySwitch = 'switch' FunctionalAssemblyExpression AssemblyCase*
        ( 'default' AssemblyBlock )?
    AssemblyCase = 'case' FunctionalAssemblyExpression AssemblyBlock
    AssemblyFunctionDefinition = 'function' Identifier '(' IdentifierList? ')'
        ( '->' '(' IdentifierList ')' )? AssemblyBlock
    AssemblyFor = 'for' ( AssemblyBlock | FunctionalAssemblyExpression)
        FunctionalAssemblyExpression ( AssemblyBlock | FunctionalAssemblyExpression) AssemblyBlock
    SubAssembly = 'assembly' Identifier AssemblyBlock
    LinkerSymbol = 'linkerSymbol' '(' StringLiteral ')'
    NumberLiteral = HexNumber | DecimalNumber
    HexLiteral = 'hex' ('"' ([0-9a-fA-F]{2})* '"' | '\'' ([0-9a-fA-F]{2})* '\'')
    StringLiteral = '"' ([^"\r\n\\] | '\\' .)* '"'
    HexNumber = '0x' [0-9a-fA-F]+
    DecimalNumber = [0-9]+



#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2017-10-29 15:31](https://github.com/ethereum/solidity/issues/3151#issuecomment-340270780):

The best, if at all possible, would be treating data between `assembly { .. }` as verbatim because it essentially is a different language with its own grammar.

#### <img src="https://avatars.githubusercontent.com/u/1956149?u=f8593a6bb6acd2626bbb47dcd231360c8a9a2f6a&v=4" width="50">[walter-weinmann](https://github.com/walter-weinmann) commented at [2017-10-29 18:47](https://github.com/ethereum/solidity/issues/3151#issuecomment-340284705):

How about we have two grammar definitions: `grammar_solidity.txt` and `grammar_assembly.txt`? The existing grammar definition `grammar.txt` becomes `grammar_solidity.txt`. The following rules are removed:
```
InlineAssemblyBlock = '{' AssemblyItem* '}'

AssemblyItem = Identifier | FunctionalAssemblyExpression | InlineAssemblyBlock | AssemblyLocalBinding | AssemblyAssignment | AssemblyLabel | NumberLiteral | StringLiteral | HexLiteral
AssemblyLocalBinding = 'let' Identifier ':=' FunctionalAssemblyExpression
AssemblyAssignment = ( Identifier ':=' FunctionalAssemblyExpression ) | ( '=:' Identifier )
AssemblyLabel = Identifier ':'
FunctionalAssemblyExpression = Identifier '(' AssemblyItem? ( ',' AssemblyItem )* ')'
```
The grammar rule

`InlineAssemblyStatement = 'assembly' StringLiteral? InlineAssemblyBlock`

becomes the link to `grammatik_assembly.txt`, which contains the rules of the assembly language according to the document `assembly.rst`.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
