# [\#3469 Issue](https://github.com/ethereum/solidity/issues/3469) `closed`: Struct overwriting storage

#### <img src="https://avatars.githubusercontent.com/u/6056989?u=c66d760f40fa492136db1c5425d3e4f46fc8b8be&v=4" width="50">[Arctek](https://github.com/Arctek) opened issue at [2018-02-08 13:58](https://github.com/ethereum/solidity/issues/3469):

Hi,

While checking new verified contracts my friend discovered the following one (meant to scam people):

    pragma solidity 0.4.19;

    contract test{
        struct SeedComponents{
            uint component1;
            uint component2;
            uint component3;
            uint component4;
        }
        
        address owner;
        uint public secretSeed;
        uint public lastReseed;
        
        uint public winnerLuckyNumber = 7;
            
        mapping (address => bool) participated;

        event showme(uint luckyNumberOfAddress, uint winnerLuckyNumber, uint n);

        
        function showNumber() constant returns (uint winnerLuckyNumber){}
        
        function test() {
            owner = msg.sender;
            reseed(SeedComponents(12345678, 0x12345678, 0xabbaeddaacdc, 0x22222222));
        }
        
        modifier onlyOwner() {
            require(msg.sender == owner);
            _;
        }
      
        modifier onlyHuman() {
            require(msg.sender == tx.origin);
            _;
        }
        
        function participate() payable onlyHuman { 
            require(msg.value == 0.1 ether);
            
            // every address can only win once, obviously
           require(!participated[msg.sender]);
             showme(luckyNumberOfAddress(msg.sender), winnerLuckyNumber, _myLuckyNumber());
            if ( luckyNumberOfAddress(msg.sender) == winnerLuckyNumber)
            {
                participated[msg.sender] = true;
                require(msg.sender.call.value(this.balance)());
            }
        }
        
        function luckyNumberOfAddress(address addr) constant returns(uint n){
            // 1 in 8 chance
            n = uint(keccak256(uint(addr), secretSeed)[0]) % 8;
           
        }
        
        function reseed(SeedComponents components) internal {
            secretSeed = uint256(keccak256(
                components.component1,
                components.component2,
                components.component3,
                components.component4
            ));
            lastReseed = block.number;
        }
        
        function kill() onlyOwner {
            suicide(owner);
        }
        
        function forceReseed() onlyOwner{
            SeedComponents s;
            s.component1 = uint(msg.sender);
            s.component2 = uint256(block.blockhash(block.number - 1));
            s.component3 = block.number * 1337;
            s.component4 = tx.gasprice * 7;
            reseed(s);
        }
        
        function () payable {}
        
        // DEBUG, DELETE BEFORE DEPLOYMENT!!
        function _myLuckyNumber() constant returns(uint n){
            n = luckyNumberOfAddress(msg.sender);
        }
    }

When `forceReseed()` is called `winnerLuckyNumber` is overwritten.
Declaring `SeedComponents s` as `memory` solves this issue.
Is this intended behaviour?

I have deployed both of these versions to rinkeby:
https://rinkeby.etherscan.io/address/0x50776f731602549acf57ba7f9ea8fae5f8b15581#readContract
https://rinkeby.etherscan.io/address/0xdb1975e10575d9ee05344ca84eeccb5fd4fdbe5f#readContract


#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-02-08 14:15](https://github.com/ethereum/solidity/issues/3469#issuecomment-364123998):

The compiler should warn you about the storage pointer `SeedComponents s;` being uninitialized. Please take warnings seriously!

#### <img src="https://avatars.githubusercontent.com/u/6056989?u=c66d760f40fa492136db1c5425d3e4f46fc8b8be&v=4" width="50">[Arctek](https://github.com/Arctek) commented at [2018-02-08 14:20](https://github.com/ethereum/solidity/issues/3469#issuecomment-364125549):

Thanks, I can see that.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
