# [\#4635 Issue](https://github.com/ethereum/solidity/issues/4635) `closed`: Allow mapping storage parameters for  library functions
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) opened issue at [2018-08-01 10:10](https://github.com/ethereum/solidity/issues/4635):

Since  library functions allow storage struct parameters, storage mapping parameters should also be allowed. This would enable us to modularize some things:

```
library Balances {
    function send(mapping(address => uint256) storage balances, address from, address to, uint amount) internal {
        require(balances[from] >= amount);
        require(balances[to] + amount >= balances[to]);
        balances[from] -= amount;
        balances[to] += amount;
    }
}

contract Token {
    mapping(address => uint256) balances;
    using Balances for *;
    mapping(address => mapping (address => uint256)) allowed;

    event Transfer(address from, address to, uint amount);
    event Approval(address owner, address spender, uint amount);

    function balanceOf(address tokenOwner) public constant returns (uint balance) {
        return balances[tokenOwner];
    }
    function transfer(address to, uint amount) public returns (bool success) {
        balances.send(msg.sender, to, amount);
        emit Transfer(msg.sender, to, amount);
        return true;

    }

    function transferFrom(address from, address to, uint amount) public returns (bool success) {
        require(allowed[from][msg.sender] >= amount);
        allowed[from][msg.sender] -= amount;
        balances.send(from, to, amount);
        emit Transfer(from, to, amount);
        return true;
    }

    function approve(address spender, uint tokens) public returns (bool success) {
        require(allowed[msg.sender][spender] == 0, "");
        allowed[msg.sender][spender] = tokens;
        emit Approval(msg.sender, spender, tokens);
        return true;
    }
}
```

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2018-08-01 14:01](https://github.com/ethereum/solidity/issues/4635#issuecomment-409585310):

I think this a good idea, extends the usability of libraries.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2018-08-10 12:16](https://github.com/ethereum/solidity/issues/4635#issuecomment-412064994):

Should this only be the case for internal *library* functions?

Currently the following does not work either, and I don't see a good reason, why not:

```
contract C {
  function get(mapping(uint => uint) storage m, uint key) internal view returns (uint) {
    return m[key];
  }
}
```
This yields ``TypeError: Type is required to live outside storage.``

Is there any good reason to require a type ``to live outside storage`` for the arguments of internal functions at all?

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2018-08-10 12:20](https://github.com/ethereum/solidity/issues/4635#issuecomment-412065758):

Somewhat related: #4670

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-08-13 13:52](https://github.com/ethereum/solidity/issues/4635#issuecomment-412525334):

This requires defining a type string for mappings for the function selector (as we did with strings).

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-08-13 13:53](https://github.com/ethereum/solidity/issues/4635#issuecomment-412525613):

(extended scope to public library functions)

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2018-08-14 10:15](https://github.com/ethereum/solidity/issues/4635#issuecomment-412823901):

Moved to 0.5.1


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
