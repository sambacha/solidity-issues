# [\#10084 Issue](https://github.com/ethereum/solidity/issues/10084) `closed`: 0.7.4 Compiler Warning: unnamed return variable 
**Labels**: `bug :bug:`, `should compile without error`


#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) opened issue at [2020-10-21 08:52](https://github.com/ethereum/solidity/issues/10084):

As discussed with @chriseth on Gitter I am reporting the issue here:

The 0.7.4 compiler warns me about an Unnamed return variable:

```solidity
function getPrice(string memory _oracle, bytes calldata _oracleRequestData)
    external
    view
    returns (uint256)
{
    (bool success, bytes memory returndata) = oracle[_oracle].staticcall(
        _oracleRequestData
    );
    if (success) return abi.decode(returndata, (uint256));
    returndata.revertWithErrorString("PriceOracleResolver.getPrice:");
}
```

As you can see I return a uint256 via `return abi.decode(returndata, (uint256));`

The warning text is: `Warning: Unnamed return variable can remain unassigned. Add an explicit return with value to all non-reverting code paths or name the variable.`

**It seems the compiler thinks that the last line does not always revert.** 

Here is the library fn I am `using for` `bytes returndata`: 

```solidity
    function revertWithErrorString(
        bytes memory _bytes,
        string memory _tracingInfo
    ) internal pure {
        // 68: 32-location, 32-length, 4-ErrorSelector, UTF-8 err
        if (_bytes.length % 32 == 4) {
            bytes4 selector;
            assembly {
                selector := mload(add(0x20, _bytes))
            }
            if (selector == 0x08c379a0) {
                // Function selector for Error(string)
                assembly {
                    _bytes := add(_bytes, 68)
                }
                revert(string(abi.encodePacked(_tracingInfo, string(_bytes))));
            } else {
                revert(
                    string(abi.encodePacked(_tracingInfo, "NoErrorSelector"))
                );
            }
        } else {
            revert(
                string(abi.encodePacked(_tracingInfo, "UnexpectedReturndata"))
            );
        }
    }
```

#### <img src="https://avatars.githubusercontent.com/u/424752?u=2d50de05ec528b9b84f8b905a56e90669b0f8927&v=4" width="50">[Marenz](https://github.com/Marenz) commented at [2020-10-21 15:27](https://github.com/ethereum/solidity/issues/10084#issuecomment-713658933):

Simplified test case:
```
contract C
{
	function iWillRevert() pure public { revert(); }

	function test(bool _param) pure external returns(uint256)
	{
		if (_param) return 1;

		iWillRevert();
	}
}
```

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2020-11-10 07:27](https://github.com/ethereum/solidity/issues/10084#issuecomment-724517424):

This also gives an unwanted compiler warning: `Unnamed return variable can remain unassigned. Add an explicit return with value to all non-reverting code paths or name the variable.`

```solidity
    function assign(TaskReceipt calldata _taskReceipt)
        public
        payable
        returns (bool accepted, bytes memory)
    {
        require(msg.sender == _taskReceipt.client, "BServer.assign: !client");

        accepted = true;
    }
```

#### <img src="https://avatars.githubusercontent.com/u/424752?u=2d50de05ec528b9b84f8b905a56e90669b0f8927&v=4" width="50">[Marenz](https://github.com/Marenz) commented at [2021-04-06 14:29](https://github.com/ethereum/solidity/issues/10084#issuecomment-814167363):

@gitpusha your example seems to be a valid example of that warning. Your second return variable is unnamed and remains unassigned, too.

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2021-04-07 17:02](https://github.com/ethereum/solidity/issues/10084#issuecomment-815074908):

@Marenz yeah seems like my second example is invalidated - but my first comment not.

#### <img src="https://avatars.githubusercontent.com/u/424752?u=2d50de05ec528b9b84f8b905a56e90669b0f8927&v=4" width="50">[Marenz](https://github.com/Marenz) commented at [2021-04-07 17:03](https://github.com/ethereum/solidity/issues/10084#issuecomment-815075786):

Indeed, for the first comment we already have a PR that will fix it. just needs a bit more work


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
