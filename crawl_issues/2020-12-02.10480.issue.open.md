# [\#10480 Issue](https://github.com/ethereum/solidity/issues/10480) `open`: Wrong evaluation order of arguments to parent constructors
**Labels**: `breaking change :warning:`, `language design :rage4:`, `medium effort`, `low impact`


#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) opened issue at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480):

## Description

The arguments to parent constructors are evaluated before all parent constructors.

This is a problem when those arguments are (non-pure) function calls, because the function is evaluated before its defining contract has been initialized.

## Steps to Reproduce

```solidity
pragma solidity 0.7.5;

contract Foo {
    event FooConstructor();
    event FooGetValue();
    
    uint value;
    
    constructor (uint _value) {
        emit FooConstructor();
        value = _value;
    }
    
    function getValue() public returns (uint) {
        emit FooGetValue();
        return value;
    }
}

contract Bar {
    event BarConstructor(uint value);
    
    constructor (uint value) {
        emit BarConstructor(value);
    }
}

contract Child is Foo, Bar {
    constructor () Foo(5) Bar(Foo.getValue()) {
    }
}
```

Deploying `Child` currently emits the following sequence of events:

1. `FooGetValue()`
2. `FooConstructor()`
3. <code>BarConstructor(<b>value: 0</b>)</code>

Note: The same thing happens if you add a constraint on linearization such as `Bar is Foo`.

### Expected Behavior

Deploying `Child` should emit the following:

1. `FooConstructor()`
2. `FooGetValue()`
3. <code>BarConstructor(<b>value: 5</b>)</code>

That is, `Foo` is first initialized by calling its constructor, only then `Foo.getValue()` is evaluated, and its value can then be used as argument to `Bar`'s constructor.

### Specification

In general, I would expect the following to happen during contract construction:

Given the linearization of a contract's inheritance `C_0`, ..., `C_n` (from base to most derived), and the argument expressions for each constructor `e_i1`, ..., `e_im` (`m` different for every `C_i`)

- For each `C_i` = `C_0`, ..., `C_n` in order
  - Evaluate all of `e_i1`, ..., `e_im` (I believe this order is not specified, as for function calls in general)
  - Evaluate the constructor `C_i(v_i1, ..., v_im)` (where `v_ik` are the results of the previous step)

Every non-pure contract function call in the expression `e_ik` must be of a function defined in one of `C_0`, ..., `C_(i-1)`, that is only contracts that have been initialized at the point it is evaluated. (Note: this check can be relaxed somewhat but I decided to keep the simpler version.)


#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2020-12-03 16:13](https://github.com/ethereum/solidity/issues/10480#issuecomment-738110420):

We had a discussion on Gitter today about that, and I personally think that the current rule makes sense and is actually quite simple. Given the linearized hierarchy:
1) Evaluate all base call arguments in the order (most derived) -> (most base)
2) Construct all contracts (most base) -> (most derived)

This still accepts the possibility that you mentioned, of accessing a contract that hasn't been fully constructed yet. However, all state variables are initialized as 0 before the rules 1) and 2) above. Also, this still wouldn't prevent a contract depending on itself to be constructed:

```
contract Foo {
    event FooConstructor();
    event FooGetValue();
    
    uint value;
    
    constructor (uint _value) {
        emit FooConstructor();
        value = _value;
    }
    
    function getValue() public returns (uint) {
        emit FooGetValue();
        return value;
    }
}

contract Bar is Foo {
    event BarConstructor(uint value);
    uint valueBar;

    constructor (uint value) Foo(value + getValue()) {
        emit BarConstructor(value);
    }

    function g() internal view returns (uint) {
        return valueBar;
    }
}

contract Child is Bar {
    constructor () Bar(Bar.g()) {
    }
}
```
Here you need to evaluate `Foo(value + getValue())` before finishing constructing `Foo`, which then depends on already having visited `Bar(Bar.g())`.

So in my opinion the order is correct.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2020-12-04 01:31](https://github.com/ethereum/solidity/issues/10480#issuecomment-738494216):

>However, all state variables are initialized as 0 before the rules 1) and 2) above

Sure, we're not talking about uninitialized memory like on a regular computer program. Of course that would be a more serious issue. What I'm pointing out is an issue of language semantics.

A contract assigns a state variable in the constructor then exposes that state variable through a getter function: would anyone expect the getter to ever return anything other than the value assigned in the constructor? I don't think so. The intuition is that the constructor runs before anything else does. And it goes beyond reading uninitialized values, constructors can establish invariants that are assumed to hold for the execution of other functions in the contract. This evaluation order is violating that assumption, which is why I see this as a pretty serious issue.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2020-12-04 01:39](https://github.com/ethereum/solidity/issues/10480#issuecomment-738497777):


> Here you need to evaluate `Foo(value + getValue())` before finishing constructing `Foo`, which then depends on already having visited `Bar(Bar.g())`.

I don't think this code should be allowed. It doesn't make sense to call a function if its defining context hasn't been initialized.

Here's a similar example in JavaScript, which is disallowed:

```js
class A {
  constructor(value) {
    this.value = value;
  }

  getValue() {
    return this.value;
  }
}

class B extends A {
  constructor() {
    super(this.getValue()); // ReferenceError: Must call super constructor
                            // in derived class before accessing 'this'
  }
}

new B();

```

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2020-12-04 08:50](https://github.com/ethereum/solidity/issues/10480#issuecomment-738653448):

> I don't think this code should be allowed. It doesn't make sense to call a function if its defining context hasn't been initialized.

Ok, I wasn't sure if that's what you were suggesting. I can agree with that!

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2020-12-04 09:49](https://github.com/ethereum/solidity/issues/10480#issuecomment-738685949):

Funnily enough, that exact thing just bit me in https://github.com/ethereum/solidity/pull/10389

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-12-07 11:55](https://github.com/ethereum/solidity/issues/10480#issuecomment-739871628):

@frangio can you please edit the description and add a summary of the desired behaviour?

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2020-12-09 16:06](https://github.com/ethereum/solidity/issues/10480#issuecomment-741872433):

@chriseth I reorganized the issue description and added a specification of the desired behavior.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2021-02-01 13:45](https://github.com/ethereum/solidity/issues/10480#issuecomment-770867579):

I believe this issue is missing the `bug` label.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2021-02-01 14:09](https://github.com/ethereum/solidity/issues/10480#issuecomment-770883737):

It's not a bug, it was a design choice - maybe a bad one :)

#### <img src="https://avatars.githubusercontent.com/in/15368?v=4" width="50">[github-actions](https://github.com/apps/github-actions) commented at [2023-03-12 12:04](https://github.com/ethereum/solidity/issues/10480#issuecomment-1465177836):

This issue has been marked as stale due to inactivity for the last 90 days.
It will be automatically closed in 7 days.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2023-03-13 21:26](https://github.com/ethereum/solidity/issues/10480#issuecomment-1466986275):

I don't think this should be closed. The current behavior is unintuitive and I believe it should be changed in 0.9.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-03-14 14:10](https://github.com/ethereum/solidity/issues/10480#issuecomment-1468176252):

I don't think there is any natural and intuitive order of evaluation here - no matter what the actual order is, some people will expect the opposite. So I actually don't see that strong of a cause for changing this. I'll nonetheless mark this for discussion for 0.9 (preventing the stale bot from closing it).

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2023-03-15 13:58](https://github.com/ethereum/solidity/issues/10480#issuecomment-1470060783):

Thanks. There may not be a "natural" order of evaluation but if we look at what other languages do we may see a pattern and in that case I would argue Solidity should follow that pattern.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-03-15 18:26](https://github.com/ethereum/solidity/issues/10480#issuecomment-1470548145):

Looking at different languages you will find different conventions for this - what we do pretty much aligns for example with the evaluation order in C++ :-).

So yeah, a survey of more examples of different languages may help in deciding whether to actually change this for 0.9.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-03-15 18:29](https://github.com/ethereum/solidity/issues/10480#issuecomment-1470554494):

Actually, disallowing calls like this may be the better option than changing the evaluation order - I'd be open for that.
Especially since by 0.9 we may already have some degree of compile-time-constant-evaluation and could still *allow* calls that are compile-time constant (like a ``function f() internal returns (uint) { return 0x4242; }`` that has a value that is compile-time constant and thereby doesn't depend on the constructor having run already)

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2023-03-15 19:13](https://github.com/ethereum/solidity/issues/10480#issuecomment-1470651745):

> what we do pretty much aligns for example with the evaluation order in C++

I'm not sure this is true. I haven't written C++ in a while but here's what I put together:

```c++
#include <iostream>

class A {
public:
  A(int x) : x(x) {}
  int x;
};

class B {
public:
  B(int y) : y(y) {}
  int y;
};

class C : public A, public B {
public:
  C(int x) : A(x), B(this->x) {}
};

int main() {
    auto c = C(1);
    std::cout << c.y;
}
```

First, `A` is initialized according to `A(x)`, and then `B` is initialized according to `B(this->x)` where `x` has already taken on the value set in the constructor for `A`, so the resulting value for `y` is 1.

As far as I can tell this is what I'm saying should happen in Solidity, but what we're seeing instead in Solidity (analogously) is that `this->x` is evaluated before `A(x)` is evaluated and thus `this->x` evaluates to a "default value" of 0. In C++ this would probably be undefined behavior so if you switch around the order of inheritance to `C : public B, public A` you get a warning saying "base class A is uninitialized when used here to access A::x".

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-03-20 11:45](https://github.com/ethereum/solidity/issues/10480#issuecomment-1476078637):

Yeah, you're actually right there. I was thinking about cases like:
```
#include <iostream>

class A {
public:
  A(int x) : x(x) {}
  int x = 42;
  int f() { return x; }
};

class B: public A {
public:
  B(int y) : A(y), y(y) {}
  int y = 23;
};

class C : public B {
public:
  C(int x) : B(A::f()) {}
};

int main() {
    auto c = C(1);
    std::cout << c.y;
}
```
which is undefined behaviour - but your case actually has "well-defined" order.

Ok, I'm convinced that we should do something in any case. Although *changing* behaviour like this is also always problematic, so we can see once we get closer to 0.9, whether we want to focus more on changing or more on disallowing behaviour.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2023-03-20 13:47](https://github.com/ethereum/solidity/issues/10480#issuecomment-1476266936):

I see, yeah, I agree that your example is something that should be disallowed in Solidity as well.

> Although _changing_ behaviour like this is also always problematic

Yes, but I think this can be mitigated by introducing a warning some time before 0.9 in one of the 0.8 releases. Accessing "uninitialized" state definitely deserves a warning as far as I'm concerned.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
