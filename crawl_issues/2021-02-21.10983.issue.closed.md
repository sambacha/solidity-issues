# [\#10983 Issue](https://github.com/ethereum/solidity/issues/10983) `closed`: Compiles but Error: invalid bytecode when trying to deploy 

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) opened issue at [2021-02-21 09:01](https://github.com/ethereum/solidity/issues/10983):

## Description

I have one specific function which compiles fine but when I try to deploy the contract which it is on with `hardhat-deploy` I get an `Error: invalid bytecode` . 

## Environment

- Compiler version: 0.8.0
- Framework/IDE: hardhat
- EVM execution environment / backend / blockchain client: hardhat network
- Operating system: macOS 
 
## Steps to Reproduce
The contract is not public yet but I am trying to distill down the part that is causing the bug here. 

1. Compile `ExecFacet` contract with `hardhat compile` using `0.8.0` (solc-js) 
2. No compilation error. 
3. Try to deploy the `ExecFacet` contract with `hardhat-deploy` 
4. Get `Error: invalid bytecode`

![image](https://user-images.githubusercontent.com/36712489/108620046-7fb54780-7429-11eb-9183-36f04c69e83a.png)


```solidity
pragma solidity 0.8.0;

library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            // When the value to delete is the last one, the swap operation is unnecessary. However, since this occurs
            // so rarely, we still do the swap anyway to avoid the gas cost of adding an 'if' statement.

            bytes32 lastvalue = set._values[lastIndex];

            // Move the last value to the index where the value to delete is
            set._values[toDeleteIndex] = lastvalue;
            // Update the index for the moved value
            set._indexes[lastvalue] = toDeleteIndex + 1; // All indexes are 1-based

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value)
        private
        view
        returns (bool)
    {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index)
        private
        view
        returns (bytes32)
    {
        require(
            set._values.length > index,
            "EnumerableSet: index out of bounds"
        );
        return set._values[index];
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value)
        internal
        returns (bool)
    {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value)
        internal
        returns (bool)
    {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value)
        internal
        view
        returns (bool)
    {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index)
        internal
        view
        returns (bytes32)
    {
        return _at(set._inner, index);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value)
        internal
        returns (bool)
    {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value)
        internal
        returns (bool)
    {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value)
        internal
        view
        returns (bool)
    {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index)
        internal
        view
        returns (address)
    {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value)
        internal
        returns (bool)
    {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value)
        internal
        view
        returns (bool)
    {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index)
        internal
        view
        returns (uint256)
    {
        return uint256(_at(set._inner, index));
    }
}


library LibService {
    using EnumerableSet for EnumerableSet.AddressSet;
    using LibExecutor for address;
    using LibService for address;

    bytes32 private constant _SERVICE_STORAGE =
        keccak256("gelato.diamond.service.storage");

    struct ServiceStorage {
        EnumerableSet.AddressSet services;
        mapping(address => EnumerableSet.AddressSet) servicesByExecutor;
    }

    function isListed(address _service) internal view returns (bool) {
        return serviceStorage().services.contains(_service);
    }

    function isAcceptedByExecutor(address _service, address _executor)
        internal
        view
        returns (bool)
    {
        return
            serviceStorage().servicesByExecutor[_executor].contains(_service);
    }

    function serviceStorage()
        internal
        pure
        returns (ServiceStorage storage ss)
    {
        bytes32 position = _SERVICE_STORAGE;
        assembly {
            ss.slot := position
        }
    }
}


library LibExecutor {
    using LibService for address;

    function canExec(address _service, address _executor)
        public
        view
        returns (bool)
    {
        return _service.isListed() && _service.isAcceptedByExecutor(_executor);
    }
}

contract ExecFacet  {
   using LibExecutor for address;
    using LibService for address;

    function canExec(address _service, address _executor)
        public
        view
        returns (bool)
    {
        return _service.canExec(_executor);
    }
}
```

Now the interesting bit is that it compiles fine but doesnt deploy. 

However, when I get rid of `LibExecutor` and change the `canExec` code to this, it compiles and deploys. The `invalid bytecode` error is gone. 


```solidity
contract ExecFacet  {
    using LibService for address;

    function canExec(address _service, address _executor)
        public
        view
        returns (bool)
    {
        return _service.isListed() && _service.isAcceptedByExecutor(_executor);
    }
}
```

Tagging @wighawag who built `hardhat-deploy` . 


#### <img src="https://avatars.githubusercontent.com/u/790580?u=dfe626d9e2d28b0ebd8fce39f8331d8df272eb55&v=4" width="50">[wighawag](https://github.com/wighawag) commented at [2021-02-21 09:31](https://github.com/ethereum/solidity/issues/10983#issuecomment-782826998):

Not sure if it is about solidity. It looks like you are using libraries. if so you need to pass their address to the deploy function so it inject the address in the bytecode. 
The diamond feature (that you seem to use here) is not complete and I do not remember if there is library support for the facet. that might be the problem. Probably better to move that into hardhat-deploy, unless you have more reason to believe it is a solidity issue

EDIT: diamond deploy function actually support libraries, you might just not have added them

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2021-02-21 10:39](https://github.com/ethereum/solidity/issues/10983#issuecomment-782835724):

Exactly as @wighawag said, it's caused by the use of a library. If you compile your file with `solc --bin`, you'll see that the bytecode for `ExecFacet` looks like this:
```
======= contract.sol:ExecFacet =======
Binary:
608060405234801561001057600080fd5b5061029e806100206000396000f3fe608060405234801561001057600080fd5b506004361061002b5760003560e01c80632a36291f14610030575b600080fd5b61004a60048036038101906100459190610135565b610060565b60405161005791906101e1565b60405180910390f35b60008273ffffffffffffffffffffffffffffffffffffffff1673__$02fc603ed1f5b1296c4169c74e287e9021$__632a36291f9091846040518363ffffffff1660e01b81526004016100b39291906101b8565b60206040518083038186803b1580156100cb57600080fd5b505af41580156100df573d6000803e3d6000fd5b505050506040513d601f19601f820116820180604052508101906101039190610171565b905092915050565b60008135905061011a8161023a565b92915050565b60008151905061012f81610251565b92915050565b6000806040838503121561014857600080fd5b60006101568582860161010b565b92505060206101678582860161010b565b9150509250929050565b60006020828403121561018357600080fd5b600061019184828501610120565b91505092915050565b6101a3816101fc565b82525050565b6101b28161020e565b82525050565b60006040820190506101cd600083018561019a565b6101da602083018461019a565b9392505050565b60006020820190506101f660008301846101a9565b92915050565b60006102078261021a565b9050919050565b60008115159050919050565b600073ffffffffffffffffffffffffffffffffffffffff82169050919050565b610243816101fc565b811461024e57600080fd5b50565b61025a8161020e565b811461026557600080fd5b5056fea2646970667358221220e97f4bc81ea0ee155da8f688bcbdeaeffc63a3600d78a20c0aa1f0b58cc74e7d64736f6c63430008010033

// $02fc603ed1f5b1296c4169c74e287e9021$ -> contract.sol:LibExecutor
```

Note the `__$02fc603ed1f5b1296c4169c74e287e9021$__` placeholder in the middle of the bytecode. It means that that there is something in the code that requires the contract to know the address where the library is deployed. Normally, when you're only using internal functions from a library it does not need to get deployed at all because the compiler includes the code of internal functions in contract bytecode. In your case, however, `LibService.canExec()` is a public function which means that any calls from within a contract will be external (this is a bit different from how `public` works in contracts). This means that the library needs to get deployed first and then you need to pass the address where it was deployed to the compiler using the `--libraries` option.

In your case a simple fix would be just to make `LibService.canExec()` `internal` (unless you really want an external call here for some reason).

@wighawag To make this easier for users, maybe you could detect the placeholders (they have a constant length and structure, at least the new-style ones) and make the error message more specific?

#### <img src="https://avatars.githubusercontent.com/u/36712489?u=0e493d609d492c4fb0ff6cc117d1588214ff03bb&v=4" width="50">[gitpusha](https://github.com/gitpusha) commented at [2021-02-22 10:52](https://github.com/ethereum/solidity/issues/10983#issuecomment-783285056):

Thanks I marked the library function as `public` by mistake. Should be `internal` like all other library functions I use on there. Stupid mistake lol. @wighawag and @cameel thanks for the tips though. Next time I get an invalid bytecode I know what to check for.

#### <img src="https://avatars.githubusercontent.com/u/1455998?u=8c723081a00a8f83d63477e731e006552951c432&v=4" width="50">[jdbertron](https://github.com/jdbertron) commented at [2023-01-24 19:31](https://github.com/ethereum/solidity/issues/10983#issuecomment-1402481866):

Awesome. I had the same issue. Great explanations guys. 
@wighawag  Should the DiamondOptions libraries list take care of this somehow if the library is included ?

#### <img src="https://avatars.githubusercontent.com/u/1455998?u=8c723081a00a8f83d63477e731e006552951c432&v=4" width="50">[jdbertron](https://github.com/jdbertron) commented at [2023-01-24 19:38](https://github.com/ethereum/solidity/issues/10983#issuecomment-1402489606):

Hah, never mind. It works properly. The library name must match what the compiler expects. So if your contract needs a library named LibMyStorage (from the import statement) then the DiamondOptions library section must look like
    const myDiamondStorageLibrary = await deployments.deploy("LibMyStorage", {
      from: deployer,
    });
  
    const diamondOptions: DiamondOptions = {
      from: deployer,
      owner: diamondAdmin,
      libraries: {
        LibMyStorage: myDiamondStorageLibrary.address
      },
      facets: [
        'Test1Facet',
        'Test2Facet',
        'FacetThatNeedsLibMyStorage'
      ],
      defaultOwnershipFacet: true,
      defaultCutFacet: true,
    };

#### <img src="https://avatars.githubusercontent.com/u/110744338?v=4" width="50">[flo-3](https://github.com/flo-3) commented at [2023-07-24 17:52](https://github.com/ethereum/solidity/issues/10983#issuecomment-1648349266):

How to solve this issue if we actually want a public library to make the smart contract smaller?

I run into this problem in my nodejs js project when trying to deploy a smart contract that has a dependency to a public function in a library:
const contractFactory = new ethers.ContractFactory(
    NFTArtefact.abi,
    NFTArtefact.bytecode,
    wallet
  );


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
