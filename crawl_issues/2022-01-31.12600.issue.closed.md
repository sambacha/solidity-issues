# [\#12600 Issue](https://github.com/ethereum/solidity/issues/12600) `closed`: [Sol->Yul] Clean up of dirty storage bits in tuple assignment does not happen on inline assembly read
**Labels**: `bug :bug:`, `codegen error`


#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) opened issue at [2022-01-31 14:42](https://github.com/ethereum/solidity/issues/12600):

```
contract C {
  uint32[] s0;
  constructor(uint32[] memory i0) {
    s0 = i0;
    unchecked {
      (s0, s0[1], s0) = ([3], 4, [5]);
    }
  }
  function f() external returns (uint) {
    uint r;
    assembly {
      mstore(0, s0.slot)
      r := sload(keccak256(0, 32))
    }
    return r;
  }
  function g() external returns (uint32[] memory o0) { o0 = s0; }
}
// ====
// compileViaYul: also
// ----
// constructor(): 0x20, 2, 1, 2 ->
// gas irOptimized: 334331
// gas legacyOptimized: 222560
// f() -> 3
// g() -> 0x20, 1, 3
```

The isoltest call to `f()` via IR does not return `3` (instead returns `uint256` 0x00...0403 i.e., `4` placed at index 1 as if the tuple assignment in the constructor was not cleaned up).

Legacy call to `f()` returns `3`.

#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) commented at [2022-01-31 14:46](https://github.com/ethereum/solidity/issues/12600#issuecomment-1025821914):

This may be a false positive in the following sense

```
Note that access via inline assembly is not considered such an operation: If you use inline assembly to access Solidity variables shorter than 256 bits, the compiler does not guarantee that the value is properly cleaned up.
```

as documented here: https://docs.soliditylang.org/en/latest/internals/variable_cleanup.html

Nonetheless, it would be good if this could be confirmed by another pair of eyes :-)

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2022-02-07 11:17](https://github.com/ethereum/solidity/issues/12600#issuecomment-1031353120):

I don't think this is about cleanup - it is about the order of evaluation of the assignment to the storage variable.
For Yul, we kind of promise strict left-to-right evaluation, so 
```
(s0, s0[1], s0) = ([3], 4, [5]);
```
should be equivalent to
```
s0 = [3];
s0[1] = 4;
s0 = [5];
```
but it seems this is not the case.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2022-02-07 11:20](https://github.com/ethereum/solidity/issues/12600#issuecomment-1031355573):

I'm also wondering if `s0 = [5]` sets the size to 1.

#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) commented at [2022-02-07 11:21](https://github.com/ethereum/solidity/issues/12600#issuecomment-1031356671):

> I don't think this is about cleanup - it is about the order of evaluation of the assignment to the storage variable. For Yul, we kind of promise strict left-to-right evaluation, so
> 
> ```
> (s0, s0[1], s0) = ([3], 4, [5]);
> ```
> 
> should be equivalent to
> 
> ```
> s0 = [3];
> s0[1] = 4;
> s0 = [5];
> ```
> 
> but it seems this is not the case.

If it is an order-of-evaluation issue, I don't understand why a call to `f()` via legacy would return `3` (it should also have evaluated the second tuple expression in any case).

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2022-02-07 12:08](https://github.com/ethereum/solidity/issues/12600#issuecomment-1031396097):

Can you modify it to also return the length? I think in legacy, it just evaluates from right to left and thus the last assignment is `s0 = [3]`.

#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) commented at [2022-02-07 13:36](https://github.com/ethereum/solidity/issues/12600#issuecomment-1031477694):

> Can you modify it to also return the length? I think in legacy, it just evaluates from right to left and thus the last assignment is `s0 = [3]`.

The function `g()` in the original post returns the contents of the array by copying it to memory. There both legacy and IR return the same length (`1`) and content (`3`).

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2022-06-09 09:51](https://github.com/ethereum/solidity/issues/12600#issuecomment-1150914411):

Simpler reproduction without inline assembly:
```
contract C {
  uint32[] s;
  constructor()
  {
    s.push();
    s.push();
  }
  function f() external returns (uint)
  {
    (s, s[1], s) = ([0], 4, [0]);
    s.push();
    return s[1];
  }
}
```
Here ``f`` returns ``0`` in legacy code generation, but ``4`` via IR.
It's basically a case of "dangling storage references", since the left-hand-side of the assignment is evaluated to storage references left-to-right before the assignments happening, i.e. ``s[1]`` still refers to the second element of the array and writes to it, bypassing the length check, s.t. a subsequent increase in length using ``.push()`` reveals the dirty storage value...

I'm not quite sure, why legacy codegen does *not* have this issue, I'd expect both via-IR *and* legacy to suffer from this...

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2022-06-09 10:29](https://github.com/ethereum/solidity/issues/12600#issuecomment-1150951006):

Ok... the following has both legacy and via-IR produce a 4 on ``f()``:
```
contract C {
  uint32[] s;
  constructor()
  {
    s.push();
    s.push();
  }
  function f() external returns (uint)
  {
    (s[1], s) = (4, [0]);
    s.push();
    return s[1];
  }
}
```

If I add another memory-to-storage copy of a size-one-array after the tuple assignment, though, legacy seems to implicitly clear the dirty byte at the end, while via-IR keeps it, i.e. in the following:
```
contract C {
  uint32[] s;
  constructor()
  {
    s.push();
    s.push();
  }
  function f() external returns (uint)
  {
    (s[1], s) = (4, [0]);
    s = [0];
    s.push();
    return s[1];
  }
}
```
legacy produces 0, while via-IR produces 4.

I think what via-IR is missing is an equivalent of libsolidity/codegen/ArrayUtils.cpp:L284-L297 (at least in some cases).
https://github.com/ethereum/solidity/blob/9b220a20077b5a5e1c141560170ef7af1ae43978/libsolidity/codegen/ArrayUtils.cpp#L284-L297

That explains the difference. But actually, I don't see that much call for adding this to via-IR, so maybe we can just keep the difference here.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
