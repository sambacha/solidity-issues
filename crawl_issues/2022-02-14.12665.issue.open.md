# [\#12665 Issue](https://github.com/ethereum/solidity/issues/12665) `open`: Overhaul Inheritance and Override Logic
**Labels**: `language design :rage4:`, `annoys users :cry:`, `high effort`, `high impact`, `must have`


#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) opened issue at [2022-02-14 16:17](https://github.com/ethereum/solidity/issues/12665):

https://github.com/ethereum/solidity/issues/12615 reported a serious error in our override logic.
While https://github.com/ethereum/solidity/pull/12616 fixes the issue, we realized that our strict override logic has some counterintuitive implications, namely https://github.com/ethereum/solidity/pull/12616#discussion_r797992050 and https://github.com/ethereum/solidity/pull/12616#discussion_r797985635.

So we intend to relax our override requirements to avoid cases like that.

In particular, we want to have a specification with the following property:
 -   Each direct base of a contract can contribute at most one base function to the override requirements of a function.

In particular consider the following situation:
```
interface I { function f() external; }
contract A is I { function f() external {} }
contract B is I { }
contract C is A, B {
  // no override is required here!
}
contract D is C {
  // The following *should* be valid, but currently fails and requires "override(I, A)".
  function f() external override // more specifically "override(A)"
  {
  }
}
```

Our current plan is to relax the override requirements, s.t. ``D`` in the example becomes valid.
However, this is breaking (since currently specifying more bases than required is an error).
So more specifically, we want to do this change while allowing additional bases (with a warning) in 0.8, while those additional bases will become an error only in 0.8.

So this issue has two parts:

- [ ] Relax the override requirements while allowing the bases that were previously required with a warning (non-breaking).
- [ ] Disallow the additional bases we warned about in the next breaking release (breaking).



#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2022-06-14 20:13](https://github.com/ethereum/solidity/issues/12665#issuecomment-1155668027):

Perhaps #13136 should be a part or this too.

#### <img src="https://avatars.githubusercontent.com/u/735355?u=bbbfa1d147219ec846004033d2a645563c04ef36&v=4" width="50">[ZumZoom](https://github.com/ZumZoom) commented at [2022-10-05 12:44](https://github.com/ethereum/solidity/issues/12665#issuecomment-1268386647):

Here is a bit simpler example of the case when explicit interface overriding is unreasonably required:

```solidity
interface IERC20 {
    function balanceOf() external view returns(uint256);
}

contract ERC20 is IERC20 {
    function balanceOf() external view virtual returns(uint256) {
        return 0;
    }
}

interface IERC20Mintable is IERC20{
    function mint() external;
}

contract MintableERC20 is ERC20, IERC20Mintable {
    function mint() external {
        this;
    }

    function balanceOf() external view override returns(uint256) {
        return 1;
    }
}
```

`balanceOf` override fails with `Function needs to specify overridden contracts "ERC20" and "IERC20"`

#### <img src="https://avatars.githubusercontent.com/u/6491112?u=eee27d774752202954a1575a88a21864fdbd59d6&v=4" width="50">[naddison36](https://github.com/naddison36) commented at [2023-10-26 09:21](https://github.com/ethereum/solidity/issues/12665#issuecomment-1780736611):

Here's another example using an abstract contract

```Solidity
// SPDX-License-Identifier: AGPL-3.0-or-later
pragma solidity ^0.8.0;

abstract contract Base {
    function genericStuff() public {
        // does some common stuff

        // and some specific stuff
        specificStuff();
    }

    function specificStuff() public virtual;
}

abstract contract ImplOne {
    function specificStuff() public virtual {
        // does something specific
    }
}

abstract contract ImplTwo {
    function specificStuff() public virtual {
        // does something else
    }
}

contract Concrete1 is Base, ImplOne {
    // why is this necessary when only ImplOne.specificStuff is implemented
    // Base.specificStuff() is abstract
    function specificStuff() public override(Base, ImplOne) {
        ImplOne.specificStuff();
    }
}

```

The above example is not that bad when you only have one abstract function. But once you have more than a handful of abstract functions it becomes very verbose doing trivial overrides that the compiler could work out.

#### <img src="https://avatars.githubusercontent.com/u/6491112?u=eee27d774752202954a1575a88a21864fdbd59d6&v=4" width="50">[naddison36](https://github.com/naddison36) commented at [2023-10-26 09:48](https://github.com/ethereum/solidity/issues/12665#issuecomment-1780781330):

One way to avoid implementing overrides for every abstract function is to use a struct of function types. For example

```Solidity
// SPDX-License-Identifier: AGPL-3.0-or-later
pragma solidity ^0.8.0;

struct BaseFunctions {
    function() returns (uint256) specificStuff;
    function(uint256) someMoreStuff;
}

abstract contract Base {

    // Abstract function that must be implemented later
    function _getFunctions() internal virtual returns (BaseFunctions memory);

    function genericStuff() public {
        // does some common stuff

        // Get the implemented functions
        BaseFunctions memory baseFunctions = _getFunctions();
        // do some implementation specific stuff
        uint256 someAnswer = baseFunctions.specificStuff();
        // do more implementation specific stuff
        baseFunctions.someMoreStuff(someAnswer);
    }
}

abstract contract ImplOne {
    function _getFunctions() internal virtual returns (BaseFunctions memory) {
        return BaseFunctions({
            specificStuff: specificStuff,
            someMoreStuff: someMoreStuff
        });
    }

    function specificStuff() public returns (uint256) {
        // does something specific
        return 1;
    }

    function someMoreStuff(uint256) public {
        // does more implementation specific stuff
    }
}

abstract contract ImplTwo {
    function _getFunctions() internal virtual returns (BaseFunctions memory) {
        return BaseFunctions({
            specificStuff: specificStuff,
            someMoreStuff: someMoreStuff
        });
    }

    function specificStuff() public returns (uint256) {
        // does something specific
        return 2;
    }

    function someMoreStuff(uint256) public {
        // does more implementation specific stuff
    }
}

contract Concrete1 is Base, ImplOne {
    // Only requires one override function, not two
    function _getFunctions() internal override(Base, ImplOne) returns (BaseFunctions memory) {
        return ImplOne._getFunctions();
    }
}
```


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
