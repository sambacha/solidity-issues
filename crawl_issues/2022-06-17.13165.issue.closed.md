# [\#13165 Issue](https://github.com/ethereum/solidity/issues/13165) `closed`: Add unchecked versions of existing operators

#### <img src="https://avatars.githubusercontent.com/u/13470840?u=eafcee87241e0f3162f8ebe5f1dbfb335c11fa7e&v=4" width="50">[benesjan](https://github.com/benesjan) opened issue at [2022-06-17 11:30](https://github.com/ethereum/solidity/issues/13165):

## Abstract

There are situations in solidity ^0.8.0 where it can be assured that overflows and underflows are not a danger. In this scenarios devs can use the unchecked code block. I think it would improve code clarity if we add unchecked version of existing operators.

## Motivation
In order to save gas, devs are now using the following syntax:
```
        arrayLength = someArray;
        for (uint256 i; i < arrayLength; ) {
            // Do something
            unchecked {
                ++i;
            }
        }
```
I think it would be very helpful to add unchecked operators. We could for example add a question mark to all the existing relevant operators to define the unchecked version (e.g. ?++, ?+, ?==, ?--, ...)

Then the loop above would look for example ike this:

```
        arrayLength = someArray;
        for (uint256 i; i < arrayLength; ?++i) {
            // Do something
        }
```


## Specification
I am not educated enough in compilers to know whether the syntax I proposed would cause issues in parser so I leave the specification for after the initial feedback is received.

## Backwards Compatibility
Since we are talking about an addition of new operators, backward compatibility should not be affected in any way.


#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2022-06-17 12:30](https://github.com/ethereum/solidity/issues/13165#issuecomment-1158824818):

We're currently working on allowing to define (the regular) operators on user-defined value types (https://github.com/ethereum/solidity/issues/11969).

That way it will become possible to define a *type* with operations that are unchecked, so you would not declare ``uint256 i`` as loop index, but ``Index i`` with a user-defined value type ``Index`` that has an unchecked increment operator defined and is convertible to ``uint256``, but has no other operations, thus justifying its increment operation not being checked, since starting from zero, you cannot reach an 256-bit overflow by increments.

The advantage of this approach is that (1) user-defined types can be defined in such a way that overflows cannot happen by design, so the approach is safer than relying on case-by-case decisions when checked arithmetic is desirable and when it isn't and (2) it does not require extending the language with additional operators/keywords.

There is also some discussion on this in https://github.com/ethereum/solidity/issues/10698

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2022-06-17 12:48](https://github.com/ethereum/solidity/issues/13165#issuecomment-1158838352):

Note that in the most recent compiler version function ``f`` in this contract:
```
contract C {
        uint[] array;
        constructor() {
                for (uint i = 0; i < 20; ++i)
                        array.push(i);
        }
        function f() external returns (uint sum) {
                unchecked {
                    uint arrayLength = array.length;
                    for (uint i; i < arrayLength; ++i) {
                            sum += array[i];
                    }
                }

        }
}
```

has identical cost (using optimized via-IR compilation) to this:

```
contract C {
        uint[] array;
        constructor() {
                for (uint i = 0; i < 20; ++i)
                        array.push(i);
        }
        function f() external returns (uint sum) {
                uint arrayLength = array.length;
                for (Index i; less(i, arrayLength); i = increment(i)) {
                                // perform the summation in an unchecked block to get a fair comparison
                                unchecked {
                                        sum += array[toUint(i)];
                                }
                }
        }
}
```

Given that you define a user-defined value type ``Index`` like this:

```
type Index is uint256;

function increment(Index x) returns (Index) {
        unchecked {
                return Index.wrap(Index.unwrap(x) + 1);
        }
}

function less(Index i, uint256 x) returns (bool) {
        return Index.unwrap(i) < x;
}

function toUint(Index i) returns (uint256) {
        return Index.unwrap(i);
}
```

So the only thing missing to make this work nicely is to define nice and readable operators on the type ``Index`` instead of having to use less readable free functions. (i.e. https://github.com/ethereum/solidity/issues/11969)
Once we have those, we expect types like ``Index`` to be defined in common libraries (or we may add them to a compiler-shipped Solidity standard library).

The result of all that should cover the use cases of builtin unchecked versions of operators, while providing additional type-safety (e.g. in only performing unchecked increments on a type that is guaranteed to be bounded by design).

#### <img src="https://avatars.githubusercontent.com/u/13470840?u=eafcee87241e0f3162f8ebe5f1dbfb335c11fa7e&v=4" width="50">[benesjan](https://github.com/benesjan) commented at [2022-06-17 14:47](https://github.com/ethereum/solidity/issues/13165#issuecomment-1158946043):

Thank you for the thorough response! Happy it's already being addressed üëç


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
