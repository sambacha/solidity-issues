# [\#13221 Issue](https://github.com/ethereum/solidity/issues/13221) `closed`: Why hardcoded string and passed string is converted into bytes differently ??

#### <img src="https://avatars.githubusercontent.com/u/84004854?u=139759a6cbe3ec745380d9215339f3f4301837db&v=4" width="50">[amit-supraoracles](https://github.com/amit-supraoracles) opened issue at [2022-06-28 06:44](https://github.com/ethereum/solidity/issues/13221):

```// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;
 

contract Converter {

    // input : 0xcf5cc63dce433a32dc3d709d86dd77fb3b4d30fe5ec81dca735d5b0610d242bb
    // Output : false
    function compareBytes(bytes32 _input) public pure returns (bool){

        string memory str1 =  bytes32IntoString(_input);
        string memory str2 =  "cf5cc63dce433a32dc3d709d86dd77fb3b4d30fe5ec81dca735d5b0610d242bb";

        return keccak256(abi.encodePacked(str1)) == keccak256(abi.encodePacked(str2));

    }


     // Input : 0xcf5cc63dce433a32dc3d709d86dd77fb3b4d30fe5ec81dca735d5b0610d242bb
     // Output : "cf5cc63dce433a32dc3d709d86dd77fb3b4d30fe5ec81dca735d5b0610d242bb"
    function bytes32IntoString(bytes32 data) public pure returns (string memory) {
        bytes memory temp = new bytes(65);
        uint256 count;

        for (uint256 i = 0; i < 32; i++) {
            bytes1 currentByte = bytes1(data << (i * 8));
            uint8 c1 = uint8(
                bytes1((currentByte << 4) >> 4)
            );
            uint8 c2 = uint8(
                bytes1((currentByte >> 4))
            );
            if (c2 >= 0 && c2 <= 9) temp[++count] = bytes1(c2 + 48);
            else temp[++count] = bytes1(c2 + 87);

            if (c1 >= 0 && c1 <= 9) temp[++count] = bytes1(c1 + 48);
            else temp[++count] = bytes1(c1 + 87);
        }

        return string(temp);
    }

}```

#### <img src="https://avatars.githubusercontent.com/u/424752?u=2d50de05ec528b9b84f8b905a56e90669b0f8927&v=4" width="50">[Marenz](https://github.com/Marenz) commented at [2022-06-29 14:18](https://github.com/ethereum/solidity/issues/13221#issuecomment-1170041736):

Str1 seems to be `"\u0000cf5cc63dce433a32dc3d709d86dd77fb3b4d30fe5ec81dca735d5b0610d242bb"` after the call.
Can't say yet whether it's because of the code or the compiler.

#### <img src="https://avatars.githubusercontent.com/u/37188783?u=f347552ad58d12640eb67b711569f3f1e0e7755a&v=4" width="50">[wechman](https://github.com/wechman) commented at [2022-07-29 12:19](https://github.com/ethereum/solidity/issues/13221#issuecomment-1199212034):

Hi @amit-supraoracles! Thank you for the ticket! I went through your code and noticed defects in `bytes32IntoString` function. Well, firstly, the function allocates 65 bytes which is pretty unexpected taking into consideration that `str2` is only 64 bytes  long. And secondly, there is yet another defect at the end of the function. The thing is, you use preincrementation on variable that `temp` is being accessed with: `temp[++count]`. In result `temp[0]` is never assigned to, but still it is a part of the function result. That is why  `compareBytes` is not working as expected.

Here is the fixed code:


```
    function bytes32IntoString(bytes32 data) pure public returns (string memory) {
        bytes memory temp = new bytes(64);
        uint256 count;

        for (uint256 i = 0; i < 32; i++) {
            bytes1 currentByte = bytes1(data << (i * 8));
            
            uint8 c1 = uint8(
                bytes1((currentByte << 4) >> 4)
            );
            uint8 c2 = uint8(
                bytes1((currentByte >> 4))
            );

            if (c2 >= 0 && c2 <= 9) temp[count++] = bytes1(c2 + 48);
            else temp[count++] = bytes1(c2 + 87);

            if (c1 >= 0 && c1 <= 9) temp[count++] = bytes1(c1 + 48);
            else temp[count++] = bytes1(c1 + 87);
        }

        return string(temp);
    }
```
    
In conclusion, the problem is provided code, not the compiler. So that I am closing the ticket.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
