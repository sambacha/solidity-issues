# [\#13713 Issue](https://github.com/ethereum/solidity/issues/13713) `closed`: Cheap and efficient calldata access [discussion]

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) opened issue at [2022-11-17 10:51](https://github.com/ethereum/solidity/issues/13713):

We noticed that some calldata access cost hundreds of gas for almost no reason.

For example following 2 examples have 150 gas difference in cost:
```solidity
struct Thing {
    uint256 a;
    address b;
    uint256 c;
}

function getB(Thing calldata thing) external view returns(address) {
    return thing.b;
}
```

```solidity
struct Thing {
    uint256 a;
    uint256 b; // <= important
    uint256 c;
}

function getB(Thing calldata thing) external view returns(address) {
    return address(uint160(thing.b)); // 150 gas cheaper
}
```

## Motivation

This would dramatically decrease cost of the transactions. If some calldata expected to be `address`, let's read those 20 bytes and **ignore** 12 high bytes, not **check it and maybe revert**.

## Specification

I am not sure Solidity compiler put so many checks of calldata size and check high bits are zeroed. I would expect this access to `thing.b` be compiled into the following assembly:
```solidity
shr(96, calldataload(48))
```

This would make sure top bits are zeroed and will cost almost nothing.

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2022-11-17 10:58](https://github.com/ethereum/solidity/issues/13713#issuecomment-1318458220):

1inch team recent invention makes every `address` struct field access 150 gas cheaper:
```solidity
library CalldataLib {
     type Address is uint256;

     function get(Address account) internal pure returns (address) {
         return address(uint160(Address.unwrap(account)));
     }
}

contract MyContract {
    using CalldataLib for CalldataLib.Address;

    struct Thing {
        uint256 a;
        CalldataLib.Address b; // <= wow
        uint256 c;
    }

    function getB(Thing calldata thing) external view returns(address) {
        return thing.b.get(); // 150 gas cheaper
    }
}
```

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2022-11-18 11:28](https://github.com/ethereum/solidity/issues/13713#issuecomment-1319878559):

Is this the gas difference with legacy or IR codegen?

With IR the generated optimized code looks like this:

- `address`:
    ```yul
    let value := calldataload(36)
    let _3 := and(value, sub(shl(160, 1), 1))
    if iszero(eq(value, _3)) { revert(_2, _2) }
    mstore(_1, _3)
    ```
- `uint256`
    ```yul
    mstore(_1, and(calldataload(36), sub(shl(160, 1), 1)))
    ```

So basically, the `address` case has an extra `if` to check that the higher bits are not dirty. Other than that they're the same apart from having some extra intermediate variables.

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2022-11-25 21:12](https://github.com/ethereum/solidity/issues/13713#issuecomment-1327887869):

@cameel it seems it was legacy. IR codegen seems pretty much optimized for me.

#### <img src="https://avatars.githubusercontent.com/u/702124?u=00e20e1963ccc9a908a5826b2d8c3b1b1f6acea4&v=4" width="50">[k06a](https://github.com/k06a) commented at [2022-11-25 21:23](https://github.com/ethereum/solidity/issues/13713#issuecomment-1327891344):

@cameel is there any specific reason to not just ignore high bits of `address`? I thought smart contract should take care of data which was passed to it and can skip and ignore rest of the data.

BTW `iszero(eq(a,b))` can be optimized to `xor(a,b)` or `sub(a,b)` if it is not part of more complex expression, where I should aim this kind of suggestion? Is there any kind of set of abstract optimization rules?

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2022-11-25 21:59](https://github.com/ethereum/solidity/issues/13713#issuecomment-1327904731):

> is there any specific reason to not just ignore high bits of address? I thought smart contract should take care of data which was passed to it and can skip and ignore rest of the data.

AFAIK this was a part of a general push to make calldata validation more strict due to bugs/attacks. I remember for example [How to Find $10M Just by Reading the Blockchain](https://blog.golemproject.net/how-to-find-10m-just-by-reading-the-blockchain/), though this particular bug was not affected specifically by dirty bits.

The decision to do this was in #5844, though I don't see rationale there. Perhaps @chriseth or @axic can say more. 

> BTW `iszero(eq(a,b))` can be optimized to `xor(a,b)` or `sub(a,b)` if it is not part of more complex expression, where I should aim this kind of suggestion? Is there any kind of set of abstract optimization rules?

This is something that would probably go into [`RuleList.h`](https://github.com/ethereum/solidity/blob/develop/libevmasm/RuleList.h). Or the [peephole optimizer](https://github.com/ethereum/solidity/blob/develop/libevmasm/PeepholeOptimiser.cpp).

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2022-11-28 11:30](https://github.com/ethereum/solidity/issues/13713#issuecomment-1328924566):

The first focus for Solidity is correctness, gas is always second. If an address is encoded with higher order bits set, it is not an address, and that is what we are checking. People assume that the higher order bits are zero, not that they are ignored. You can for example use the hash of a set of arguments to a function to see if the function has already been called that way (for example if a voucher + address combination has already been used to claim something). If the compiler would just mask away the higher order bits, you could fool this mechanism.

We introduced user defined value types specifically to allow users to break out of these checks. The reasoning behind is that if you change the name of the type to explicitly state that the address is unchecked, it is visible to the user and auditor. I really like the example above, I would just not call the type `Address` but something like `SloppyAddress`. The same goes for overflow checks: With the hopefully soon available user defined operators, you can define your own integer type that has wrapping behaviour with arithmetic.

Concerning the optimizer: `iszero(eq(x, y))` is already optimized in the peephole optimizer, but it might be a good idea to introduce a flag for rules in RuleList.h that state that the rule can only be applied if the result is treated by something like `if` or `iszero`. New issue: https://github.com/ethereum/solidity/issues/13750
Thanks for the suggestion!

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2022-11-29 17:43](https://github.com/ethereum/solidity/issues/13713#issuecomment-1331036641):

@k06a I take it that the comments above explains the fact that the behaviour is very much intentional?

Independently of that, FYI, we're trying to move towards reserving github issues mainly for concrete discussions of implementations, resp. of items we have scheduled for implementation, while we'll be trying to move most discussion to the [Solidity forum](https://forum.soliditylang.org/).

In any case, I'm closing the issue given the explanations above - if you want to discuss this further, though, feel free to comment, reopen, or open a post in the forum.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
