# [\#13718 Issue](https://github.com/ethereum/solidity/issues/13718) `closed`: User defined operators/literals
**Labels**: `roadmap`, `epic`


#### <img src="https://avatars.githubusercontent.com/u/2582498?u=a1331723a724eb612a66f75abee3048448e2fe01&v=4" width="50">[NunoFilipeSantos](https://github.com/NunoFilipeSantos) opened issue at [2022-11-17 13:55](https://github.com/ethereum/solidity/issues/13718):

### What

Allow users to define custom operators for user-defined types and suffixes for converting literals to user-defined types.

### Why

To make the use of user-defined value types more ergonomic to solidity language users. This includes use cases like fixed-point numbers or special types for performing unchecked operations by default.

### How
#### Operators
- Main implementation
    - #12362 (the original PR by @wechman)
    - #13790
        - #13660
        - #13685
        - #13693
        - #13739
        - #13741
    - https://github.com/ethereum/solidity-blog/pull/202
- Bugs affecting/blocking the implementation
    - #13764
    - #13765
    - #13908
- Bugs in the implementation
    - #14016
    - #14001

#### Literal suffixes
- #12656
    - #13415 
    - #14015
    - #14036
    - #14054
    - #14066
    - #14078
    - #14087
    - #14103
    - #14104
    - #14105
    - #14121
    - #14118

#### Extras
- #13893
    - #13900
- [x] Documentation updates
    - [x] Docs for operators.
    - [x] Docs for suffixes.
    - ~Rewrite documentation on `using for` to make it easier to understand and more coherent.~
    - ~Add a canonical example of proper use of operators to introduction to smart contracts.~
- [ ] Refactors
    - #14124.
    - [ ] Store operator definitions in an annotation to avoid having to rescan the whole source unit on each check.

### Notes

### Out of scope

- Generalized AST annotation for function calls.
- Support for `!`, `<<`, `>>` and `**`.
- Support for increment, decrement and assignments.
- Automatically derived comparison operators (or spaceship operator).
- Operators on mixed types.
- Operators on structs and reference types.

---
### Resources

1. [User Defined Value Types in Solidity](https://blog.soliditylang.org/2021/09/27/user-defined-value-types/) (blog post introducing the original design)
1. [The Future of Using For](https://notes.ethereum.org/Quug7DmAQ3akyYIsdGH8FQ) (design notes)
1. [Literal suffixes - design decisions](https://notes.ethereum.org/62ZGU8OHQw-KimH7inZ-4w) (design notes)
1. #11969 (original issue+discussion)
1. [Units](https://notes.ethereum.org/XQGJdzHhSEeg6u2Ri-oEeQ) (design notes)
1. [Fixed-point math example](https://gist.github.com/chriseth/cd6d825df5a13055b1c5d7bcf1e614a7)
1. Older related issues:
    - #9211
    - #11531
    - #12273
1. Forum: https://forum.soliditylang.org/t/user-defined-types-and-operators/456
1. Forum: [[feature preview] User-defined operators](https://forum.soliditylang.org/t/feature-preview-user-defined-operators/1435)
1. [Feature Deep-Dive: User-Defined Operators](https://blog.soliditylang.org/2023/02/22/user-defined-operators/) (release blog post)

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2022-12-07 14:36](https://github.com/ethereum/solidity/issues/13718#issuecomment-1341058649):

Here's a summary of the current state of the operator implementation, in the form suitable for a short presentation:

### Operators currently
- Solidity supports the following operators:
    1. Bitwise: `&`, `|`, `^`, `<<`, `>>`, `~`
    2. Arithmetic: `+`, `-`, `*`, `/`, `%`, `**`
    3. Comparison: `==`, `!=`, `<`, `<=`, `>`, `>=`
    4. Boolean: `!`, `&&`, `||`
    5. Assignment: `=`, `+=`, `-=`, `^=`, `*=`, `/=`, `%=`, `&=`, `|=`, `^=`, `<<=`, `>>=`
    6. Increment/decrement: `++`, `--`
    7. Ternary: `?:`
    8. Other: `delete`, `new`
- Operators are built-in and cannot be redefined.
- There are no built-in operators that work on user-defined value types.

### Refresher on `using for`
#### Example
```solidity
type Int is int;

library L {
    function add(Int a, Int b) internal pure returns (Int) {
        return Int.wrap(Int.unwrap(a) + Int.unwrap(b));
    }
    function sub(Int a, Int b) internal pure returns (Int) {
        return Int.wrap(Int.unwrap(a) - Int.unwrap(b));
    }
}

function unsub(Int a) pure returns (Int) {
    return Int.wrap(-Int.unwrap(a));
}
function equals(Int a, Int b) pure returns (bool) {
    return Int.unwrap(a) == Int.unwrap(b);
}

using L for Int;
using {unsub} for Int;
using {equals} for Int global;

function test(Int a, Int b) pure returns (Int) {
    return a.equals(b) ? a.add(b) : a.unsub().sub(b);
}
```

#### Rules
- `using for` can attach a function to any type.
- Currently has 3 forms:
    1. `using L for T`
    2. `using L for *`
    3. `using {f, g, h} for T`.
- Can be used at file level and contract level (including libraries but not interfaces).
- The wildcard type can only be used with libraries.
    - It's not allowed for individual functions
    - Attaches all non-private functions of a library.
- The wildcard type allows attaching functions to types that cannot be named.
    - We're considering deprecating it but not before we find alternative solution for such types.
- The directive can be marked as `global`.
    - `global` can only be used at file level, with user-defined value types, structs and enums that are defined in the current source unit.
    - Global `using for` has effect on all source units where the user-defined value type is visible.

### User-defined operators
#### Example 1
```solidity
type Int is int;

library L {
    function add(Int a, Int b) internal pure returns (Int) {
        return Int.wrap(Int.unwrap(a) + Int.unwrap(b));
    }
    function sub(Int a, Int b) internal pure returns (Int) {
        return Int.wrap(Int.unwrap(a) + Int.unwrap(b));
    }
}

function unsub(Int a) pure returns (Int) {
    return Int.wrap(-Int.unwrap(a));
}
function equals(Int a, Int b) pure returns (bool) {
    return Int.unwrap(a) == Int.unwrap(b);
}

using {L.add as +, L.sub as -, unsub as -} for Int;
using {equals as ==} for Int global;

function test(Int a, Int b) pure returns (Int) {
    return a == b ? a + b : -a - b;
}
```

#### Example 2
```solidity
type UncheckedCounter is uint;

using {
    add as +,
    lt as <
} for UncheckedCounter;

UncheckedCounter constant ONE = UncheckedCounter.wrap(1);

function add(UncheckedCounter x, UncheckedCounter y) pure returns (UncheckedCounter) {
    unchecked {
        return UncheckedCounter.wrap(UncheckedCounter.unwrap(x) + UncheckedCounter.unwrap(y));
    }
}

function lt(UncheckedCounter x, UncheckedCounter y) pure returns (bool) {
    return UncheckedCounter.unwrap(x) < UncheckedCounter.unwrap(y);
}

contract C {
    uint internalCounter = 12;

    function testCounter() public returns (uint) {
        for (UncheckedCounter i = UncheckedCounter.wrap(12); i < UncheckedCounter.wrap(20); i = i + ONE) {
            ++internalCounter;
        }
        return internalCounter;
    }
}
```

#### User-definable operators
- The following operators can now be user-defined
    1. Bitwise: `&`, `|`, `^`, `<<`, `>>`, `~`
    2. Arithmetic: `+`, `-`, `*`, `/`, `%`, `**`
    3. Comparison: `==`, `!=`, `<`, `<=`, `>`, `>=`
    4. Boolean: `!`
- `&&` and `||` are not allowed for now due to their short-circuiting behavior.
- `++` and `--` would be useful but are even more complicated: both have prefix and postfix variants with different semantics and modify their argument.

#### Unary and binary operators
- Unary operators: `!`, `~`, `-`
- `-` is the only operator that's both unary and binary.
    - Note that unary `+` is not available in Solidity.
- All other operators are binary.

#### Supported types
- Operators can only be defined for user-defined value types for now.
    - All other built-in and user-defined types are disallowed, including structs and enums.
- Wildcard type is not allowed for operators.

#### Operator parameters and return values
- All parameters must be of the same type and this must be the type specified in `using for`.
- The return value must be of the same type as well, except for comparison operators, where it must be `bool`.
- There are no implicit conversions allowed.

#### Global operators
The rules for `using for global` with operators are the same as for bound functions:
- Only allowed at file level
- The type must be defined in the current source unit
- Effects extend to wherever the type is visible, not just current source unit.

#### Mutability
- Operators do not have to be `pure`. They can have side-effects, including making external calls.
    - NOTE: We're still considering changing this to disallow operators with side-effects.

#### What can be used as operator definition
##### Free functions and library functions can be used
- An operator definition can be a file-level function or a library function.
- Private library functions can only be used in `using for` within a library (but note bug #13764).
- External and public library functions can be used (but note bug #13765).
- Contract and interface functions are not allowed.
    - By extension, operator definition cannot be virtual and must have a body.

##### Overloaded functions cannot be used
```solidity
type A is address;
type AP is address payable;

function add(A, A) pure returns (A) {}
function add(AP, AP) pure returns (AP) {}

using {add as +} for A;  // Error: Identifier not found or not unique.
using {add as +} for AP;
```

This mostly due to the current language limitations (it's not possible to refer to a specific overload of a function).
The restriction may be lifted in the future.

##### Built-in functions cannot be used
- Operator definition cannot be a built-in function like `keccak256`, `revert`, `abi.encode()`.
- But a function shadowing a built-in is fine.

#### Operator definitions are not attached automatically
Using a function to define an operator does not automatically make it available as an attached function.
For that the funtion needs to be attached explicitly.

```solidity
type A is address;

using {add as +} for A;
using {sub as -, sub} for A;

function add(A a, A b) pure returns (A) { /* ... */ }
function sub(A a, A b) pure returns (A) { /* ... */ }

function test(A a) pure {
    a + a;
    a.add(a); // Error: Member "add" not found or not visible after argument-dependent lookup in A.

    a - a;
    a.sub(a); // OK
}
```

#### Multiple operator definitions
- It is not possible to redefine any built-in operators. All valid cases are the ones where previously there was no built-in operator.
- Only one definition is allowed
    - Exception: the binary and unary variant are considered two different operators so `-` can have two definitions.
- It's allowed to repeat the definition multiple times. Even in the same `using for` directive.
- Operators cannot be shadowed. If one is defined at file level, it cannot have a different definition inside a contract.
- `using for` is not inherited by derived contracts and this applies to operators as well.

### Operators on structs
We decided to limit the initial implementation to user-defined value types due to the planned future changes to reference types that may complicate design and require a breaking change to fix. In particular, location is going to become a part of the type itself in the future.

If enabled, operators on structs would work as described below.

#### Overloading on location
- Structs are reference types and have a location but `using for` does not include location.
- It's allowed to have multiple definitions with different locations as long as parameters and returns values in each one are identical.
- Storage and calldata arguments are probably not very useful but allowed.

##### Example
```solidity
using {
    addC as +,
    addM as +,
    addS as +
} for S;

struct S {
    uint v;
}

function addC(S calldata, S calldata) pure returns (S calldata) { /* ... */ }
function addM(S memory, S memory) pure returns (S memory) { /* ... */ }
function addS(S storage, S storage) returns (S storage) { /* ... */ }
```

#### No implicit conversions between location
- When calling the operator, the location of each operand must match one of the definitions. There is no implicit conversion.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2022-12-07 20:56](https://github.com/ethereum/solidity/issues/13718#issuecomment-1341585718):

@paulrberg @frangio Since the PR implementing user-defined operators is pretty much ready, this might be a good moment to get some early feedback about the implementation. Want to take a look? The comment above contains a high-level description of how they're going to work in practice, including current limitations.

You can also get a binary with support for operators from CI artifacts for [jobs on `user-defined-operators-for-udvt` branch](https://app.circleci.com/pipelines/github/ethereum/solidity?branch=user-defined-operators-for-udvt) (`b_ubu_static` for Linux, `b_win` for Windows, `b_osx` for macOS, `b_ems` for wasm). Not linking directly since the link would change whenever new code is pushed to the PR.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2022-12-07 21:53](https://github.com/ethereum/solidity/issues/13718#issuecomment-1341642838):

Some early thoughts after just reading your description, which was great.

Mutability: I'm curious how others feel, but I think operators that are not `view` or `pure` should be disallowed. Having to look out for side effects on operators is scary. Static calls could be risky too, but `pure` may be too much of a restriction.

Increment/Decrement: Seeing `i + ONE` in your example made me miss these operators. I think the difficulties that you mentioned can be managed. For example, the `++` operator could be defined as a "pure" function that returns the increment of the input value, and the compiler can take care of assigning the result to the input _variable_ at the appropriate time depending on whether the operator is used prefix or postfix. Have you considered this?

Structs: I'm not sure that operators for structs will be useful with the limitations around data location and the lack of automatic casting (which does make sense). I feel it may be better to drop support for struct operators entirely, until there are location-generic functions (#13207) and the operator could be defined in that way.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2022-12-08 15:44](https://github.com/ethereum/solidity/issues/13718#issuecomment-1342926337):

 @frangio Thanks, that's very useful feedback!

> I think operators that are not `view` or `pure` should be disallowed.

I'm with you on that and @ekpyron was leaning that way too when I brought that up yesterday. We're seriously considering forcing `view` or `pure` and requiring the definition to be non-external. Restricting it initially and relaxing later is easier because the later change would not be breaking.

As for me personally, I'd be completely fine with `pure` because anything else will be hell for auditing and reading code in general. I usually don't expect side-effects from operators when I'm reading code myself.

> Increment/Decrement: Seeing `i + ONE` in your example made me miss these operators.

Yeah, exactly why I included that example. But note that the literal suffixes will somewhat improve this. With them it would look like this:
```solidity
function cycles(uint c) pure suffix returns (UncheckedCounter) {
    return UncheckedCounter.wrap(c);
}
```
```solidity
for (UncheckedCounter i = 12 cycles; i < 20 cycles; i = i + 1 cycles)
    ++internalCounter;
```

Still more verbose than `++` but may be good enough for the initial implementation.

> I think the difficulties that you mentioned can be managed. For example, the `++` operator could be defined as a "pure" function that returns the increment of the input value, and the compiler can take care of assigning the result to the input _variable_ at the appropriate time depending on whether the operator is used prefix or postfix. Have you considered this?

Yeah, this sounds reasonable and we might do it like this. I don't see technical obstacles. It's really not as much about design issues as about keeping the implementation manageable. We wanted to start with something good enough and will be open to improving it after we get the main PR merged.

> Structs: I'm not sure that operators for structs will be useful with the limitations around data location and the lack of automatic casting (which does make sense). I feel it may be better to drop support for struct operators entirely, until there are location-generic functions (#13207) and the operator could be defined in that way.

Ok, that's a useful piece of feedback on their usefulness. We're leaning towards dropping them too. It's just that we have them already implemented and working so we can still decide to keep them if we hear good reasons for that.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-04-11 12:01](https://github.com/ethereum/solidity/issues/13718#issuecomment-1503203213):

Here's a summary of the current state of the literal suffix implementation:

### Denominations
Currently Solidity supports *denominations*, which superficially resemble the new feature:
- The following denomination suffixes are available:
    - [Ether units](https://docs.soliditylang.org/en/latest/units-and-global-variables.html#ether-units): `wei`, `gwei`, `ether`.
    - [Time units](https://docs.soliditylang.org/en/latest/units-and-global-variables.html#time-units): `seconds`, `minutes`, `hours`, `days`, `weeks`
- The following denominations used to be available, but have been deprecated:
    - `finney`, `szabo` (now usable as identifiers)
    - `years` (forbidden as identifier)
- Denomination suffixes are allowed only on rational number literals.
    - This includes fractional literals (`1.1 gwei` is valid).
    - This includes rational literals in scientific notation (`1e2 wei` is valid).
    - Note: Hexadecimal numbers in Solidity represent fixed-bytes literals or addresses (`0x01 wei` is invalid).
- Denominations do not affect the type.
    - Literal with a denomination is still the same kind of literal.
    - Operations on numbers with denominations are performed in unlimited precision, just as on bare literals.

### User-defined literal suffixes
#### Example
```solidity
type Length is uint;

function km(uint meters) pure suffix returns (Length) {
    return Length.wrap(meters * 1000);
}

struct Float {
    uint mantissa;
    uint exponent;
}

function f(uint mantissa, uint exponent) pure suffix returns (Float memory) {
    return Float(mantissa, exponent);
}

contract C {
    Length public length = 5000 km;
    Float public factor = 1.23 f;
}
```

#### Suffix definitions
- Must have the `suffix` modifier.
- Must be free functions.
- Must be pure.
    - Like all other pure functions, can perform pure external calls.
- Suffix names have the same restrictions as function names.
    - A suffix cannot have the same name as an existing denomination.
    - The deprecated `finney` and `szabo` are allowed as names. `years` is not.

#### Suffix call syntax
- Suffixes can be called in two ways:
    - Suffix call syntax (`123 suffix`).
    - Function call syntax (`suffix(123)`) - they're still functions.
- Suffix calls use the same syntax as numbers with denominations: literal followed by the name of the suffix.
- Suffix must be separated from the literal by whitespace.
    - The only exception are string literals, where whitespace is optional.
- The input of the suffix must be just a bare literal. Expressions are not allowed (e.g. `(123) suffix` is not valid).
    - String concatenation is not considered an expression. E.g. `"a" "b" unicode"c" suffix` is equivalent to `"abc" suffix` and allowed.
    - Note that there are no negative literals in Solidity. A number preceded by a minus is considered an expression.
        - The minus is applied to the result of the suffix call. `-123 suffix` is equivalent to `-(123 suffix)`.
        - A suffix definition will only ever receive positive numbers as input.
- Applying a suffix to a literal with a denomination is not allowed. `123 wei suffix` is invalid.
- The suffix itself is considered an expression but can have only two forms: identifier (`suffix`) or member access (`m.suffix`).
    - Note that currently the only situation where member access is possible is when `m` is a module.
    - Function pointers cannot be used as suffixes.
    - Builtin functions cannot be used as suffixes, even if the parameters and return values happen to match.

#### Suffix call semantics
- Application of a suffix is equivalent to a free function call.
    - The result is **not** considered a literal and further operations on it are **not** performed in unlimited precision.
- Semantics may change in future versions, when we introduce compile-time constant evaluation or nameable types for literals.

#### Parameters of a suffix function
- Only types that some literal can be implicitly converted to are allowed:
    - Unsigned integer types (`uint8`, ..., `uint256`, `uint`).
    - Signed integer types (`int8`, ..., `int256`, `int`).
    - Fixed bytes types (`bytes1`, ..., `bytes32`).
    - `address` (but not `address payable`).
    - `bool`.
    - `string` and `bytes`.
- Reference types stored in `calldata` or `storage` are not allowed.
- Must have one or two parameters.

##### Fractional decomposition
Definitions with two parameters are a special case:
- Can be used with any rational number literal, including fractional numbers.
- The compiler decomposes the number into the `mantissa * 10**-exponent` form, where
    - *mantissa* is an integer.
    - *exponent* is the smallest non-negative integer fitting the equation.
- The rules above make the decomposition unambiguous:
    - `123000` will always be decomposed into `123000 * 10**-0` never `123 * 10**3` or `123000000 * 10**-3`.
    - `1.23` will be decomposed into `123 * 10**-2` never `1.23 * 10**-0` or `123000 * 10**-5`.
- The compiler imposes the following restrictions on the suffix definition taking two arguments:
   - The first parameter receives the mantissa and can be of any integer type.
       - Note that in `-123` the sign is not considered a part of the literal. Therefore mantissa will never be negative in practice.
       - Signed types are still allowed as a way to limit the range of input or avoid explicit conversions.
   - The second parameter receives the exponent and can be of any unsigned integer type.
- Decomposition is performed only for suffix call syntax. In the standard call syntax two-parameter suffixes work like any other two-parameter function.

#### Return values of a suffix function
- Types with `calldata` or `storage` locations are not allowed.
- All other types are allowed.
- A suffix must return exactly one value.

#### Overloading
- Suffix functions can be overloaded.
- It is not possible to define two functions that differ only by presence of the `suffix` modifier.
- Overload resolution rules are the same as for normal functions with a single modification:
    - A two-argument suffix definition is considered a match for any rational number literal that fits its arguments after decomposition.
- As per usual overloading rules, if a suffix call matches multiple definitions, it is considered ambiguous and triggers a compilation error. For example:
    - `1 suffix` is considered ambiguous when both `suffix(uint)` and `suffix(uint, uint)` are defined.
    - `1024 suffix` is unambiguous when only `suffix(uint8)` and `suffix(uint, uint)` are defined. It only matches argument types of the second definition.

**NOTE: It is very likely that we'll decide to disallow overloading for suffix calls.** This would mean that:
- Defining overloaded suffixes would be allowed.
- Calling such suffixes via normal call syntax would be allowed.
- Calling such suffixes via suffix call syntax would **not** be allowed.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-04-14 15:05](https://github.com/ethereum/solidity/issues/13718#issuecomment-1508734005):

> - Documentation updates
>    - Rewrite documentation on `using for` to make it easier to understand and more coherent.
>    - Add a canonical example of proper use of operators to introduction to smart

I looked into adding that canonical example while working on #14121 but I actually don't see how to fit it nicely into one of the existing examples (except maybe the [subcurrency example](https://docs.soliditylang.org/en/latest/introduction-to-smart-contracts.html#subcurrency-example)?). I could cobble up something random, but I'm not sure we want that. We don't really have time to create a new well-thought out example right now, so I'm going to leave that as is unless anyone objects.

Similarly with the `using for` refactor. I think the docs on `using for` overall are not well structured and I'm really tempted to rewrite them, but maybe better leave that for some other time.

#### <img src="https://avatars.githubusercontent.com/u/2582498?u=a1331723a724eb612a66f75abee3048448e2fe01&v=4" width="50">[NunoFilipeSantos](https://github.com/NunoFilipeSantos) commented at [2023-04-18 11:59](https://github.com/ethereum/solidity/issues/13718#issuecomment-1512960535):

Sounds reasonable.
If we don't have an issue yet, create one and scope it out of this task.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-04-18 13:45](https://github.com/ethereum/solidity/issues/13718#issuecomment-1513188200):

Not sure if there's a point submitting an issue of this kind. It will end up getting closed as stale anyway.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-04-18 15:54](https://github.com/ethereum/solidity/issues/13718#issuecomment-1513412510):

You can consider all of this deprecated anyways (even though we're not yet at a place where we can officially call it like that), so there is really no point in brushing up the documentation around it :-).

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-05-17 14:20](https://github.com/ethereum/solidity/issues/13718#issuecomment-1551488965):

Decision from the design call today: we won't be releasing literal suffixes after all. The main reason is that the feature turned out not to be a good short-term solution and in the long term we don't see it as a part of the language at all.

In the short term, we can't optimize the code it generates on legacy codegen well enough to make it worthwhile as an alternative to just defining UDVT constants. The extra syntax sugar it adds for UDVT literals is also not enough of a reason on its own to keep it. In the long term our new type system will make it obsolete. 

This means that there's nothing more to implement here and I'm closing the issue (and the related PRs).

#### <img src="https://avatars.githubusercontent.com/u/2582498?u=a1331723a724eb612a66f75abee3048448e2fe01&v=4" width="50">[NunoFilipeSantos](https://github.com/NunoFilipeSantos) commented at [2023-05-17 14:31](https://github.com/ethereum/solidity/issues/13718#issuecomment-1551513635):

Also removed from the public roadmap to not induce users in error.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-05-17 14:34](https://github.com/ethereum/solidity/issues/13718#issuecomment-1551519182):

Well, the operator part was done, so I don't see a problem with having it there as completed.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
