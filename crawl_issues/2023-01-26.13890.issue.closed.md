# [\#13890 Issue](https://github.com/ethereum/solidity/issues/13890) `closed`: Support public functions in interfaces
**Labels**: `good first issue candidate`, `low effort`, `low impact`, `closed due inactivity`, `stale`


#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) opened issue at [2023-01-26 09:36](https://github.com/ethereum/solidity/issues/13890):

## Abstract

Currently, interface only allow functions declared as external. In the implementation, the function can be declared public to extend the scope. However sometimes complexify inheritance patterns (see Motivation bellow). By allowing functions to be declared as public in interfaces, we would improve the composability of contracts

## Motivation

Lets consider the following code

```solidity
interface ISomething {
    function doSomething(bytes32) external;
}

contract Something is ISomething {
    event gotSomething(address indexed sender, bytes32 value);

    function doSomething(bytes32 value) public override {
        emit gotSomething(msg.sender, value);
    }
}

contract SomethingElse is ISomething {
    bytes32[] public values;

    function doSomething(bytes32 value) public override {
        values.push(value);
    }
}

abstract contract SomethingExtra is ISomething {
    function doSomething(string calldata value) external {
        doSomething(keccak256(bytes(value)));
    }
}

contract Composed1 is Something, SomethingExtra {}
contract Composed2 is SomethingElse, SomethingExtra {}
```

This doesn't compile because `SomethingExtra.doSomething(string)` can't access `ISomething.doSomething(bytes32)` that is declared external. A simple fix here would be to do 

```diff
-        doSomething(keccak256(bytes(value)));
+        this.doSomething(keccak256(bytes(value)));
```

but when using Composed1, the "sender" of events triggered using the string variant would not be correct. Also, calling `this.doSomething` is possibly more expensive because of redirection gas cost when working with a proxy.

Another option is to do

```diff
-interface ISomething {
-    function doSomething(bytes32) external;
+abstract contract ISomething {
+    function doSomething(bytes32) public virtual;
```

and this would be seamless here, but from my experience, this often result in override specification nightmare, because the "interface" everything inherit from is actually not declared as an interface.

## Specification

Allow functions in interfaces to be declared as public. There function will have to be implemented publicly. Implementing them using an external function would be an error. Any contract that inherit the interface would be able to call that function. In particular, abstract contract that inherit the interface but don't implement the function would still be able to call it.

## Backwards Compatibility

This doesn't affect the code that currently compile as the behaviour of the compiler with external functions declared in interfaces.

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-26 15:28](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405182546):

To illustrate the nightmare it is to work with abstract contract, the following code doesn't compile without overriding `clock()` in `MyGovernor`

```solidity
interface IERC6372 {
    function clock() external view returns (uint48);
}

abstract contract IGovernor is IERC6372 {
    function clock() public view virtual override returns (uint48);
    function doSomethingWithClock() external virtual;
}

abstract contract Governor is IGovernor {
    function doSomethingWithClock() external virtual override {
        clock();
    }
}

contract ERC6372Timestamp is IERC6372 {
    function clock() public view virtual override returns (uint48) {
        return uint48(block.timestamp);
    }
}

// This doesn't compile without an override of `clock()`
contract MyGovernor is Governor, ERC6372Timestamp {
}
```

IMO, when there is only a single implementation in the inheritance tree, and other contract declare the function in a compatible way without implementing, the resolution should be a no-brainer and not require user to manually override.

---

If `clock()` was public in `IERC6372`, we would not need to declare it as public in `IGovernor` (which could be declared as interface), and everything should be ok

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-01-26 15:53](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405218045):

This sounds reasonable to me. Interfaces already have some elements that are not strictly necessary to describe the ABI of another contract (parameter names, parameters can be `calldata`) so adding this would not be a big precedent and looks like it would be a quality of life improvement. Implementation should be pretty simple too because it's just a change in the analysis.

#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2023-01-26 17:19](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405339870):

> This doesn't compile because SomethingExtra.doSomething(string) can't access ISomething.doSomething(bytes32) that is declared external. A simple fix here would be to do
>
>```diff
>-        doSomething(keccak256(bytes(value)));
>+        this.doSomething(keccak256(bytes(value)));
>```

Note: there is a big difference between the above change and the proposed addition of `public` keyword.

The `this.doSomething` is an external call, whereas the proposed change would be an internal one (`jump`).

---

Wouldn't you say it's clearer to write the following instead? @Amxx 

```solidity
  abstract contract SomethingExtra is ISomething {
      function doSomething(string calldata value) external {
          doSomething(keccak256(bytes(value)));
      }
      // The override is optional
      function doSomething(bytes32) public virtual override; 
  }
```

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-26 17:29](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405351765):

Yes, using `this.` is very different. I already pointed out that its not an acceptable option

> ```solidity
> abstract contract SomethingExtra is ISomething {
>       function doSomething(string calldata value) external {
>           doSomething(keccak256(bytes(value)));
>       }
>       // The override is optional
>       function doSomething(bytes32) public virtual override; 
>   }
> ```

as I said [here](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405182546), this causes override issues

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2023-01-26 17:33](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405356798):

> The `this.doSomething` is an external call, whereas the proposed change would be an internal one (`jump`).

Yes, this is indirectly mentioned in the issue description:

> the "sender" of events triggered using the string variant would not be correct. Also, calling `this.doSomething` is possibly more expensive because of redirection gas cost when working with a proxy.

---

> Wouldn't you say it's clearer to write the following instead?

This results in override errors.

What's implicit here is that interface functions are treated differently with respect to override requirements (https://github.com/ethereum/solidity/pull/11628). By allowing interface functions to be `public`, that special treatment is extended to internal functions as well, improving usability by requiring less manual disambiguating overrides.

As I've mentioned [before](https://forum.soliditylang.org/t/thoughts-on-override-a-b-syntax/767), we are desperate for ways to write our contracts so that they don't require developers to write overrides to manually disambiguate something, when the compiler could in theory do it for them without causing problems.

Allowing `public` in interfaces seems like an easy improvement in that direction.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-01-26 17:47](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405373261):

> > Wouldn't you say it's clearer to write the following instead?
> 
> This results in override errors.

I'd say this is the core problem here. We've planned to relax and overhaul the override requirements in cases like this in response to https://forum.soliditylang.org/t/thoughts-on-override-a-b-syntax/767 for quite a while, either by just doing https://github.com/ethereum/solidity/issues/12665 or even going beyond that - it's unfortunate that we haven't gotten around to this just yet, I'm sorry about that, but we've staged it as part of our roadmap for this year (https://github.com/ethereum/solidity/issues/13727).

I'd say that solving this properly on the level of fixing the override-error-logic is preferable to complicating the interface rules and allowing public interface functions.

Are we in agreement that fixing the override rules will solve this issue?

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-26 18:02](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405391807):

> Are we in agreement that fixing the override rules will solve this issue?

Somehow, but we would still need to define the function publicly somewhere, in addition to having it in the interface. Declaring it only once, publicly, high in the inheritance tree, would make code cleaner.

#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2023-01-26 18:05](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405395430):

@Amxx About the `MyGoverner` example, I feel that an automatic `clock` resolution doesn't make sense at a high level. There is an `external` function `clock` defined in `IERC6372`. It gets overridden in `IGovernor`. Another implementation gets `overriden` in `ERC6372Timestamp`. It doesn't feel right to automatically resolve the conflict in `MyGovernor`.

Inheritance is already far too complex that it should be. I'm uncertain if adding additional rules for this sub-category is the path we should take. I understand in this specific example, the automatic resolution may be the correct one. But I think the inheritance rules should be generalized, and it hurts the language to have separate rules for every single cases.

The following is how I would fix the code (even though I'm not a fan of the `override(...)` syntax):

```solidity
  abstract contract MyGovernor is Governor, ERC6372Timestamp {
      function clock() public view override(IGovernor, ERC6372Timestamp)  returns (uint48) {
          return ERC6372Timestamp.clock();
      }
  }
```

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-26 18:28](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405425004):

> About the `MyGoverner` example, I feel that an automatic `clock` resolution doesn't make sense at a high level. There is an `external` function `clock` defined in `IERC6372`. It gets overridden in `IGovernor`. Another implementation gets `overriden` in `ERC6372Timestamp`. It doesn't feel right to automatically resolve the conflict in `MyGovernor`.

This is my point. If it was public in `IERC6372`, we could call it from `Governor` without having to redeclare anywhere. It would just be implemented in `ERC6372Timestamp`.

It would look like this:

```solidity
interface IERC6372 {
    function clock() public view returns (uint48);
}

interface IGovernor is IERC6372 {
    function doSomethingWithClock() external;
}

abstract contract Governor is IGovernor {
    function doSomethingWithClock() external virtual override {
        clock();
    }
}

contract ERC6372Timestamp is IERC6372 {
    function clock() public view virtual override returns (uint48) {
        return uint48(block.timestamp);
    }
}

// I would expect that to be ok
contract MyGovernor is Governor, ERC6372Timestamp {
}
```

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-01-26 18:34](https://github.com/ethereum/solidity/issues/13890#issuecomment-1405431794):

> Somehow, but we would still need to define the function publicly somewhere, in addition to having it in the interface. Declaring it only once, publicly, high in the inheritance tree, would make code cleaner.

This makes interfaces less "pure" but personally I think that there are few downsides and the added usability is worth it. Having to introduce an intermediate abstract contract is a bit awkward.

Alternatively, maybe we should keep interfaces pure but instead introduce a new kind of abstract contract that also behaves like an interface and has features of both. The change proposed here is much simpler though.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-01-30 12:43](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408552217):

> > About the `MyGoverner` example, I feel that an automatic `clock` resolution doesn't make sense at a high level. There is an `external` function `clock` defined in `IERC6372`. It gets overridden in `IGovernor`. Another implementation gets `overriden` in `ERC6372Timestamp`. It doesn't feel right to automatically resolve the conflict in `MyGovernor`.
> 
> This is my point. If it was public in `IERC6372`, we could call it from `Governor` without having to redeclare anywhere. It would just be implemented in `ERC6372Timestamp`.
> 
> It would look like this:
> 
> ```solidity
> interface IERC6372 {
>     function clock() public view returns (uint48);
> }
> 
> interface IGovernor is IERC6372 {
>     function doSomethingWithClock() external;
> }
> 
> abstract contract Governor is IGovernor {
>     function doSomethingWithClock() external virtual override {
>         clock();
>     }
> }
> 
> contract ERC6372Timestamp is IERC6372 {
>     function clock() public view virtual override returns (uint48) {
>         return uint48(block.timestamp);
>     }
> }
> 
> // I would expect that to be ok
> contract MyGovernor is Governor, ERC6372Timestamp {
> }
> ```

What's stopping you from just using
```solidity
abstract contract IERC6372 {
     function clock() public view returns (uint48);
}
```
without redeclaring anything instead of making it an interface? The interface exceptions in the inheritance/override logic aside, which are broken and should be fixed.

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-30 13:20](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408621293):

> without redeclaring anything instead of making it an interface? The interface exceptions in the inheritance/override logic aside, which are broken and should be fixed.

- The interface exceptions 
- The fact that an interface cannot inherit an abstract contract

If I do `abstract contract IERC6372`, then I need to also do `abstract contract IGovernor is IERC6372`, and many of the interface exceptions that came from an interface are gone here too ... which floods all the downstream contracts (written by the user)

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-30 13:23](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408625834):

I would love to use
- `interface` for instances where everything is unimplemented (we say virtual pure in C++, but that translates poorly)
- `abstract contract` for instances where some functions are implemented, some are not
- `contract` for instances where everything is implemented.

Using `abstract contract` instead of `interface` would work, but its not clean to me.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-01-30 13:34](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408642168):

Ok, I see your position there - however, I'm not sure if enriching an interface which currently is purely an *external* interface with details about the internal implementations (which implementations being ``public`` is) - i.e. one could argue that anything that requires an implementation to be callable internally is no longer an (external) interface, but is an abstract contract (and that it's valid that this is hereditary)...
But yeah, would be nice to get more opinions on this.

But yeah, mixing concepts that better fit for an external-call view and for an internal-call view is generally a challenge, which never really perfectly fits the intuition from other contexts/languages in all cases... we have the same problem with the meaning of ``pure`` for example...

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-30 14:14](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408697588):

I follow you on everything. That is why I limited that to public function.

Technically, adding `internal` function to interfaces would improve inheritance discovery even more, but would add data about non-public facing functions in the interface, which IMO would be going too far. Abstract contract feels like the right abstraction here.

I'd like the interface to be the "pure" object we want them to be ... but then this purity goes against writing clean code because we need to re-declare things with another visibility and then resolve the conflicts we introduced. At that point I favor code readability and cleanness to absolute purity of interfaces.

Note that this purity is already broken by the "payable" keyword.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2023-01-30 14:47](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408752105):

An alternative to this proposal is to extend the override exception to all virtual unimplemented functions, even in abstract contracts. I think this would have the same effect, and additionally extend to internal functions. Personally that would be my preferred alternative.

But the point is we're looking for some improvement to the current state of things that does not require an overhaul.

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-30 15:00](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408780157):

> An alternative to this proposal is to extend the override exception to all virtual unimplemented functions, even in abstract contracts. I think this would have the same effect, and additionally extend to internal functions. Personally that would be my preferred alternative.

But then in the example above, would you declare `IERC6372` and `IGovernor` as abstract contract?

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2023-01-30 15:08](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408795492):

No, there would need to be some intermediate abstract contract, but my thinking is that it would not cause "override nightmare" (not 100% sure about it).

#### <img src="https://avatars.githubusercontent.com/u/2432299?v=4" width="50">[Amxx](https://github.com/Amxx) commented at [2023-01-30 15:18](https://github.com/ethereum/solidity/issues/13890#issuecomment-1408811801):

For resolution to work well, the abstract contract (that makes the function public) should be a common ancestor of `ERC6372Timestamp` and `Governor`, so that means it should be between `IERC6372` and `IGovernor`, so won't be able to make `IGovernor` an interface.

#### <img src="https://avatars.githubusercontent.com/u/97833681?v=4" width="50">[Coderayush13](https://github.com/Coderayush13) commented at [2023-02-09 15:38](https://github.com/ethereum/solidity/issues/13890#issuecomment-1424393397):

Hey can you assign this issue to me

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-02-09 19:42](https://github.com/ethereum/solidity/issues/13890#issuecomment-1424717740):

@Coderayush13 This already has a PR that implements it (#13896). The blocker here is just the design decision.

#### <img src="https://avatars.githubusercontent.com/in/15368?v=4" width="50">[github-actions](https://github.com/apps/github-actions) commented at [2023-05-11 12:04](https://github.com/ethereum/solidity/issues/13890#issuecomment-1543874777):

This issue has been marked as stale due to inactivity for the last 90 days.
It will be automatically closed in 7 days.

#### <img src="https://avatars.githubusercontent.com/in/15368?v=4" width="50">[github-actions](https://github.com/apps/github-actions) commented at [2023-05-19 12:04](https://github.com/ethereum/solidity/issues/13890#issuecomment-1554471621):

Hi everyone! This issue has been automatically closed due to inactivity.
If you think this issue is still relevant in the latest Solidity version and you have something to [contribute](https://docs.soliditylang.org/en/latest/contributing.html), feel free to reopen.
However, unless the issue is a concrete proposal that can be implemented, we recommend starting a language discussion on the [forum](https://forum.soliditylang.org) instead.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
