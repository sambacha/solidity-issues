# [\#14018 Issue](https://github.com/ethereum/solidity/issues/14018) `closed`: Data returned from staticcall does not contain first offset, for structs containing byte arrays

#### <img src="https://avatars.githubusercontent.com/u/27789784?u=9b17f3a96e56aedd0caa0e208847ec7eefe20371&v=4" width="50">[0xTycoon](https://github.com/0xTycoon) opened issue at [2023-02-28 15:56](https://github.com/ethereum/solidity/issues/14018):

## Description

We have a simple struct like so:

```solidity
struct Blocker {
        uint layer;
        bytes dataA;
        bytes dataB;
    }
```

Used in a mapping

```solidity
    mapping(bytes32 => Blocker) public blocks;
```

When accessing this mapping from another contract using staticcall, the call reverts, if `Blocker.layer > 0`

Upon investigation, it was found that the first 32 bytes does not contain the offset. [See more details here](https://github.com/0xTycoon/punk-blocks/issues/1).

## Environment

- Compiler version: 0.8.19
- Target EVM version (as per compiler settings): 0.8.19
- Framework/IDE (e.g. Truffle or Remix): Hardhat, also deployed test case & on mainnet & getting same issue on mainnet
- EVM execution environment / backend / blockchain client: geth
- Operating system: linux
- optimizations off or on

## Steps to Reproduce

Narrowed it down to the following test case.

See comment `Changing this 9 to a 0 will make doesntWork() return without error.` 

There are detailed observations & a test deployed to mainnet, [described here](https://github.com/0xTycoon/punk-blocks/issues/1)

```solidity
pragma solidity ^0.8.19;

contract Blocking {

    uint public nextId;

    struct Blocker {
        uint layer;
        bytes dataA;
        bytes dataB;
    }

    mapping(bytes32 => Blocker) public blocks;

    constructor() {
        Blocker storage b = blocks[bytes32(hex"9039da071f773e85254cbd0f99efa70230c4c11d63fce84323db9eca8e8ef283")];
        b = blocks[bytes32(hex"1885fe71e225eade934ab7040d533bd49efc5d66e8f2d4b5aa42477ae9892ec9")];
        b.layer = 9;
        b.dataA = hex"89504e470d0a1a0a0000000d4948445200000018000000180806000000e0773df80000004b4944415478da6262a03118b560d482510b869a0577efde6d18";
        nextId++;
        b = blocks[bytes32(hex"1885fe71e225eade934ab7040d533bd49efc5d66e8f2d4b5aa42477ae9892ec9")];
        // Changing this 9 to a 0 will make doesntWork() return without error.
        b.layer = 9;
        b.dataB = hex"89504e470d0a1a0a0000000d4948445200000018000000180806000000e0773df80000004b4944415478da6262a03118b560d482510b869a0577efde6d180da2116801cd010b057affe31067c4c921c5f063c78e6108ca4c4d60905b7a0bc55c465afb806134990eb80580000000ffff78ff0b44c51816510000000049454e44ae426082";
        nextId++;
        b = blocks[bytes32(hex"1bb61a688fea4953cb586baa1eadb220020829a1e284be38d2ea8fb996dd7286")];
        b.layer = 0;
        b.dataA = hex"89504e470d0a1a0a0000000d4948445200000018000000180403000000125920cb00000015504c5445000000000000713f1d8b532c5626007237094a120162eb383b0000000174524e530040e6d8660000004c4944415478da62a03160141414807384949414e112ca4a4a4a302946255c1c2115272517384731484914c61154c26380102e19b5343807c5390c42082d208b0419905c2d80c901040000ffff2f3c090f8ffce8ac0000000049454e44ae426082";
    }
}

contract TestBlocking {
    IPunkBlocks bl; // Blocking
    constructor(address _b) {
        bl = IPunkBlocks(_b);
    }
    function doesntWork() external view returns(IPunkBlocks.Block memory) {
        // This works if you call the Blocking.blocks(bytes32) method directly via web3
        // https://etherscan.io/address/0x58E90596C2065BEfD3060767736C829C18F3474c#readContract
        // 0x1885.. is a valid key, it exists.
        //  IPunkBlocks.Block memory test = pb.blocks(0x1885fe71e225eade934ab7040d533bd49efc5d66e8f2d4b5aa42477ae9892ec9);
        address addr = address(bl);
        (bool success, bytes memory retVal) = addr.staticcall(abi.encodeWithSignature("blocks(bytes32)", bytes32(0x1885fe71e225eade934ab7040d533bd49efc5d66e8f2d4b5aa42477ae9892ec9)));
        IPunkBlocks.Block memory blk = abi.decode(retVal, (IPunkBlocks.Block));
        return blk;
    }

    function works() external view returns(IPunkBlocks.Block memory) {
        address addr = address(bl);
        (bool success, bytes memory retVal) = addr.staticcall(abi.encodeWithSignature("blocks(bytes32)", bytes32(0x1BB61A688FEA4953CB586BAA1EADB220020829A1E284BE38D2EA8FB996DD7286)));
        IPunkBlocks.Block memory blk = abi.decode(retVal, (IPunkBlocks.Block)); // reverts here
        return blk;
    }

}

interface IPunkBlocks {
    struct Block {
        uint layer;
        bytes dataA;
        bytes dataB;
    }
    function blocks(bytes32) external view returns(Block memory);
}
```

#### <img src="https://avatars.githubusercontent.com/u/27789784?u=9b17f3a96e56aedd0caa0e208847ec7eefe20371&v=4" width="50">[0xTycoon](https://github.com/0xTycoon) commented at [2023-03-01 04:10](https://github.com/ethereum/solidity/issues/14018#issuecomment-1449309089):

fyi, Here is a workaround that fixes `doesntWork` 
It resizes retVal one slot back and puts an offset value of 0x20 at the beginning.

```solidity
function doesntWork2() external view returns(IPunkBlocks.Block memory) {
        // This works if you call the Blocking.blocks(bytes32) method directly via web3
        // https://etherscan.io/address/0x58E90596C2065BEfD3060767736C829C18F3474c#readContract
        // 0x1885.. is a valid key, it exists.
        //  IPunkBlocks.Block memory test = pb.blocks(0x1885fe71e225eade934ab7040d533bd49efc5d66e8f2d4b5aa42477ae9892ec9);
        address addr = address(bl);
        (bool success, bytes memory retVal) = addr.staticcall(abi.encodeWithSignature("blocks(bytes32)", bytes32(0x1885fe71e225eade934ab7040d533bd49efc5d66e8f2d4b5aa42477ae9892ec9)));
        // bugfix, prefix to retVal an offset with a value of "0x20"
        assembly {
            let len := mload(retVal)
            retVal := sub(retVal, 0x20)     // set a new start for retVal, going back 1 slot
            mstore(retVal, add(len, 0x20))  // set the new length, 1 slot longer
            mstore(add(retVal, 0x20), 0x20) // place an offset in first position
        }
        IPunkBlocks.Block memory blk = abi.decode(retVal, (IPunkBlocks.Block));
        return blk;
    }
```

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-03-21 13:06](https://github.com/ethereum/solidity/issues/14018#issuecomment-1477806119):

The autogenerated getters do *not* return structs, but the struct members as tuples, that's why you're not getting the encoding you're expecting. So this is not a bug, but weird, but expected behaviour.

I.e. to be more precise, what you get as a return value of ``blocks(bytes32)``, is not `(IPunkBlocks.Block)`, but `(uint256,bytes,bytes)`, so you can't directly decode to a `IPinkBlocks.Block`, but only to a tuple.

So another workaround would be to not rely on the weird behaviour of automatically generated getters in these cases, but to define a manual one like
```
    function getBlocks(bytes32 a) external returns (IPunkBlocks.Block memory) { return blocks[a]; }
```

Calling that you'll get something you can properly decode directly to the struct type.

The fact that getters return tuples not structs in cases like this is for historic reasons back from times where returning structs was impossible. We will probably change this eventually, but it's a tricky breaking change.

Given that, this is not actually a bug in Solidity, so I'm closing this issue - but feel free to reopen it, if you think this is an issue after all despite the explanation above!

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2023-03-21 13:10](https://github.com/ethereum/solidity/issues/14018#issuecomment-1477812825):

Maybe to make this a bit clearer still: you can also see this behaviour on high-level calls to the external getters:

This does *not* type-check:
```
Blocking bl;
IPunkBlocks.Block memory blk = bl.blocks(0);
```

Since the return type is in fact a tuple, so only the following works:
```
Blocking bl;
(uint a, bytes memory b, bytes memory c) = bl.blocks(0);
```

#### <img src="https://avatars.githubusercontent.com/u/27789784?u=9b17f3a96e56aedd0caa0e208847ec7eefe20371&v=4" width="50">[0xTycoon](https://github.com/0xTycoon) commented at [2023-03-23 05:55](https://github.com/ethereum/solidity/issues/14018#issuecomment-1480640410):

Thanks, this is useful information.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
