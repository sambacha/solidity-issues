# [\#14056 Issue](https://github.com/ethereum/solidity/issues/14056) `closed`: Costly keccak of parameter

#### <img src="https://avatars.githubusercontent.com/u/40341007?u=73a96d4874c3459748a0af8078fc8d1c5dde6a4b&v=4" width="50">[drortirosh](https://github.com/drortirosh) opened issue at [2023-03-19 13:07](https://github.com/ethereum/solidity/issues/14056):

Consider the following Remix sample.

keccak of `calldata` parameter takes  extra 150gas for no apparent reason. a simple assembly wrapper can be used to demonstrate getting the same result  for smaller cost.
This is a "low-hanging fruit" optimization. There are other places like this.

```
// SPDX-License-Identifier: GPL-3.0

pragma solidity >=0.7.0 <0.9.0;
import "hardhat/console.sol";

//deploy this contract to run the 2 tests
contract Asd {
    constructor() {
        bytes memory d = "A";
        new Test().work2(d);
        new Test().work1(d);
    }
}

contract Test {

    function calldataKeccak(bytes calldata data) internal pure returns (bytes32 ret) {
        assembly {
            let mem := mload(0x40)
            let len := data.length
            calldatacopy(mem, data.offset, len)
            ret := keccak256(mem, len)
        }
    }

    function work1(bytes calldata input) external view {
        uint g= gasleft();
        bytes32 data = keccak256(input);
        uint g1 = gasleft();
        console.logBytes32(data);
        console.log('gas used', g-g1);
    }

    function work2(bytes calldata input) external view {
        uint g= gasleft();
        bytes32 data = calldataKeccak(input);
        uint g1 = gasleft();
        console.logBytes32(data);
        console.log('gas used', g-g1);
    }

}

```

#### <img src="https://avatars.githubusercontent.com/u/457348?u=e02c93e6d98c1154952140a8d5af50d9d5ca59c9&v=4" width="50">[r0qs](https://github.com/r0qs) commented at [2023-03-24 11:42](https://github.com/ethereum/solidity/issues/14056#issuecomment-1482669518):

Hi @drortirosh thanks for reporting this. Please, consider the snippets below extracted from your example, ignoring the `console.log()` part:

- sol-keccak.sol
```solidity
contract Test {
    function run(bytes calldata data) external pure returns (bytes32 ret) {
        ret = keccak256(data);
    }
}
```

- yul-keccak.sol
```solidity
contract Test {
    function run(bytes calldata data) external pure returns (bytes32 ret) {
        assembly {
            let mem := mload(0x40)
            let len := data.length
            calldatacopy(mem, data.offset, len)
            ret := keccak256(mem, len)
        }
    }
}
```

```sh
forge debug --use 0.8.19 --via-ir --optimize sol-keccak.sol --sig "run(bytes)" 0x41
# uses 468 gas
forge debug --use 0.8.19 --via-ir --optimize yul-keccak.sol --sig "run(bytes)" 0x41
# uses 410 gas
```

If you run each of them using `--ir-optimized` compiler flag (e.g. `solc --ir-optimized sol-keccak.sol`), you will notice that the extra gas usage shown above comes from the fact that the Solidity code is doing proper memory allocation for the array and the assembly snippet is not. It does not move the free memory pointer or allocate a size field for the array.

This is an already know issue and it is similar to the following ones:

- https://github.com/ethereum/solidity/issues/12335
- https://github.com/ethereum/solidity/issues/13290
- https://github.com/ethereum/solidity/issues/13885

Optimizing this falls under https://github.com/ethereum/solidity/issues/13722 which is already in our roadmap. So, I will be closing this issue as duplicated. Please, feel free to reopen it if necessary.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
