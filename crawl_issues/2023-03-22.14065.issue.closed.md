# [\#14065 Issue](https://github.com/ethereum/solidity/issues/14065) `closed`: Expensive struct hashing.
**Labels**: `closed due inactivity`, `stale`


#### <img src="https://avatars.githubusercontent.com/u/40341007?u=73a96d4874c3459748a0af8078fc8d1c5dde6a4b&v=4" width="50">[drortirosh](https://github.com/drortirosh) opened issue at [2023-03-22 15:59](https://github.com/ethereum/solidity/issues/14065):

I need to create a unique hash for a Struct.
Using `keccak256(abi.encode())` on all 10 fields costs **1210** gas
As a comparison, hashing directly `msg.data` (which contains all fields data but also includes lengths, abi offsets and methodsig) costs **143** gas

I understand the compiler has some overheads (like validating the address range, and multiple keccak calls), but I don't think it warrants 8 times the gas price.

Remix example (the given struct to encode is taken from [erc4337](https://github.com/eth-infinitism/account-abstraction/blob/develop/contracts/interfaces/UserOperation.sol#L20)
```solidity
pragma solidity ^0.8.17;

// SPDX-License-Identifier: GPL-3.0
import 'hardhat/console.sol';

contract Test {

    struct UserOperation {
        address sender;
        uint256 nonce;
        bytes initCode;
        bytes callData;
        uint256 callGasLimit;
        uint256 verificationGasLimit;
        uint256 preVerificationGas;
        uint256 maxFeePerGas;
        uint256 maxPriorityFeePerGas;
        bytes paymasterAndData;
        bytes signature;
    }

    function testhash(UserOperation calldata u) external view {

        uint g = gasleft();
        calldata_keccak256(msg.data);
        uint g1 = gasleft();
        internalHash(u);
        uint g2 = gasleft();

        console.log("keccak msg.data", g-g1); // 143
        console.log("keccak fields", g1-g2); // 1210
    }

    function internalHash(UserOperation calldata u) internal pure returns (bytes32) {
        address sender = u.sender;
        uint nonce = u.nonce;
        uint callGasLimit = u.callGasLimit;
        uint verificationGasLimit = u.verificationGasLimit;
        uint preVerificationGas = u.preVerificationGas;
        uint maxFeePerGas = u.maxFeePerGas;
        uint maxPriorityFeePerGas = u.maxPriorityFeePerGas;

        return keccak256(abi.encode(
            sender, nonce, 
            calldata_keccak256(u.initCode),
            calldata_keccak256(u.callData),
            callGasLimit, verificationGasLimit, preVerificationGas,
            maxFeePerGas, maxPriorityFeePerGas,
            calldata_keccak256(u.paymasterAndData) 
        ));
    }

    function run() public view {
        UserOperation memory userOp = UserOperation(
            address(this),
            1,
            "initcode (if constructing a new account)",
            "calldata (methodsig and params)",
            4, 5, 6, 7, 8,
            "paymaster and data info",
            ""
        );
        this.testhash(userOp);
    }
    
    function calldata_keccak256(bytes calldata data) private pure returns (bytes32 ret) {
        assembly {
            let mem := mload(0x40)
            let len := data.length
            calldatacopy(mem, data.offset, len)
            ret := keccak256(mem, len)
        }
    }

}

contract Arun {
    constructor() {
        new Test().run();
    }
}
```

Partly-related issues:
- #14056 
-  #12104

#### <img src="https://avatars.githubusercontent.com/in/15368?v=4" width="50">[github-actions](https://github.com/apps/github-actions) commented at [2023-06-21 12:04](https://github.com/ethereum/solidity/issues/14065#issuecomment-1600711365):

This issue has been marked as stale due to inactivity for the last 90 days.
It will be automatically closed in 7 days.

#### <img src="https://avatars.githubusercontent.com/in/15368?v=4" width="50">[github-actions](https://github.com/apps/github-actions) commented at [2023-06-28 12:05](https://github.com/ethereum/solidity/issues/14065#issuecomment-1611280735):

Hi everyone! This issue has been automatically closed due to inactivity.
If you think this issue is still relevant in the latest Solidity version and you have something to [contribute](https://docs.soliditylang.org/en/latest/contributing.html), feel free to reopen.
However, unless the issue is a concrete proposal that can be implemented, we recommend starting a language discussion on the [forum](https://forum.soliditylang.org) instead.

#### <img src="https://avatars.githubusercontent.com/u/30870580?u=c117a61f164ea54547d8a10a74effe9e3f014914&v=4" width="50">[pegahcarter](https://github.com/pegahcarter) commented at [2024-08-19 21:43](https://github.com/ethereum/solidity/issues/14065#issuecomment-2297509282):

@drortirosh did you figure out a way around this?


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
