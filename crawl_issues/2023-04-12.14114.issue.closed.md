# [\#14114 Issue](https://github.com/ethereum/solidity/issues/14114) `closed`: Unexpected results are produced when fetching Struct across contracts
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/13927203?u=de25f6647a01fb69c84672f7fe364abea635e6f2&v=4" width="50">[nishuzumi](https://github.com/nishuzumi) opened issue at [2023-04-12 16:23](https://github.com/ethereum/solidity/issues/14114):

## Description
I encountered a very strange problem, unexpected results are produced when fetching Struct across contracts
## Environment

- Compiler version: 0.8.18
- Target EVM version (as per compiler settings):0.8.18+commit.87f61d96
- Framework/IDE (e.g. Truffle or Remix):Remix
- EVM execution environment / backend / blockchain client:Remix
- Operating system:Remix

## Steps to Reproduce

```
pragma solidity ^0.8.0;

interface IStorage{
    struct Filed{
        uint id;
        string name;
    }
}

contract Storage is IStorage {
    mapping(uint256 => Filed) public store;

    constructor(){
        store[1] = Filed(1,"Bot");
        store[0] = Filed(0,"Gun");
        store[2] = Filed(3,"Love");
    }
}

interface IStorageExtra is IStorage{
    function store(uint id) external view returns(Filed memory);
}

contract Query {
    IStorageExtra public storageContract;
    constructor(){
        storageContract = IStorageExtra(address(new Storage()));
    }

    function query() public view returns(Storage.Filed[] memory){
        uint max = 2;
        Storage.Filed[] memory data = new Storage.Filed[](max);
        for(uint i = 0; i < max ;i++){
            data[i] = storageContract.store(i);
        }
        return data;
    }

    function query2() public view returns(Storage.Filed[] memory){
        Storage.Filed[] memory data = new Storage.Filed[](1);
        data[0] = storageContract.store(2);
        return data;
    }

    function query3() public view returns(Storage.Filed memory){
        Storage.Filed memory data = storageContract.store(2);
        return data;
    }


    function query4OK() public view returns(Storage.Filed memory){
        Storage.Filed memory data = storageContract.store(0);
        return data;
    }

    function query5OK() public view returns(Storage.Filed[] memory){
        uint max = 1;
        Storage.Filed[] memory data = new Storage.Filed[](max);
        for(uint i = 0; i < max ;i++){
            data[i] = storageContract.store(i);
        }
        return data;
    }
}
```
I can deploy this code to run on Remix. When I execute the query method in Query, this line of code will give an error, where `query`, `query2`, `query3` will all give an error, while `query4`, `query5` will not give an error.


#### <img src="https://avatars.githubusercontent.com/u/29773064?u=9ebb6137c022ab37a64348e2511fe453e64e8156&v=4" width="50">[0xAWM](https://github.com/0xAWM) commented at [2023-04-13 02:11](https://github.com/ethereum/solidity/issues/14114#issuecomment-1506226071):

``` solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

contract Storage {
    struct Filed {
        uint256 id;
        string name;
    }

    mapping(uint256 => Filed) public store;

    function storeOK(uint256 _id) external view returns (Filed memory) {
        return store[_id];
    }
}

contract Query {
    Storage public storageContract;

    constructor() {
        storageContract = new Storage();
    }

    function query() public view {
        Storage.Filed memory errData = storageContract.store(0); // It will trigger an error from the compiler.
        Storage.Filed memory data = storageContract.storeOK(0); // fine
    }
}

```

```
TypeError: Different number of components on the left hand side (1) than on the right hand side (2).
  --> box.sol:25:9:
   |
25 |         Storage.Filed memory errData = storageContract.store(0);
   |         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

TypeError: Type uint256 is not implicitly convertible to expected type struct Storage.Filed memory.
  --> box.sol:25:9:
   |
25 |         Storage.Filed memory errData = storageContract.store(0);
   |         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
```

#### <img src="https://avatars.githubusercontent.com/u/14999583?u=ed3b3d5cd58c7825923647b5f7dbe122d0a2ca5e&v=4" width="50">[tonyke-bot](https://github.com/tonyke-bot) commented at [2023-04-13 02:26](https://github.com/ethereum/solidity/issues/14114#issuecomment-1506236057):

Seems like solc just flattens the Field returns from auto-generated storage getter. 

This works with @0xAWM 's min-repro.
```solidity
(uint256 id, string memory name) = storageContract.store(0); // This works
```

I noticed this behavior some time ago but thought it's a feature instead of a bug ðŸ˜„

#### <img src="https://avatars.githubusercontent.com/u/13927203?u=de25f6647a01fb69c84672f7fe364abea635e6f2&v=4" width="50">[nishuzumi](https://github.com/nishuzumi) commented at [2023-04-13 03:47](https://github.com/ethereum/solidity/issues/14114#issuecomment-1506296058):

> ```solidity
> // SPDX-License-Identifier: UNLICENSED
> pragma solidity ^0.8.13;
> 
> contract Storage {
>     struct Filed {
>         uint256 id;
>         string name;
>     }
> 
>     mapping(uint256 => Filed) public store;
> 
>     function storeOK(uint256 _id) external view returns (Filed memory) {
>         return store[_id];
>     }
> }
> 
> contract Query {
>     Storage public storageContract;
> 
>     constructor() {
>         storageContract = new Storage();
>     }
> 
>     function query() public view {
>         Storage.Filed memory errData = storageContract.store(0); // It will trigger an error from the compiler.
>         Storage.Filed memory data = storageContract.storeOK(0); // fine
>     }
> }
> ```
> 
> ```
> TypeError: Different number of components on the left hand side (1) than on the right hand side (2).
>   --> box.sol:25:9:
>    |
> 25 |         Storage.Filed memory errData = storageContract.store(0);
>    |         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
> 
> TypeError: Type uint256 is not implicitly convertible to expected type struct Storage.Filed memory.
>   --> box.sol:25:9:
>    |
> 25 |         Storage.Filed memory errData = storageContract.store(0);
>    |         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
> ```

Any best practice code tells us not to directly import contracts but to interface.
Second, this code works fine in a new case.
```
pragma solidity ^0.8.0;

interface IStorage{
    struct Filed{
        uint id;
        uint name;
    }
}

contract Storage is IStorage {
    mapping(uint256 => Filed) public store;

    constructor(){
        store[1] = Filed(1,1);
        store[0] = Filed(0,2);
        store[2] = Filed(3,4);
    }
}

interface IStorageExtra is IStorage{
    function store(uint id) external view returns(Filed memory);
}

contract Query {
    IStorageExtra public storageContract;
    constructor(){
        storageContract = IStorageExtra(address(new Storage()));
    }

    function query() public view returns(Storage.Filed[] memory){
        uint max = 2;
        Storage.Filed[] memory data = new Storage.Filed[](max);
        for(uint i = 0; i < max ;i++){
            data[i] = storageContract.store(i);
        }
        return data;
    }

    function query2() public view returns(Storage.Filed[] memory){
        Storage.Filed[] memory data = new Storage.Filed[](1);
        data[0] = storageContract.store(2);
        return data;
    }

    function query3() public view returns(Storage.Filed memory){
        Storage.Filed memory data = storageContract.store(2);
        return data;
    }


    function query4OK() public view returns(Storage.Filed memory){
        Storage.Filed memory data = storageContract.store(0);
        return data;
    }

    function query5OK() public view returns(Storage.Filed[] memory){
        uint max = 1;
        Storage.Filed[] memory data = new Storage.Filed[](max);
        for(uint i = 0; i < max ;i++){
            data[i] = storageContract.store(i);
        }
        return data;
    }
}
```
Now, all query is working well. @THaGKI9 @0xAWM

#### <img src="https://avatars.githubusercontent.com/u/457348?u=e02c93e6d98c1154952140a8d5af50d9d5ca59c9&v=4" width="50">[r0qs](https://github.com/r0qs) commented at [2023-04-13 11:57](https://github.com/ethereum/solidity/issues/14114#issuecomment-1506840013):

Hi @nishuzumi if I get correctly the main issue here, the problem is that the generated public getter returns the members of the struct as tuple instead of returning the struct itself. This is a know long-standing issue that is currently kept for compatibility reasons. Please see these comments here: https://github.com/ethereum/solidity/issues/6337#issuecomment-475170052 and https://github.com/ethereum/solidity/issues/14106#issuecomment-1503060644 for more information.

We don't have concrete plans to add support to it though. But we may consider it for the `0.9.0` release: https://github.com/ethereum/solidity/issues/6337#issuecomment-1140000320

In case you need to return the struct for your use case, you will need to define your own getter for `store` as in the example posted by @0xAWM: https://github.com/ethereum/solidity/issues/14114#issuecomment-1506226071.

So for now, I'm closing this issue. Please, feel free to reopen in case I misunderstood the problem and there is anything else to add.

Also, thanks @THaGKI9 and @0xAWM for the constructive discussions and examples :)


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
