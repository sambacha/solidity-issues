# [\#14134 Issue](https://github.com/ethereum/solidity/issues/14134) `closed`: [SMTChecker] Assumptions about the state of address(this).balance
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/58312089?u=5b60aca8b9483cd24c7f2cf160ca66f108605486&v=4" width="50">[fsainas](https://github.com/fsainas) opened issue at [2023-04-17 15:13](https://github.com/ethereum/solidity/issues/14134):

## Description

I was not able to prove that the total amount of ethers sent by the following contract is always less then or equal to the total amount deposited. Is there any reason why the property is not met, or is it an issue regarding the inability of smtchecker to guess the state of `address(this).balance` resulting in a false positive?

## Steps to Reproduce

```solidity
pragma solidity >= 0.8.2;

contract SimpleTransfer {

    uint sent;         // tracks the total amount sent outside the contract
    uint deposited;    // tracks the total amount deposited in the contract, including the balance at deployment time

    constructor () payable {
        deposited = address(this).balance;
    }

    function withdraw(uint _amount) public {
        require(_amount <= address(this).balance);
        sent += _amount;
        (bool succ,) = msg.sender.call{value: _amount}("");
        require(succ);
    }

    function invariant() public view {
        assert(sent <= deposited);
    }
}
```

This is the command I ran:
```
solc simple_transfer.sol --model-checker-engine chc --model-checker-timeout 0 --model-checker-targets "assert"
```

And this is the output:
```
Warning: CHC: Assertion violation happens here.
Counterexample:
sent = 1, deposited = 0

Transaction trace:
SimpleTransfer.constructor(){ msg.value: 0 }
State: sent = 0, deposited = 0
SimpleTransfer.withdraw(1){ msg.sender: 0x16dd }
    msg.sender.call{value: _amount}("") -- untrusted external call
State: sent = 1, deposited = 0
SimpleTransfer.invariant()
  --> simple_transfer_ext_call_no_balance.sol:21:9:
   |
21 |         assert(sent <= deposited);
   |         ^^^^^^^^^^^^^^^^^^^^^^^^^
```

## Notes
- By trusting external calls the property remains unverified, but no counterexample is returned.
- By adding a variable that tracks the contract balance smtchecker works as expected:
```solidity
pragma solidity >= 0.8.2;

contract SimpleTransfer {

    uint balance;
    uint sent;
    uint deposited;

    constructor () payable {
        balance = address(this).balance;
        deposited = balance;
    }

    function withdraw(uint _amount) public {
        require(_amount <= balance);
        sent += _amount;
        balance -= _amount;
        (bool succ,) = msg.sender.call{value: _amount}("");
        require(succ);
    }

    function invariant() public view {
        assert(sent <= deposited);   // verified
    }
}
```

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2023-04-18 11:30](https://github.com/ethereum/solidity/issues/14134#issuecomment-1512915698):

The counterexample is actually correct, here's what is happening:

Besides payable calls into the contract, there are two other ways to increase a contract's balance that can't be caught with code, those are 1) another contract calls `selfdestruct` with your contract as target, or 2) your contract's address is used as the `coinbase` of a block. Because of that, the CHC engine encodes the balance of a contract precisely plus the possibility of it increasing by a `k >= 0` between two external calls to the contract.

So what happens in the counterexample (which I agree should be mentioned there somehow) is that even though the contract's balance is `0` after deployment, it may increase before `withdraw` is called. When it does, the call to `withdraw(1)` succeeds, the contract sends 1 to `msg.sender`, and `sent` increases, even though `deposit` was 0.

In your second contract, the tracked `balance` doesn't take into account 1) and 2) above, that's why the property is true for that case.

#### <img src="https://avatars.githubusercontent.com/u/58312089?u=5b60aca8b9483cd24c7f2cf160ca66f108605486&v=4" width="50">[fsainas](https://github.com/fsainas) commented at [2023-04-18 15:00](https://github.com/ethereum/solidity/issues/14134#issuecomment-1513311686):

Thank you for your concise answer. You are right, mentioning it somehow would definitely make it clearer what might happen. Perhaps putting something like `selfdestruct(address(this)) { address(this).balance: 1 }` in the transaction trace.

Closing the issue!


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
