# [\#14212 Issue](https://github.com/ethereum/solidity/issues/14212) `closed`: Interface type check inconsistency during import

#### <img src="https://avatars.githubusercontent.com/u/25297591?u=3251a9019e72f66e32ba31d57729796ad505c530&v=4" width="50">[pcaversaccio](https://github.com/pcaversaccio) opened issue at [2023-05-10 18:24](https://github.com/ethereum/solidity/issues/14212):

## Description

Depending on how the same interface is imported (let's call it interface `A`), a function using a subset interface of `A` (let's call it interface `B`) is either compiled or thrown.

## Environment

- Compiler version: `0.8.19`
- Target EVM version (as per compiler settings): `paris`
- Framework/IDE (e.g. Truffle or Remix): `forge`
- Operating system: `ubuntu`

## Steps to Reproduce

So I define the following interface called `IERC20Extended`:

```solidity
// SPDX-License-Identifier: AGPL-3.0
pragma solidity ^0.8.0;

import {IERC20Metadata} from "../../lib/openzeppelin-contracts/contracts/token/ERC20/extensions/IERC20Metadata.sol"; // using this import does compile
// import {IERC20Metadata} from "./IERC20Metadata.sol"; // using this import does not compile

interface IERC20Extended is IERC20Metadata {
    function increaseAllowance(
        address spender,
        uint256 addedAmount
    ) external returns (bool);

    function decreaseAllowance(
        address spender,
        uint256 subtractedAmount
    ) external returns (bool);

    function burn(uint256 amount) external;

    function burnFrom(address owner, uint256 amount) external;

    function mint(address owner, uint256 amount) external;
}
```

I copy the OpenZeppelin interface `IERC20Metadata` locally but I also pull the OZ contracts via submodules. They are both exactly the same except that I have to adjust locally the path in `IERC20Metadata` from `import "../IERC20.sol";` to `import "./IERC20.sol";`.

So now I have a contract `C`:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.19;

import {IERC20Extended} from "./interfaces/IERC20Extended.sol";
import {IERC20, SafeERC20} from "../lib/openzeppelin-contracts/contracts/token/ERC20/utils/SafeERC20.sol";

contract C {
    using SafeERC20 for IERC20;
    IERC20Extended private DAI = IERC20Extended(0x6B175474E89094C44Da98b954EedeAC495271d0F);

    function run() external {
        approveAll(DAI, address(1));
    }

    function approveAll(IERC20 token, address to) public virtual {
        token.safeApprove(to, type(uint256).max);
    }
}
```

It's important to observe that `IERC20` is a subset of `IERC20Extended`. Now the following inconsistency happens:
- If I import the interface from the OZ submodules in `IERC20Extended` it compiles successfully,
- If I import the interface from the local directory in `IERC20Extended` it throws with the following (just run `forge compile`):
```bash
Compiler run failed
error[9553]: TypeError: Invalid type for argument in function call. Invalid implicit conversion from contract IERC20Extended to contract IERC20 requested.
  --> src/C.sol:12:20:
   |
12 |         approveAll(DAI, address(1));
   |                    ^^^
```

I created a repo [here](https://github.com/pcaversaccio/interface-solc-test) to reproduce this error. I tested it with other interfaces and experienced the same behavior. It looks like as if the import with long path resolution relaxes the type checking.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-05-11 16:15](https://github.com/ethereum/solidity/issues/14212#issuecomment-1544285801):

You're getting this error because you have two separate copies of `IERC20`, one in `src/interfaces/IERC20.sol`, the other in `lib/openzeppelin-contracts/contracts/interfaces/IERC20.sol`. To the compiler they're two distinct interfaces. Your `IERC20Metadata` uses one and OZ uses the other. When you try to cast your `IERC20Metadata` to OZ's `IERC20`, you get a conversion error because one is not derived from the other. It's derived from the other `IERC20`. The fact that they happen to have the same name and the same content, does not matter - the compiler sees two separate interface definitions in the AST. It's perfectly legal to have two different things with the same name in Solidity as long as they're not both imported into the same namespace and. The compiler does not attempt to "deduplicate" them in any way.

It's just the fact that the interfaces have the same name that makes the error message somewhat confusing. Also the fact that you never import both `IERC20` in the same file - you'd get a more explicit error then. If you just have them in the inheritance hierarchies of separate contracts, it works until they meet - then it doesn't :)

#### <img src="https://avatars.githubusercontent.com/u/25297591?u=3251a9019e72f66e32ba31d57729796ad505c530&v=4" width="50">[pcaversaccio](https://github.com/pcaversaccio) commented at [2023-05-11 16:39](https://github.com/ethereum/solidity/issues/14212#issuecomment-1544320542):

@cameel that makes perfect sense, thx for elaborating. One remaining question: Does Solidity always allow using interface casting of variables that are using a superset interface into a subset interface? Let me make a quick example:
- You have in interface `IERC20`
- You have an interface `IERC20Metadata` which inherits from `IERC20`.

Now there is a `public` function `f(IERC20 token)` in a contract `C` that imports `IERC20Metadata`. Via another `external` function you call `f` with a `token` argument that was cast into `IERC20Metadata` (and not `IERC20`). I think this should always be possible as long as the interface used is inherited from a parent interface that is used in the function. Am I correct here?

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-05-11 17:15](https://github.com/ethereum/solidity/issues/14212#issuecomment-1544385376):

You mean something like this?
```solidity
interface IERC20 {}
interface IERC20Metadata is IERC20 {}

contract C {
    function f(IERC20 token) public {}
    function g() external {
        IERC20Metadata token;
        this.f(token);
    }
}
```

Yes, there's always an implicit conversion from a contract/interface to any other contract/interface in its inheritance hierarchy. It being a parameter of an external function does not prevent you from doing it.

In fact, it could allow you to do more, since in the ABI contracts and interfaces have the same exact representation as the plain `address` type. A tool that did not have access to the declaration of `f()` might allow you to call it with any address. When you make a call from Solidity, though, the declaration must be available in some form so the compiler can check it. Of course, it depends on the declaration being correct - if it does not match what's deployed, that's another story :)

#### <img src="https://avatars.githubusercontent.com/u/25297591?u=3251a9019e72f66e32ba31d57729796ad505c530&v=4" width="50">[pcaversaccio](https://github.com/pcaversaccio) commented at [2023-05-11 18:25](https://github.com/ethereum/solidity/issues/14212#issuecomment-1544481053):

thanks for the insights!


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
