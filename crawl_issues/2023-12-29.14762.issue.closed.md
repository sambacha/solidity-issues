# [\#14762 Issue](https://github.com/ethereum/solidity/issues/14762) `closed`:  Incorrect reading of enum variable when sharing storage slot

#### <img src="https://avatars.githubusercontent.com/u/1125368?v=4" width="50">[nuliknol](https://github.com/nuliknol) opened issue at [2023-12-29 14:47](https://github.com/ethereum/solidity/issues/14762):

**Short description:**
When packed with another state variable in the same slot an enum-type variable reads 0 despite having different value.

**Solidity version:** 0.8.23

**How to reproduce:**

1. Create empty hardhat project
2. Put attached solidity files
3. Run the script 'scripts/deploybug.js'
4. Observe how the value of the variable is equal to 0 , this is incorrect, the value returned should be 2
5. Uncomment line 8 in ContractA.sol, comment line 7 in ContractA.sol , comment the constructor (lines 13,14,15) (i.e. remove the constrtuctor with Ownable super call), compile and run
6. Observe how the value of the variable is equal to 2 (the correct behavior, no bugs)

**Long description:**
Because when using `Ownable` in ContractA, the variable of the owner address is inherited from the parent contract.  At the same time we have the first variable (curType) and the owner address both packed into slot0 of the storage. The EVM stores and reads the data correctly, it is at Solidity side where the problem occurs, it fails to extract the value from the slot (i.e unpack the variable of enum type).

**Notes:** 
1. I don't know if this bug is DELEGATECALL-specific, I just had this setup in my project, so I reproduced the same use case with minimal code (maybe this bug will be present without DELEGATECALL , a test is needed )
2. I added return at the end of `doSomething()` so you can easily verify the bug for presence with `debug.traceTransaction()`

Here is the trace call with `hardhat-tracer` where you can observe that `doSomething()` returns 0  (first line) while the last SLOAD actually returns 02 (the rightmost byte at last line):


```
[CALL] ContractA(0x5fbdb2315678afecb367f032d93f642f64180aa3).doSomething() → (0)
   [SLOAD]  0x0000000000000000000000000000000000000000000000000000000000000001 → 0x000000000000000000000000e7f1725e7734ce288f8367e1bb143e90bb3f0512
   [DELEGATECALL] ContractB(0xe7f1725e7734ce288f8367e1bb143e90bb3f0512).executeSomething()
      [SLOAD]  0x0000000000000000000000000000000000000000000000000000000000000000 → 0x000000000000000000000000f39fd6e51aad88f6f4ce6ab8827279cfffb92266
      [SSTORE] 0x0000000000000000000000000000000000000000000000000000000000000000 ← 0x000000000000000000000000f39fd6e51aad88f6f4ce6ab8827279cfffb92202
   [SLOAD]  0x0000000000000000000000000000000000000000000000000000000000000000 → 0x000000000000000000000000f39fd6e51aad88f6f4ce6ab8827279cfffb92202
```
   
   For easier reproduction I have hardhat project already prepared at this repository, just download and run:
[ https://github.com/nuliknol/SolBug/tree/main]( https://github.com/nuliknol/SolBug/tree/main )

Files: 

**1. ContractA.sol**
```
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.23;
import { Ownable } from "@openzeppelin/contracts/access/Ownable.sol";
import { ContractConstants } from "./ContractConstants.sol";
import { ContractB } from "./ContractB.sol";

contract ContractA is Ownable {
//contract ContractA  {

    ContractConstants.MyType public curType;
	ContractB public executor;
	
	constructor() Ownable(msg.sender) {
		
	}
   
    function doSomething() public payable returns (ContractConstants.MyType) {

        (bool success,)=address(executor).delegatecall(abi.encodeWithSelector(ContractB.executeSomething.selector));
        require(success, "Call to ContractB failed.");
		return curType;
	}
    function setContractB(address addr) external{
        executor = ContractB(addr);
    }
}

```
2. **ContractB.sol**
```
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.23;
import { ContractConstants } from "./ContractConstants.sol";
import { ContractA } from "./ContractA.sol";

contract ContractB {

    ContractConstants.MyType public curType;
	ContractB public executor;

    function executeSomething() public payable {
		curType = ContractConstants.MyType.Two;
	}
}
```

3. **contracts/ContractConstants.sol**
```
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.23;

library ContractConstants {

    enum MyType {
        Zero,
        One,
        Two
    }
}
```

4 **scripts/deploybug.js**
```
const hre = require("hardhat");

async function main() {
    const ContractA = await hre.ethers.getContractFactory("ContractA");
    let cA = await ContractA.deploy();

    const ContractB = await hre.ethers.getContractFactory("ContractB");
    let cB = await ContractB.deploy();

	await cA.setContractB(cB.target);

	let tx = await cA.doSomething();
	let r = await tx.wait();
 
	let curType = await cA.curType();
	console.log("A address: "+cA.target);
	console.log("B address: "+cB.target);
	console.log("At the end , curType = "+curType);
}
main()
	.then(() => process.exit(0))
	.catch((error) => {
		console.error(error);
		process.exit(1);
	});
```

5 **hardhat.config.js**
```
/** @type import('hardhat/config').HardhatUserConfig */
require('@openzeppelin/hardhat-upgrades');
require("hardhat-tracer");
module.exports = {
  solidity: "0.8.23",
  networks: {
	localhost: {
		url : `http://localhost:8545/`,
		gasMultiplier: 4,
	}
  }

};
```

6. **package.json**
```
{
  "name": "hardhat-project",
  "devDependencies": {
    "@nomicfoundation/hardhat-ethers": "^3.0.5",
    "@openzeppelin/contracts": "^5.0.1",
    "@openzeppelin/hardhat-upgrades": "^3.0.1",
    "ethers": "^6.9.1"
  },
  "dependencies": {
    "hardhat-tracer": "^2.7.0"
  }
}
```




#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-12-30 16:43](https://github.com/ethereum/solidity/issues/14762#issuecomment-1872562594):

It's not a bug, The behavior you're showing is correct. I think your expectations about how delegatecall works are incorrect.

When you delegatecall `ContractB.executeSomething()` it will write to the location of its `curType` variable, which is slot 0. In a delegate call its storage is simply mapped to `ContractA`'s storage space (i.e. instead of reading/writing `ContractB`'s slot N, you read/write `ContractB`'s slot N). `ContractB`'s bytecode is executed on that remapped storage space. Variable names do not matter.

That's because when `ContractB` is compiled, all its bytecode knows is that it's supposed to write to a variable located in slot 0. The knowledge of what the variable was called is not preserved and it is not in any way remapped to match the location of a similarly named variable in `ContractA`. It just writes to slot 0, which, in `ContractA`'s storage space, happens to be variable holding the owner address.

#### <img src="https://avatars.githubusercontent.com/u/1125368?v=4" width="50">[nuliknol](https://github.com/nuliknol) commented at [2023-12-31 20:46](https://github.com/ethereum/solidity/issues/14762#issuecomment-1873035117):

@cameel could you please verify this one more time? I am not saying that the writing of the variable is incorrect, I am saying that the **READING** of the variable is incorrect (at Contract A, not Contract B) . Actually if you see the the contents of the slot 0 in the hardhat-tracer trace I posted, the variable (at slot 0) is holding the correct value (value = 2) , but when the output to the client is done, the value sent is 0.

> It just writes to slot 0, which, in ContractA's storage space, happens to be variable holding the owner address.

all writes are correct. I am saying that the "reading" is incorrect, this line here from the log I posted above:
[SLOAD]  0x0000000000000000000000000000000000000000000000000000000000000000 → 0x000000000000000000000000f39fd6e51aad88f6f4ce6ab8827279cfffb92202

means the ContractA have read the slot 0 , and there is the value 02 there. If it gets value = 02, why does it returns 0 instead of 2?
@cameel

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2023-12-31 21:57](https://github.com/ethereum/solidity/issues/14762#issuecomment-1873043793):

Well, I still think that the behavior is correct, but looks like I misinterpreted why you're expecting to see 2 there. You do correctly account for the behavior of delegatecall, but I think you're missing a small detail of how variables are laid out.

As stated in [Layout of State Variables in Storage](https://docs.soliditylang.org/en/latest/internals/layout_in_storage.html), the `curType` variable gets packed into a single slot with the inherited `owner` variable as you correctly observed. But the other relevant point is:

> - The first item in a storage slot is stored lower-order aligned.

I.e. variables in a storage slot are placed from right to left and the slot is padded on the left. Which means that in the layouts seen by `ContractA` and `ContractB` `curType` actually does end up in the same slot but at different offsets. Below is an illustration of how both contracts see the storage at the point when the value is being returned.

`ContractA`'s layout:
```
        ----------------------TTOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
slot 0  000000000000000000000000f39fd6e51aad88f6f4ce6ab8827279cfffb92202
        ------------------------EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE
slot 1  000000000000000000000000e7f1725e7734ce288f8367e1bb143e90bb3f0512
```

`ContractB`'s layout:
```
        ----------------------EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEETT
slot 0  000000000000000000000000f39fd6e51aad88f6f4ce6ab8827279cfffb92202
        ----------------------------------------------------------------
slot 1  000000000000000000000000e7f1725e7734ce288f8367e1bb143e90bb3f0512
```

In the above the `OO` bytes belong to `owner`, `TT` to `curType` and `EE` to `executor`. `--` bytes are unallocated space.

If you compile the contracts and look at the IR output (specifically `--ir-optimized --optimize`, unoptimized is more verbose, but still equivalent), you'll see that `ContractA` is not reading the rightmost byte and somehow returning it wrong. It's reading a byte that's to the left of `owner` and really has a value of 0:
```yul
let value_2 := and(shr(160, sload(0)), 0xff)
```

Let me know if you still think there's something wonky happening, but after looking at it, I'm still pretty confident that the generated code is behaving correctly.

#### <img src="https://avatars.githubusercontent.com/u/1125368?v=4" width="50">[nuliknol](https://github.com/nuliknol) commented at [2024-01-02 16:57](https://github.com/ethereum/solidity/issues/14762#issuecomment-1874288592):

@cameel yes, I concur (after checking everything) that this problem is due to storage addressing rather than a bug in solidity. When outputting the storage layout with `--storage-layout` option I got these storage locations:


```
              {
                "astId": 190, 
                "contract": "contracts/ContractA.sol:ContractA",
                "label": "curType",
                "offset": 20,
                "slot": "0", 
                "type": "t_enum(MyType)272"
              },
```

and for ContractB:

```
              {
                "astId": 252,
                "contract": "contracts/ContractB.sol:ContractB",
                "label": "curType",
                "offset": 0,
                "slot": "0",
                "type": "t_enum(MyType)272"
              },
```

the slots are the same but offsets are different. Thank you very much for the help.

Maybe in future versions of Solidity we could add a warning like "you are using the same label of the same type within different contract files but these variables are not the same in the physical storage" , or something like that. (when delegateCall is made).

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2024-01-02 17:34](https://github.com/ethereum/solidity/issues/14762#issuecomment-1874332555):

> Maybe in future versions of Solidity we could add a warning like "you are using the same label of the same type within different contract files but these variables are not the same in the physical storage" , or something like that. (when delegateCall is made).

I don't think that's ever going to happen with the current syntax. There are very few ways in which names in the language actually matter (basically just selectors). Automatically making this kind of implicit assumption for any delegatecall goes hard against the direction in which the language is heading.

The only way I see something like that making sense is if we introduced some syntax to make that expectation explicit. E.g. by somehow marking a variable declaration in two contracts as occupying the same location. Actually, this sounds related to the often requested feature of explicitly specifying storage locations (#597). If we had that, you could just set the same location for both variables. And, if we allowed using not just absolute location but also relative to some other variable (potentially from another contract), it would cover this use case. I'm not sure this is a good idea, but technically, this could work. You could try to propose it in that issue and see what the general sentiment about that is. I suspect it would be a pretty niche thing though.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
