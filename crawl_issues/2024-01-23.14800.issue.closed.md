# [\#14800 Issue](https://github.com/ethereum/solidity/issues/14800) `closed`: solc produce Different Bytcode in multiple contracts after an equivalent expression change
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/36297330?u=ec716a9bef32d6f777e73680d0a24a39760696a8&v=4" width="50">[chamitro](https://github.com/chamitro) opened issue at [2024-01-23 16:26](https://github.com/ethereum/solidity/issues/14800):

<!--## Prerequisites

- First, many thanks for taking part in the community. We really appreciate that.
- We realize there is a lot of information requested here. We ask only that you do your best to provide as much information as possible so we can better help you.
- Support questions are better asked in one of the following locations:
    - [Solidity chat](https://gitter.im/ethereum/solidity)
    - [Stack Overflow](https://ethereum.stackexchange.com/)
- Ensure the issue isn't already reported.
- The issue should be reproducible with the latest solidity version; however, this isn't a hard requirement and being reproducible with an older version is sufficient.

*Delete the above section and the instructions in the sections below before submitting*
-->

## Description

I have 2 versions of the smart contract below. The only difference is in line: 
 ```solidity
 string private _name = "ManofDevelopingIndia";
 ```
 which changes to :
```solidity
string private _name = "Man" "of" "Developing" "India";
```
These 2 expressions are equivalent according to the solidity documentation (https://docs.soliditylang.org/en/latest/types.html#integers).

So, when I compile to bytecode the 2 contracts (using the command solc --bin standard.sol and solc --bin standard2.sol), there are many differences in the produced bytecode. Not only in the ManofDevelopingIndia Contract but in other libraries like SafeMath and Utils.
While the IERC20, IDexRouter, IDexFactory and Context bytecode is the same. 

My question is that. Should the bytecode be the same as long the two expressions are equivalent and there are no optimizations? And if not why there are changes in SafeMath and Utils bytecode apart from the MAnOdDevelopingIndia, while the 
```solidity
string private _name
```
has no correlation to them.

The contract is the one below:

```solidity

// SPDX-License-Identifier:MIT
pragma solidity ^0.8.23;

interface IERC20 {
    function totalSupply() external view returns (uint256);

    function balanceOf(address account) external view returns (uint256);

    function transfer(address recipient, uint256 amount)
        external
        returns (bool);

    function allowance(address owner, address spender)
        external
        view
        returns (uint256);

    function approve(address spender, uint256 amount) external returns (bool);

    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) external returns (bool);

    event Transfer(address indexed from, address indexed to, uint256 value);

    event Approval(
        address indexed owner,
        address indexed spender,
        uint256 value
    );
}

// Dex Factory contract interface
interface IDexFactory {
    function createPair(address tokenA, address tokenB)
        external
        returns (address pair);
}

// Dex Router contract interface
interface IDexRouter {
    function factory() external pure returns (address);

    function WETH() external pure returns (address);

    function addLiquidityETH(
        address token,
        uint256 amountTokenDesired,
        uint256 amountTokenMin,
        uint256 amountETHMin,
        address to,
        uint256 deadline
    )
        external
        payable
        returns (
            uint256 amountToken,
            uint256 amountETH,
            uint256 liquidity
        );

    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external;
}

abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return payable(msg.sender);
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(
        address indexed previousOwner,
        address indexed newOwner
    );

    constructor() {
        _owner = _msgSender();
        emit OwnershipTransferred(address(0), _owner);
    }

    function owner() public view returns (address) {
        return _owner;
    }

    modifier onlyOwner() {
        require(_owner == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = payable(address(0));
    }

    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(
            newOwner != address(0),
            "Ownable: new owner is the zero address"
        );
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}

contract ManofDevelopingIndia is Context, IERC20, Ownable {
    using SafeMath for uint256;

    mapping(address => uint256) private _balances;
    mapping(address => mapping(address => uint256)) private _allowances;

    mapping(address => bool) public isExcludedFromFee;
    mapping(address => bool) public isExcludedFromMaxTxn;
    mapping(address => bool) public isExcludedFromMaxHolding;
    mapping(address => bool) public isBot;

    string private _name = "ManofDevelopingIndia";
    string private _symbol = "MODI";
    uint8 private _decimals = 9;
    uint256 private _totalSupply = 1_400_000_000 * 1e9;

    address private constant DEAD = address(0xdead);
    address private constant ZERO = address(0);
    IDexRouter public dexRouter;
    address public dexPair;
    address public marketingWallet;
    address public DevelopmentWallet;
    address public liquidityReceiverWallet;

    uint256 public minTokenToSwap = _totalSupply.div(1e5); // this amount will trigger swap and distribute
    uint256 public maxHoldLimit = _totalSupply.mul(30).div(1000); // this is the max wallet holding limit
    uint256 public maxTxnLimit = _totalSupply.mul(20).div(1000); // this is the max transaction limit
    uint256 public percentDivider = 1000;
    uint256 public snipingTime = 30 seconds;
    uint256 public launchedAt;

    bool public distributeAndLiquifyStatus; // should be true to turn on to liquidate the pool
    bool public feesStatus = true; // enable by default
    bool public trading; // once enable can't be disable afterwards

    uint256 public liquidityFeeOnBuying = 10; // 1% will be added to the liquidity
    uint256 public marketingFeeOnBuying = 10; // 1% will be added to the marketing address
    uint256 public DevelopmentFeeOnBuying = 10; // 1% will be added to the Development address

    uint256 public liquidityFeeOnSelling = 10; // 1% will be added to the liquidity
    uint256 public marketingFeeOnSelling = 10; // 1% will be added to the marketing address
    uint256 public DevelopmentFeeOnSelling = 10; // 1% will be added to the Development address

    uint256 liquidityFeeCounter = 0;
    uint256 marketingFeeCounter = 0;
    uint256 DevelopmentFeeCounter = 0;

    event SwapAndLiquify(
        uint256 tokensSwapped,
        uint256 ethReceived,
        uint256 tokensIntoLiqudity
    );

    constructor() {
        _balances[owner()] = _totalSupply;
        liquidityReceiverWallet = msg.sender;
        marketingWallet = address(0x8c1eFacD4690dA48A6918b648A7CA22d7c86FaB2);
        DevelopmentWallet = address(0x3a3ddd1C3d4AF0b7D69BFc965DD149dfA93Ec806);

        IDexRouter _dexRouter = IDexRouter(
            0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D
        );
        // Create a dex pair for this new ERC20
        address _dexPair = IDexFactory(_dexRouter.factory()).createPair(
            address(this),
            _dexRouter.WETH()
        );
        dexPair = _dexPair;

        // set the rest of the contract variables
        dexRouter = _dexRouter;

        //exclude owner and this contract from fee
        isExcludedFromFee[owner()] = true;
        isExcludedFromFee[address(this)] = true;

        //exclude owner and this contract from max Txn
        isExcludedFromMaxTxn[owner()] = true;
        isExcludedFromMaxTxn[address(this)] = true;

        //exclude owner and this contract from max hold limit
        isExcludedFromMaxHolding[owner()] = true;
        isExcludedFromMaxHolding[address(this)] = true;
        isExcludedFromMaxHolding[dexPair] = true;
        isExcludedFromMaxHolding[marketingWallet] = true;
        isExcludedFromMaxHolding[DevelopmentWallet] = true;

        emit Transfer(address(0), owner(), _totalSupply);
    }

    //to receive ETH from dexRouter when swapping
    receive() external payable {}

    function name() public view returns (string memory) {
        return _name;
    }

    function symbol() public view returns (string memory) {
        return _symbol;
    }

    function decimals() public view returns (uint8) {
        return _decimals;
    }

    function totalSupply() public view override returns (uint256) {
        return _totalSupply;
    }

    function balanceOf(address account) public view override returns (uint256) {
        return _balances[account];
    }

    function transfer(address recipient, uint256 amount)
        public
        override
        returns (bool)
    {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    function allowance(address owner, address spender)
        public
        view
        override
        returns (uint256)
    {
        return _allowances[owner][spender];
    }

    function approve(address spender, uint256 amount)
        public
        override
        returns (bool)
    {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) public override returns (bool) {
        _transfer(sender, recipient, amount);
        _approve(
            sender,
            _msgSender(),
            _allowances[sender][_msgSender()].sub(
                amount,
                "MODI: transfer amount exceeds allowance"
            )
        );
        return true;
    }

    function increaseAllowance(address spender, uint256 addedValue)
        public
        virtual
        returns (bool)
    {
        _approve(
            _msgSender(),
            spender,
            _allowances[_msgSender()][spender].add(addedValue)
        );
        return true;
    }

    function decreaseAllowance(address spender, uint256 subtractedValue)
        public
        virtual
        returns (bool)
    {
        _approve(
            _msgSender(),
            spender,
            _allowances[_msgSender()][spender].sub(
                subtractedValue,
                "MODI: decreased allowance or below zero"
            )
        );
        return true;
    }

    function includeOrExcludeFromFee(address account, bool value)
        external
        onlyOwner
    {
        isExcludedFromFee[account] = value;
    }

    function includeOrExcludeFromMaxTxn(address account, bool value)
        external
        onlyOwner
    {
        isExcludedFromMaxTxn[account] = value;
    }

    function includeOrExcludeFromMaxHolding(address account, bool value)
        external
        onlyOwner
    {
        isExcludedFromMaxHolding[account] = value;
    }

    function removeBots(address account)
        external
        onlyOwner
    {
        isBot[account] = false;
    }

    function setMinTokenToSwap(uint256 _amount) external onlyOwner {
        require(_amount > 0,"MODI: cant be 0");
        minTokenToSwap = _amount;
    }

    function setMaxHoldLimit(uint256 _amount) external onlyOwner {
        require(_amount >= _totalSupply.mul(5).div(percentDivider), "MODI: should be greater than 0.5%");
        maxHoldLimit = _amount;
    }

    function setMaxTxnLimit(uint256 _amount) external onlyOwner {
        require(_amount >= _totalSupply/percentDivider, "MODI: should be greater than 0.1%");
        maxTxnLimit = _amount;
    }

    function setBuyFeePercent(uint256 _lwFee, uint256 _marketingFee, uint256 _DevelopmentFee)
        external
        onlyOwner
    {
        marketingFeeOnBuying = _lwFee;
        DevelopmentFeeOnBuying = _DevelopmentFee;
        liquidityFeeOnBuying = _marketingFee;
        require(
            _lwFee.add(_marketingFee).add(_DevelopmentFee) <= percentDivider.div(10),
            "MODI: cant be more than 10%"
        );
    }

    function setSellFeePercent(uint256 _lwFee, uint256 _marketingFee, uint256 _DevelopmentFee)
        external
        onlyOwner
    {
        marketingFeeOnSelling = _lwFee;
        DevelopmentFeeOnSelling = _DevelopmentFee;
        liquidityFeeOnSelling = _marketingFee;
        require(
            _lwFee.add(_marketingFee).add(_DevelopmentFee) <= percentDivider.div(10),
            "MODI: cant be more than 10%"
        );
    }

    function setDistributionStatus(bool _value) public onlyOwner {
        distributeAndLiquifyStatus = _value;
    }

    function enableOrDisableFees(bool _value) external onlyOwner {
        feesStatus = _value;
    }

    function removeStuckEth(address _receiver) public onlyOwner {
        payable(_receiver).transfer(address(this).balance);
    }

    function updateAddresses(address _marketingWallet, address _DevelopmentWallet, address _liquidityReceiverWallet) external onlyOwner {
        marketingWallet = _marketingWallet;
        DevelopmentWallet = _DevelopmentWallet;
        liquidityReceiverWallet = _liquidityReceiverWallet;
    }

    function enableTrading() external onlyOwner {
        require(!trading, "MODI: already enabled");
        trading = true;
        feesStatus = true;
        distributeAndLiquifyStatus = true;
        launchedAt = block.timestamp;
    }

    function totalBuyFeePerTx(uint256 amount) public view returns (uint256) {
        uint256 fee = amount.mul(marketingFeeOnBuying.add(liquidityFeeOnBuying).add(DevelopmentFeeOnBuying)).div(
            percentDivider
        );
        return fee;
    }

    function totalSellFeePerTx(uint256 amount) public view returns (uint256) {
        uint256 fee = amount
            .mul(marketingFeeOnSelling.add(liquidityFeeOnSelling).add(DevelopmentFeeOnSelling))
            .div(percentDivider);
        return fee;
    }

    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) private {
        require(owner != address(0), "MODI: approve from the zero address");
        require(spender != address(0), "MODI: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    function _transfer(
        address from,
        address to,
        uint256 amount
    ) private {
        require(from != address(0), "MODI: transfer from the zero address");
        require(to != address(0), "MODI: transfer to the zero address");
        require(amount > 0, "MODI: Amount must be greater than zero");
        require(!isBot[from], "MODI: Bot detected");

        if (!isExcludedFromMaxTxn[from] && !isExcludedFromMaxTxn[to]) {
            require(amount <= maxTxnLimit, "MODI: max txn limit exceeds");

            // trading disable till launch
            if (!trading) {
                require(
                    dexPair != from && dexPair != to,
                    "MODI: trading is disable"
                );
            }
            // antibot
            if (
                block.timestamp < launchedAt + snipingTime &&
                from != address(dexRouter)
            ) {
                if (dexPair == from) {
                    isBot[to] = true;
                } else if (dexPair == to) {
                    isBot[from] = true;
                }
            }
        }

        if (!isExcludedFromMaxHolding[to]) {
            require(
                balanceOf(to).add(amount) <= maxHoldLimit,
                "MODI: max hold limit exceeds"
            );
        }

        // swap and liquify
        distributeAndLiquify(from, to);

        //indicates if fee should be deducted from transfer
        bool takeFee = true;

        //if any account belongs to isExcludedFromFee account then remove the fee
        if (isExcludedFromFee[from] || isExcludedFromFee[to] || !feesStatus) {
            takeFee = false;
        }

        //transfer amount, it will take tax, burn, liquidity fee
        _tokenTransfer(from, to, amount, takeFee);
    }

    //this method is responsible for taking all fee, if takeFee is true
    function _tokenTransfer(
        address sender,
        address recipient,
        uint256 amount,
        bool takeFee
    ) private {
        if (dexPair == sender && takeFee) {
            uint256 allFee = totalBuyFeePerTx(amount);
            uint256 tTransferAmount = amount.sub(allFee);
            _balances[sender] = _balances[sender].sub(amount,"MODI: insufficient balance");
            _balances[recipient] = _balances[recipient].add(tTransferAmount);

            emit Transfer(sender, recipient, tTransferAmount);
            takeTokenFee(sender, allFee);
            setFeeCountersOnBuying(amount);
        } else if (dexPair == recipient && takeFee) {
            uint256 allFee = totalSellFeePerTx(amount);
            uint256 tTransferAmount = amount.sub(allFee);
            _balances[sender] = _balances[sender].sub(amount,"MODI: insufficient balance");
            _balances[recipient] = _balances[recipient].add(tTransferAmount);

            emit Transfer(sender, recipient, tTransferAmount);
            takeTokenFee(sender, allFee);
            setFeeCountersOnSelling(amount);
        } else {
            _balances[sender] = _balances[sender].sub(amount,"MODI: insufficient balance");
            _balances[recipient] = _balances[recipient].add(amount);

            emit Transfer(sender, recipient, amount);
        }
    }

    function takeTokenFee(address sender, uint256 amount) private {
        _balances[address(this)] = _balances[address(this)].add(amount);

        emit Transfer(sender, address(this), amount);
    }

    function setFeeCountersOnBuying(uint256 amount) private {
        liquidityFeeCounter += amount.mul(liquidityFeeOnBuying).div(
            percentDivider
        );
        marketingFeeCounter += amount.mul(marketingFeeOnBuying).div(percentDivider);
        DevelopmentFeeCounter += amount.mul(DevelopmentFeeOnBuying).div(percentDivider);
    }

    function setFeeCountersOnSelling(uint256 amount) private {
        liquidityFeeCounter += amount.mul(liquidityFeeOnSelling).div(
            percentDivider
        );
        marketingFeeCounter += amount.mul(marketingFeeOnSelling).div(percentDivider);
        DevelopmentFeeCounter += amount.mul(DevelopmentFeeOnSelling).div(percentDivider);
    }

    function distributeAndLiquify(address from, address to) private {
        // is the token balance of this contract address over the min number of
        // tokens that we need to initiate a swap + liquidity lock?
        // also, don't get caught in a circular liquidity event.
        // also, don't swap & liquify if sender is Dex pair.
        uint256 contractTokenBalance = balanceOf(address(this));

        bool shouldSell = contractTokenBalance >= minTokenToSwap;

        if (
            shouldSell &&
            from != dexPair &&
            distributeAndLiquifyStatus &&
            !(from == address(this) && to == address(dexPair)) // swap 1 time
        ) {
            // approve contract
            _approve(address(this), address(dexRouter), contractTokenBalance);

            uint256 halfLiquidity = liquidityFeeCounter.div(2);
            uint256 otherHalfLiquidity = liquidityFeeCounter.sub(halfLiquidity);

            uint256 tokenAmountToBeSwapped = contractTokenBalance.sub(
                otherHalfLiquidity
            );

            uint256 balanceBefore = address(this).balance;

            // now is to lock into liquidty pool
            Utils.swapTokensForEth(address(dexRouter), tokenAmountToBeSwapped);

            uint256 deltaBalance = address(this).balance.sub(balanceBefore);
            uint256 ethToBeAddedToLiquidity = deltaBalance
                .mul(halfLiquidity)
                .div(tokenAmountToBeSwapped);
            uint256 ethFormarketing = deltaBalance.mul(marketingFeeCounter).div(
                tokenAmountToBeSwapped
            );
            uint256 ethForDevelopment = deltaBalance.sub(ethToBeAddedToLiquidity).sub(
                ethFormarketing
            );

            // add liquidity to Dex
            if (ethToBeAddedToLiquidity > 0) {
                Utils.addLiquidity(
                    address(dexRouter),
                    liquidityReceiverWallet,
                    otherHalfLiquidity,
                    ethToBeAddedToLiquidity
                );

                emit SwapAndLiquify(
                    halfLiquidity,
                    ethToBeAddedToLiquidity,
                    otherHalfLiquidity
                );
            }

            // sending eth to marketing wallet
            if (ethFormarketing > 0) payable(marketingWallet).transfer(ethFormarketing);

            // sending eth to development wallet
            if (ethForDevelopment > 0) payable(DevelopmentWallet).transfer(ethForDevelopment);

            // Reset all fee counters
            liquidityFeeCounter = 0;
            marketingFeeCounter = 0;
            DevelopmentFeeCounter = 0;
        }
    }
}

// Library for doing a swap on Dex
library Utils {
    using SafeMath for uint256;

    function swapTokensForEth(address routerAddress, uint256 tokenAmount)
        internal
    {
        IDexRouter dexRouter = IDexRouter(routerAddress);

        // generate the Dex pair path of token -> weth
        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = dexRouter.WETH();

        // make the swap
        dexRouter.swapExactTokensForETHSupportingFeeOnTransferTokens(
            tokenAmount,
            0, // accept any amount of ETH
            path,
            address(this),
            block.timestamp + 300
        );
    }

    function addLiquidity(
        address routerAddress,
        address owner,
        uint256 tokenAmount,
        uint256 ethAmount
    ) internal {
        IDexRouter dexRouter = IDexRouter(routerAddress);

        // add the liquidity
        dexRouter.addLiquidityETH{value: ethAmount}(
            address(this),
            tokenAmount,
            0, // slippage is unavoidable
            0, // slippage is unavoidable
            owner,
            block.timestamp + 300
        );
    }
}

library SafeMath {
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }

    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }

    function sub(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }

    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }

    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }

    function div(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }

    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return mod(a, b, "SafeMath: modulo by zero");
    }

    function mod(
        uint256 a,
        uint256 b,
        string memory errorMessage
    ) internal pure returns (uint256) {
        require(b != 0, errorMessage);
        return a % b;
    }
}

```

<!--Please shortly describe the bug you have found, and what you expect instead.-->

## Environment

- Compiler version: 0.8.23
- Target EVM version (as per compiler settings):
- Framework/IDE (e.g. Truffle or Remix): 
- EVM execution environment / backend / blockchain client:
- Operating system: ubuntu 22.04

## Steps to Reproduce
```
solc --bin standard.sol > 1.txt
solc --bin standard2.sol > 2.txt
diff 1.txt 2.txt | colordiff
```
<!--
Please provide a *minimal* source code example to trigger the bug you have found.
Please also mention any command-line flags that are necessary for triggering the bug.
Provide as much information as necessary to reproduce the bug.

```solidity
// Some *minimal* Solidity source code to reproduce the bug.
// ...
```
-->


#### <img src="https://avatars.githubusercontent.com/u/457348?u=e02c93e6d98c1154952140a8d5af50d9d5ca59c9&v=4" width="50">[r0qs](https://github.com/r0qs) commented at [2024-01-24 03:18](https://github.com/ethereum/solidity/issues/14800#issuecomment-1907287685):

Thanks for reporting, @chamitro, but I believe this is not a bug. The string expressions are indeed equivalent; the difference in the bytecode is due to metadata. If you compare the Yul code generated by both, they should be equal.

Please see the following warning in [our documentation about contract metadata](https://docs.soliditylang.org/en/v0.8.23/metadata.html):
```
Since the bytecode of the resulting contract contains the metadata hash by default, any change to the metadata
might result in a change of the bytecode. This includes changes to a filename or path, and 
since the metadata includes a hash of all the sources used, a single whitespace change results 
in different metadata, and different bytecode.
```

For instance, the contracts below will generate different bytecode due to metadata divergence, although `f()` will produce the same output for both (i.e., `ManofDevelopingIndia`):

- a.sol:
```solidity
contract A {
    string private _name = "ManofDevelopingIndia";
    function f() public view returns (string memory) {
        return _name;
    }
}
```
- b.sol
```solidity
contract A {
    string private _name = "Man" "of" "Developing" "India";
    function f() public view returns (string memory) {
        return _name;
    }
}
```

You can check the generated Yul by doing
```sh
solc --ir a.sol > a.yul
solc --ir b.sol > b.yul
diff a.yul b.yul
```

You will see that only the metadata hash and some source locations will be different, like:
```diff
<         data ".metadata" hex"a2646970667358221220fef6372fd6d9f1584086e88f921cc5f73d7c76d9ff3f0f3913e4fb9b6805dad164736f6c63430008170033"
---
>         data ".metadata" hex"a2646970667358221220ee7e0fb2c94e6b02415895ae48c4229bfae8191cff9bf875a305caccc0d6116664736f6c63430008170033"
```

#### <img src="https://avatars.githubusercontent.com/u/36297330?u=ec716a9bef32d6f777e73680d0a24a39760696a8&v=4" width="50">[chamitro](https://github.com/chamitro) commented at [2024-01-24 09:51](https://github.com/ethereum/solidity/issues/14800#issuecomment-1907778127):

Thank you for the information! My question is that while the change is in the ManOfDevelopingIndia contract's source code, the bytecode of  : IERC20, IDexRouter, IDexFactory and Context contracts is not changing, while the bytecode of Utils and SafeMath is changing. I was expecting that there wouldl be changes only in the ManOfDevelopingIndia contract.

#### <img src="https://avatars.githubusercontent.com/u/74224236?u=340b0471c91863a35448700940035b2c739c5ac7&v=4" width="50">[max-clinch](https://github.com/max-clinch) commented at [2024-01-24 14:38](https://github.com/ethereum/solidity/issues/14800#issuecomment-1908263108):

> Thank you for the information! My question is that while the change is in the ManOfDevelopingIndia contract's source code, the bytecode of : IERC20, IDexRouter, IDexFactory and Context contracts is not changing, while the bytecode of Utils and SafeMath is changing. I was expecting that there wouldl be changes only in the ManOfDevelopingIndia contract.

the behavior you're observing is a result of how Solidity handles metadata and bytecode generation. While the logical functionality of certain contracts remains unchanged, metadata changes can propagate to affect other parts of the bytecode.

#### <img src="https://avatars.githubusercontent.com/u/36297330?u=ec716a9bef32d6f777e73680d0a24a39760696a8&v=4" width="50">[chamitro](https://github.com/chamitro) commented at [2024-01-24 16:30](https://github.com/ethereum/solidity/issues/14800#issuecomment-1908490070):

Ok thank you for the informations!

#### <img src="https://avatars.githubusercontent.com/u/457348?u=e02c93e6d98c1154952140a8d5af50d9d5ca59c9&v=4" width="50">[r0qs](https://github.com/r0qs) commented at [2024-02-05 08:37](https://github.com/ethereum/solidity/issues/14800#issuecomment-1926465113):

> Thank you for the information! My question is that while the change is in the ManOfDevelopingIndia contract's source code, the bytecode of : IERC20, IDexRouter, IDexFactory and Context contracts is not changing, while the bytecode of Utils and SafeMath is changing. I was expecting that there wouldl be changes only in the ManOfDevelopingIndia contract.

Hi @chamitro, as @max-clinch mentioned, such metadata changes can propagate when compiling multiple contracts together. If you execute the commands below, you should only observe changes in source locations and metadata information. Therefore, I don't believe this is a bug, and I will be closing this issue.

```
solc --ir standard.sol > 1.yul
solc --ir standard2.sol > 2.yul
diff 1.yul 2.yul
```

Please feel free to reopen if there are additional points to address. Pinging @cameel in case I misunderstood something and this is indeed a bug ;)


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
