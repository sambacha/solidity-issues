# [\#15483 Issue](https://github.com/ethereum/solidity/issues/15483) `open`: use before assignment of calldata struct instance inside a function does not throw an error
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/34816323?u=30559e19824fd038b33dc4b466b8a5600cee5b15&v=4" width="50">[haoyang9804](https://github.com/haoyang9804) opened issue at [2024-10-07 09:56](https://github.com/ethereum/solidity/issues/15483):

<!--## Prerequisites

- First, many thanks for taking part in the community. We really appreciate that.
- We realize there is a lot of information requested here. We ask only that you do your best to provide as much information as possible so we can better help you.
- Support questions are better asked in one of the following locations:
    - [Solidity chat](https://gitter.im/ethereum/solidity)
    - [Stack Overflow](https://ethereum.stackexchange.com/)
- Ensure the issue isn't already reported.
- The issue should be reproducible with the latest solidity version; however, this isn't a hard requirement and being reproducible with an older version is sufficient.

*Delete the above section and the instructions in the sections below before submitting*
-->

## Description

The following program is a trivially correct one. I copy data from calldata to memory.
```solidity
contract A {
  struct S {
    int a;
  }
  function f (S calldata s) public pure {
    S memory s3;
    s3 = s;
  }
}
```
Then I mutate this test program into the following:
```solidity
contract A {
  struct S {
    int a;
  }
  function f (S calldata s) public pure {
    S calldata s2;
    S memory s3;
    s3 = s2; // fail: This variable is of calldata pointer type and can be accessed without prior assignment, which would lead to undefined behaviour.
  }
}
```
This test program causes an error, saying that `This variable is of calldata pointer type and can be accessed without prior assignment, which would lead to undefined behaviour.`. This is understandable. calldata is used to receive data from other contracts. So the calldata in a function without initialization is empty and should be initialized first.

I continue the mutation by initializing s2 first like the below and it passed the compilation.
```solidity
contract A {
  struct S {
    int a;
  }
  function f (S calldata s) public pure {
    S calldata s2 = s;
    S memory s3;
    s3 = s2; // pass
  }
}
```
Now I wonder if the compiler can find an incorrect initialization of a calldata slot. So I initialize the calldata slot with itself, an initialized calldata slot, to get the following program:

```solidity
contract A {
  struct S {
    int a;
  }
  function f (S calldata s) public pure {
    S calldata s2;
    s2 = s2;
    S memory s3;
    s3 = s2; // pass
  }
}
```
Interestingly, the above test program does not trigger an expected error with message such as `This variable is of calldata pointer type and can be accessed without prior assignment, which would lead to undefined behaviour.` but passed the compilation.


## Environment

- Compiler version:0.8.28-develop.2024.9.30+commit.33b67f0a
- Operating system: macos

## Steps to Reproduce
Just compile the above programs and you will reproduce the compilation results.
<!--
Please provide a *minimal* source code example to trigger the bug you have found.
Please also mention any command-line flags that are necessary for triggering the bug.
Provide as much information as necessary to reproduce the bug.

```solidity
// Some *minimal* Solidity source code to reproduce the bug.
// ...
```
-->


#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2024-10-07 10:42](https://github.com/ethereum/solidity/issues/15483#issuecomment-2396563287):

This is somewhat intentional as a deliberate workaround to allow complex initialization patterns. I.e. the following is correct in the sense that there can never be an uninitialized access, but it's in general infeasible to detect this at compile-time:
```solidity
contract C {
    struct A {
        uint256 x;
    }
    struct B {
        A a1;
        A a2;
    }
    function f(B calldata b, bool c) external pure returns (uint256) {
        A calldata a;
        if (c) {
            a = b.a1;
        }
        if (!c) {
            a = b.a2;
        }
        return a.x;
    }
}
```
Which is why deliberately assigning to self can be used to work around the detection, i.e. this works as deliberate workaround:
```solidity
contract C {
    struct A {
        uint256 x;
    }
    struct B {
        A a1;
        A a2;
    }
    function f(B calldata b, bool c) external pure returns (uint256) {
        A calldata a;
        a = a;
        if (c) {
            a = b.a1;
        }
        if (!c) {
            a = b.a2;
        }
        return a.x;
    }
}
```
Now the question is whether allowing this mode of workaround is generally a good thing - the idea was that it's rather unlikely that an assignment to self happens unintentionally and is thus safe to allow for allowing otherwise valid patterns through analysis. But since this is a known workaround against this kind of analysis, we'd likely need to consider changing the behavior here a breaking change.

#### <img src="https://avatars.githubusercontent.com/u/34816323?u=30559e19824fd038b33dc4b466b8a5600cee5b15&v=4" width="50">[haoyang9804](https://github.com/haoyang9804) commented at [2024-10-07 11:12](https://github.com/ethereum/solidity/issues/15483#issuecomment-2396627170):

I see. Thanks for the clarification.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2024-10-07 12:23](https://github.com/ethereum/solidity/issues/15483#issuecomment-2396781091):

Not sure if this is specifically documented, but what happens for ``c ? a : b`` is that you need a common type between `a` and `b` - which is the case just if `a` and `b` have the same type - or if `a`'s type is implicitly convertible to the type of `b` - or if `b`'s type is implicitly convertible to the type of `a`. In the latter two cases, the respective implicit conversion will happen and the ternary gets the "common type".

So e.g. for `a` in memory and `b` in calldata, `c ? a : b` will be in memory and `b` will be copied from calldata to memory.

#### <img src="https://avatars.githubusercontent.com/u/34816323?u=30559e19824fd038b33dc4b466b8a5600cee5b15&v=4" width="50">[haoyang9804](https://github.com/haoyang9804) commented at [2024-10-07 12:41](https://github.com/ethereum/solidity/issues/15483#issuecomment-2396821017):

> Not sure if this is specifically documented, but what happens for `c ? a : b` is that you need a common type between `a` and `b` - which is the case just if `a` and `b` have the same type - or if `a`'s type is implicitly convertible to the type of `b` - or if `b`'s type is implicitly convertible to the type of `a`. In the latter two cases, the respective implicit conversion will happen and the ternary gets the "common type".
> 
> So e.g. for `a` in memory and `b` in calldata, `c ? a : b` will be in memory and `b` will be copied from calldata to memory.

Following this understanding, I found an issue:
```solidity
contract C{
  struct S {
    int a;
  }
  S st;
  function f(S calldata cd) public {
    st = cd; // pass, S calldata -> S storage
    true ? st : cd; // fail: True expression's type struct C.S storage pointer does not match false expression's type struct C.S calldata.
  }
}
```
Does it reveal the inconsistency of type checking?

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2024-10-22 13:28](https://github.com/ethereum/solidity/issues/15483#issuecomment-2429291868):

That's a tricky example :-). ``st`` in ``st = cd``, i.e. on the left-hand-side of an assignment, is a storage reference - assigning to it will trigger a deep copy from ``cd`` to ``st``. However ``st`` in ``true ? st : cd`` (so in right-hand-side context) is a *storage pointer*. And there is no conversion from ``S calldata`` -> ``S storage (pointer)``.

You can also see the following fail:

```solidity
contract C{
  struct S {
    int a;
  }
  S st;
  function f(S calldata cd) public {
    S storage sp = st;
    sp = cd;
  }
}
```

If *that* passed, then ``c ? st : cd`` would also be expected to pass - but it doesn't.

Now that's the technical explanation in terms of the type system. But on a higher-level, it would be very weird behaviour if ``c ? st : cd`` *was* allowed and typed as ``S storage``. The only way to interpret that semantically would be that if ``c`` is ``false``, it would be a storage pointer to ``st``, but then what happens if ``c`` is ``true``? You need a storage pointer then as well - where should we point? You certainly won't expect ``cd`` to be copied to ``st`` in that case.

The only way to *make* ``c ? st : cd`` work semantically would be to copy *both* arguments to memory - but the current typing rules don't allow for that, and for good reason, since you're not guaranteed that each pair of types has a unique type that both are implicitly convertible to.

#### <img src="https://avatars.githubusercontent.com/u/34816323?u=30559e19824fd038b33dc4b466b8a5600cee5b15&v=4" width="50">[haoyang9804](https://github.com/haoyang9804) commented at [2024-10-22 16:01](https://github.com/ethereum/solidity/issues/15483#issuecomment-2429678150):

> That's a tricky example :-). `st` in `st = cd`, i.e. on the left-hand-side of an assignment, is a storage reference - assigning to it will trigger a deep copy from `cd` to `st`. However `st` in `true ? st : cd` (so in right-hand-side context) is a _storage pointer_. And there is no conversion from `S calldata` -> `S storage (pointer)`.
> 
> You can also see the following fail:
> 
> ```solidity
> contract C{
>   struct S {
>     int a;
>   }
>   S st;
>   function f(S calldata cd) public {
>     S storage sp = st;
>     sp = cd;
>   }
> }
> ```
> 
> If _that_ passed, then `c ? st : cd` would also be expected to pass - but it doesn't.
> 
> Now that's the technical explanation in terms of the type system. But on a higher-level, it would be very weird behaviour if `c ? st : cd` _was_ allowed and typed as `S storage`. The only way to interpret that semantically would be that if `c` is `false`, it would be a storage pointer to `st`, but then what happens if `c` is `true`? You need a storage pointer then as well - where should we point? You certainly won't expect `cd` to be copied to `st` in that case.
> 
> The only way to _make_ `c ? st : cd` work semantically would be to copy _both_ arguments to memory - but the current typing rules don't allow for that, and for good reason, since you're not guaranteed that each pair of types has a unique type that both are implicitly convertible to.

Really thanks for your patient clarification. It helps a lot for my building the semantics model of Solidity to generate Solidity programs.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
