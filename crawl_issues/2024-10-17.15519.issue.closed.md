# [\#15519 Issue](https://github.com/ethereum/solidity/issues/15519) `closed`: viaIR pipeline does not clean uints before indexing storage arrays
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/41449730?u=070a8dceef0482a736635917f5f834d0cd999ff2&v=4" width="50">[nonergodic](https://github.com/nonergodic) opened issue at [2024-10-17 04:41](https://github.com/ethereum/solidity/issues/15519):

## Description

When compiling with via_ir=true, dirty uints don't get cleaned before indexing into a `storage` array leading to an incorrect access / out of bounds panic. Indexing into `memory` arrays with dirty uints works fine.

## Environment

- Compiler version: 0.8.28 (but also happens with e.g. 0.8.24)
- Target EVM version (as per compiler settings): Cancun (but it also happens with e.g. Paris)
- Framework/IDE (e.g. Truffle or Remix): foundry
- EVM execution environment / backend / blockchain client: -
- Operating system: -

## Steps to Reproduce

Compile with forge and  `via_ir=true` to see out of bounds panic, compile with `via_ir=false` to see tests pass:
```solidity
pragma solidity ^0.8.24;

import "forge-std/Test.sol";

contract ViaIRCleaningIssue is Test {
  uint[] private _storageArr;

  constructor() {
    _storageArr.push(1);
  }

  function getDirtyUint8() private pure returns (uint8 ret) {
    assembly ("memory-safe") { ret := shl(8, 1) }
  }

  function testCleaningFailsForStorageArrays() public view {
    //when compiled with via_ir=true, storage array access with dirty uint8 causes
    //  "Index out of bounds" Panic
    assertEq(_storageArr[getDirtyUint8()], 1);
  }

  function testCleaningWorksForMemoryArrays() public pure {
    uint[] memory memArr = new uint[](1);
    memArr[0] = 1;

    //but memory arrays always handle dirty u8 access correctly
    assertEq(memArr[getDirtyUint8()], 1);
  }

  function testCleaningWorksWithCast() public view {
    //casting to uint cleans the value and hence fixes the problem for via_ir=true
    assertEq(_storageArr[uint(getDirtyUint8())], 1);
  }
}
```


#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2024-10-17 18:30](https://github.com/ethereum/solidity/issues/15519#issuecomment-2420258219):

Thanks for the report. This has actually been reported before in #15142 so I'm going to close this in favor of that older one.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2024.12.15 at 06:45:24.]
