# [\#6489 Issue](https://github.com/ethereum/solidity/issues/6489) `closed`: Yul example from docs does not compile
**Labels**: `documentation :book:`


#### <img src="https://avatars.githubusercontent.com/u/3867501?u=8126b3c609b132f8625faae11480d2f8113f5acb&v=4" width="50">[charles-cooper](https://github.com/charles-cooper) opened issue at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489):

Trying to compile the example from the docs (https://solidity.readthedocs.io/en/develop/yul.html#specification-of-yul-object)
```javascript
// example.yul
object "Contract1" {
    code {
        // first create "runtime.Contract2"
        let size := datasize("runtime.Contract2")
        let offset := allocate(size)
        // This will turn into a memory->memory copy for eWASM and
        // a codecopy for EVM
        datacopy(offset, dataoffset("runtime.Contract2"), size)
        // constructor parameter is a single number 0x1234
        mstore(add(offset, size), 0x1234)
        create(offset, add(size, 32))

        // now return the runtime object (this is
        // constructor code)
        size := datasize("runtime")
        offset := allocate(size)
        // This will turn into a memory->memory copy for eWASM and
        // a codecopy for EVM
        datacopy(offset, dataoffset("runtime"), size)
        return(offset, size)
    }

    data "Table2" hex"4123"

    object "runtime" {
        code {
            // runtime code

            let size = datasize("Contract2")
            let offset = allocate(size)
            // This will turn into a memory->memory copy for eWASM and
            // a codecopy for EVM
            datacopy(offset, dataoffset("Contract2"), size)
            // constructor parameter is a single number 0x1234
            mstore(add(offset, size), 0x1234)
            create(offset, add(size, 32))
        }

        // Embedded object. Use case is that the outside is a factory contract,
        // and Contract2 is the code to be created by the factory
        object "Contract2" {
            code {
                // code here ...
            }

            object "runtime" {
                code {
                    // code here ...
                }
             }

             data "Table1" hex"4123"
        }
    }
}
```
fails with the following error:
```
$ docker run -v $PWD:/app -w /app ethereum/solc:nightly-0.5.8-ef3a18999c840c523cce73f6074e816334b5c753 --yul example.yul
Warning: Yul and its optimizer are still experimental. Please use the output with care.
example.yul:4:18: Error: Expected ':' but got '='
        let size = datasize("runtime.Contract2")
                 ^
example.yul:4:18: Error: Expected keyword "data" or "object" or "}".
        let size = datasize("runtime.Contract2")
                 ^
```
Replacing `=` with `:=` also fails:
```
$ docker run -v $PWD:/app -w /app ethereum/solc:nightly-0.5.8-ef3a18999c840c523cce73f6074e816334b5c753 --yul example.yul
Warning: Yul and its optimizer are still experimental. Please use the output with care.
example.yul:4:18: Error: Expected ':' but got ':='
        let size := datasize("runtime.Contract2")
                 ^^
example.yul:4:18: Error: Expected keyword "data" or "object" or "}".
        let size := datasize("runtime.Contract2")
                 ^^
```

#### <img src="https://avatars.githubusercontent.com/u/3867501?u=8126b3c609b132f8625faae11480d2f8113f5acb&v=4" width="50">[charles-cooper](https://github.com/charles-cooper) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-480616402):

tagging @ChrisChinchilla

#### <img src="https://avatars.githubusercontent.com/u/42080?u=37db5129c5c71d0293952c8a1a2ef1c181e0e1d6&v=4" width="50">[ChrisChinchilla](https://github.com/ChrisChinchilla) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-480745913):

As far as I understand, this code is not solidity code to run, but rather an example of a YUL object generated from Solidity code, which is why you can't compile it.

But perhaps @ekpyron can confirm that?

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-480771878):

This should in fact compile - it is an example of Yul code as it might be generated from solidity code, but it should be compilable with ``solc --yul`` nonetheless - but it may well be that the compiler has insufficient support for Yul object as of now.

#### <img src="https://avatars.githubusercontent.com/u/42080?u=37db5129c5c71d0293952c8a1a2ef1c181e0e1d6&v=4" width="50">[ChrisChinchilla](https://github.com/ChrisChinchilla) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-480784729):

Ahh, ok @ekpyron Does it compile for you?

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-480788891):

No :-). And actually I mixed up the compiler options myself (it's getting horribly confusing :-D).
It definitely can't be compiled with ``solc --yul``, since that expects typed yul and the code is untyped...

The best chances of compiling this would be ``solc --assemble --strict-assembly``, but even that fails, since the example is incomplete and not entirely correct... we should probably fix it, so that it works with ``solc --assemble --strict-assembly`` at least...

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-480792672):

Or actually, since this is the Yul (as in typed Yul) section of the documentation, we might instead want to add types to it... but we won't be able to get it to work that way, since typed yul is not yet sufficiently implemented for it...

We could change the example to typed yul as:
```

// example.yul
object "Contract1" {
    code {
        // first create "runtime.Contract2"
        let size:u256 := datasize("runtime.Contract2":string)
        let offset:u256 := allocate(size)
        // This will turn into a memory->memory copy for eWASM and
        // a codecopy for EVM
        datacopy(offset, dataoffset("runtime.Contract2":string), size)
        // constructor parameter is a single number 0x1234
        mstore(add(offset, size), 0x1234:u256)
        create(offset, add(size, 32:u256))

        // now return the runtime object (this is
        // constructor code)
        size := datasize("runtime":u256)
        offset := allocate(size)
        // This will turn into a memory->memory copy for eWASM and
        // a codecopy for EVM
        datacopy(offset, dataoffset("runtime":u256), size)
        return(offset, size)
    }

    data "Table2" hex"4123"

    object "runtime" {
        code {
            // runtime code

            let size:u256 := datasize("Contract2":string)
            let offset:u256 := allocate(size)
            // This will turn into a memory->memory copy for eWASM and
            // a codecopy for EVM
            datacopy(offset, dataoffset("Contract2":string), size)
            // constructor parameter is a single number 0x1234
            mstore(add(offset, size), 0x1234:u256)
            create(offset, add(size, 32:u256), 4:u256)
        }

        // Embedded object. Use case is that the outside is a factory contract,
        // and Contract2 is the code to be created by the factory
        object "Contract2" {
            code {
                // code here ...
            }

            object "runtime" {
                code {
                    // code here ...
                }
             }

             data "Table1" hex"4123"
        }
    }
}
```

And add a note stating that this will not compile due to typed yul support still being incomplete?
Right now it won't even parse, since not even the ``string`` type is supported yet :-).

#### <img src="https://avatars.githubusercontent.com/u/3867501?u=8126b3c609b132f8625faae11480d2f8113f5acb&v=4" width="50">[charles-cooper](https://github.com/charles-cooper) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-480911814):

@ekpyron @ChrisChinchilla my broader concern is that programs generated with `solc --ir` will not compile. In fact, I was originally trying to get programs generated with `--asm` or `--ir` to compile and only went to the doc examples after not being able to get `solc` to consume its own output.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-481178851):

@charles-cooper The code solidity generates with ``solc --ir`` should be compilable with ``solc --assemble --strict-assembly``. For now the compiler generates untyped Yul and ``solc --yul`` expects typed Yul. If ``solc --assemble --strict-assembly`` fails to accept what ``solc --ir`` generated, then it would be nice if you posted your code here!

Note, however, that ``solc --ir`` is in very early stages of development and the main part of solidity is as of yet unimplemented. So you have to expect ``solc --ir`` to generate incomplete, resp. wrong code without warning or error.

#### <img src="https://avatars.githubusercontent.com/u/3867501?u=8126b3c609b132f8625faae11480d2f8113f5acb&v=4" width="50">[charles-cooper](https://github.com/charles-cooper) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-481316958):

@ekpyron nice! `solc --assemble --strict-assembly` works with the output of `solc --ir` for me (barring the `IR:` header in the output). That solves my use case. I have a couple more questions though; does `--assemble` work without `--strict-assembly` here? Can `--asm` output be consumed by `solc`? Sorry if these are trivial questions, the answers are not obvious to newcomers like myself (and don't seem to be answered in the docs, although I may have missed something).

#### <img src="https://avatars.githubusercontent.com/u/42080?u=37db5129c5c71d0293952c8a1a2ef1c181e0e1d6&v=4" width="50">[ChrisChinchilla](https://github.com/ChrisChinchilla) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-481913232):

Thanks for everyone's input, I'll get on to updating docs with some details asap. There are lots of efforts into YUL right now, so it's in fairly regular flux.

#### <img src="https://avatars.githubusercontent.com/u/1347491?v=4" width="50">[ekpyron](https://github.com/ekpyron) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-482056442):

@charles-cooper In fact I'd need to check some of this myself ;-) (so better to recheck what I'm saying), but I think ``--assemble`` *does* accept non-strict assembly (i.e. work without ``--strict-assembly``), but I think non-strict assembly is deprecated and will be removed in the near future.
I don't think ``--asm`` output can be consumed by the compiler.
In the long term, ``solc --ir`` will be the main way to go (I could imagine everything else might be dropped at some point), but as I said, as of now, it doesn't implement most of the language, so it's not yet usable in practice.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-483262933):

It should compile using `solc --strict-assembly`. The option `--assemble` is not required.

#### <img src="https://avatars.githubusercontent.com/u/3260363?u=801d9204e5d0deb6af511e7f320de6d09eae60dd&v=4" width="50">[vedant1811](https://github.com/vedant1811) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-488643181):

I am getting a similar issue trying to get the exponent example to compile:

test.yul:
```

object "Contract" {
    code {
        function power(base:u256, exponent:u256) -> result:u256
        {
            switch exponent
            case 0:u256 { result := 1:u256 }
            case 1:u256 { result := base }
            default
            {
                result := power(mul(base, base), div(exponent, 2:u256))
                switch mod(exponent, 2:u256)
                    case 1:u256 { result := mul(base, result) }
            }
        }
    }
}
```

command ouput:

```
$ ../solidity/build/solc/solc --strict-assembly test.yul 
Warning: Yul and its optimizer are still experimental. Please use the output with care.
test.yul:3:28: Error: Expected ',' but got ':'
        function power(base:u256, exponent:u256) -> result:u256
                           ^
test.yul:3:28: Error: Expected keyword "data" or "object" or "}".
        function power(base:u256, exponent:u256) -> result:u256
```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-488680717):

@vedant1811 you cannot use types with `--strict-assembly`. Types are not yet implemented at all.

#### <img src="https://avatars.githubusercontent.com/u/3260363?u=801d9204e5d0deb6af511e7f320de6d09eae60dd&v=4" width="50">[vedant1811](https://github.com/vedant1811) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-488682719):

@chriseth Thanks that worked :)
I guess the docs need to be updated with that as well. Hopefully I will get time to submit a PR soon

#### <img src="https://avatars.githubusercontent.com/u/42080?u=37db5129c5c71d0293952c8a1a2ef1c181e0e1d6&v=4" width="50">[ChrisChinchilla](https://github.com/ChrisChinchilla) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-490540685):

@vedant1811 The lack of types is mentioned here - https://solidity.readthedocs.io/en/v0.5.8/yul.html

But I'm going to check over all the syntax questions to see if they are mentioned.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-490602073):

The main problem is that the example does not compile. @vedant1811 do you think you will have time to submit a pull request? Otherwise someone from the team can also do it.

#### <img src="https://avatars.githubusercontent.com/u/3260363?u=801d9204e5d0deb6af511e7f320de6d09eae60dd&v=4" width="50">[vedant1811](https://github.com/vedant1811) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-490734670):

Sure, I'll do it. (I am working on a coding task but that'll take longer)

I will update the `power` example- make it compile and run by removing the types.

Anything else?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-490761182):

Thanks! There are also some errors in the example at the end of yul.rst, for example `let offset = allocate(size)` should be `let offset := allocate(size)`

#### <img src="https://avatars.githubusercontent.com/u/3260363?u=801d9204e5d0deb6af511e7f320de6d09eae60dd&v=4" width="50">[vedant1811](https://github.com/vedant1811) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-491149853):

@chriseth I fixed the ones we talked about. Now I'm getting 

```
test.1.yul:6:27: Error: Function not found.
            let offset := allocate(size)
                          ^------^
```

What should I do with the `allocate` commands?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-491722096):

Oh I'm sorry, that seems to be missing. You can add it as
```
function allocate(size) -> ptr {
  ptr := mload(0x40)
  if iszero(ptr) { ptr := 0x60 }
  mstore(0x40, add(ptr, size))
}
```

#### <img src="https://avatars.githubusercontent.com/u/42080?u=37db5129c5c71d0293952c8a1a2ef1c181e0e1d6&v=4" width="50">[ChrisChinchilla](https://github.com/ChrisChinchilla) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-491753641):

Thanks @vedant1811 üëç

#### <img src="https://avatars.githubusercontent.com/u/3260363?u=801d9204e5d0deb6af511e7f320de6d09eae60dd&v=4" width="50">[vedant1811](https://github.com/vedant1811) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-491800239):

There are a lot of issues in the second example. I have fixed some but still (unknown number of) issues.

Like:
`create` command is giving the errors:
```
test.1.yul:18:9: Error: Top-level expressions are not supposed to return values (this expression returns 1 value). Use ``pop()`` or assign them.
        create(offset, size, 32)
        ^----------------------^
test.1.yul:3:10: Error: Unbalanced stack at the end of a block: 1 surplus item(s).
    code {
         ^ (Relevant source part starts here and spans across multiple lines).
test.1.yul:49:13: Error: Top-level expressions are not supposed to return values (this expression returns 1 value). Use ``pop()`` or assign them.
            create(offset, size, 32)
            ^----------------------^
test.1.yul:33:14: Error: Unbalanced stack at the end of a block: 1 surplus item(s).
        code {
             ^ (Relevant source part starts here and spans across multiple lines).
``` 

even without `create`, the code compiles but the compiler errors out:
```
Pretty printed source:
object "Contract1" {
    code {
        function allocate(size) -> ptr
        {
            ptr := mload(0x40)
            if iszero(ptr) { ptr := 0x60 }
            mstore(0x40, add(ptr, size))
        }
        let size := datasize("runtime.Contract2")
        let offset := allocate(size)
        datacopy(offset, dataoffset("runtime.Contract2"), size)
        mstore(add(offset, size), 0x1234)
        size := datasize("runtime")
        offset := allocate(size)
        datacopy(offset, dataoffset("runtime"), size)
        return(offset, size)
    }
    data "Table2" hex"4123"
    object "runtime" {
        code {
            function allocate(size) -> ptr
            {
                ptr := mload(0x40)
                if iszero(ptr) { ptr := 0x60 }
                mstore(0x40, add(ptr, size))
            }
            let size := datasize("Contract2")
            let offset := allocate(size)
            datacopy(offset, dataoffset("Contract2"), size)
            mstore(add(offset, size), 0x1234)
        }
        object "Contract2" {
            code { }
            object "runtime" {
                code { }
            }
            data "Table1" hex"4123"
        }
    }
}

Exception while assembling: /Users/vedant.agarwala/my/crypto/solidity/libyul/backends/evm/EVMDialect.cpp(58): Throw in function auto yul::EVMDialect::EVMDialect(yul::AsmFlavour, bool, langutil::EVMVersion)::(anonymous class)::operator()(const yul::FunctionCall &, yul::AbstractAssembly &, std::function<void ()>) const
Dynamic exception type: boost::wrapexcept<yul::YulException>
std::exception::what: Could not find assembly object <runtime.Contract2>.
[dev::tag_comment*] = Could not find assembly object <runtime.Contract2>.

```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-491802980):

I think you can use `pop(create(...))` to fix it. Hm. `runtime.Contract2` not being found is a bit more serious - it might be something that is not yet implemented in the compiler.

#### <img src="https://avatars.githubusercontent.com/u/3260363?u=801d9204e5d0deb6af511e7f320de6d09eae60dd&v=4" width="50">[vedant1811](https://github.com/vedant1811) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-491804593):

Yes `pop(...)` fixes the problem. The second error is definitely a compiler error. It does manage to parse and show `Pretty printed source`.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-07 18:19](https://github.com/ethereum/solidity/issues/6489#issuecomment-491806145):

Created https://github.com/ethereum/solidity/issues/6739 to track the `runtime.Contract2` issue.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
