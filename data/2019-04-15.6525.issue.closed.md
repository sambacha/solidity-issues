# [\#6525 Issue](https://github.com/ethereum/solidity/issues/6525) `closed`: Variables of function internal type have wrong default value inside constructors
**Labels**: `bug :bug:`


#### <img src="https://avatars.githubusercontent.com/u/35589221?v=4" width="50">[haltman-at](https://github.com/haltman-at) opened issue at [2019-04-15 22:46](https://github.com/ethereum/solidity/issues/6525):

## Description

When inside a constructor, variables of `function internal` type use their lower 4 bytes to store the PC of the function in the deployed code, and use their upper 4 bytes to store the PC of the function in the constructor code.

However, this format is not correctly applied to `function internal` variables (in locations other than storage) that have not been initialized and have their default value.  In deployed code, such variables have a default value which points to some code that throws an invalid opcode exception (let's call this PC value `deployedInvalid`).  And when a `function internal` variable is used in a constructor, a designated invalid function is set up in the constructor, too (let's call its PC value `constructorInvalid`).  One expects, then, that in a constructor an uninitialized variable of type `function internal` (and in a location other than storage) should have the value `constructorInvalid << 4 | deployedInvalid`.  However, this is not what occurs.  Instead, it has the value `constructorInvalid`.

The result is that if you call such a variable, it looks at the upper 4 bytes (which are 0) and attempts to jump to there, instead of to the designated invalid function.  Ultimately the end result is the same, since either way one will get an exception, but it's still the wrong value.

But there's a bigger problem here -- if one saves such a variable in storage, and then later reads it in another transaction and attempts to call it, it will look at the lower 4 bytes, which should contain the designated invalid function for the deployed code, but instead contain the designated invalid function for the constructor code.  This will still probably cause an exception, but it's not a guarantee.

To fix this, unitialized internal function variables in constructors should use the correct format, storing the constructor's designated invalid function in the upper 4 bytes and the deployed code's designated invalid function in the lower 4 bytes.

## Environment

- Compiler version: 0.5.7
- Target EVM version (as per compiler settings): whatever the default is
- Framework/IDE (e.g. Truffle or Remix): Truffle (unbundled) (core: 5.0.12)
- EVM execution environment / backend / blockchain client: Ganache CLI v6.4.1 (ganache-core: 2.5.3)
- Operating system: Linux Mint 18.3 (64-bit)

## Steps to Reproduce

Minimal example for just the wrong-format bug:

```
pragma solidity ^0.5.7;

contract InvalidTest {

  function() internal storedFn;

  constructor() public {
    function() internal invalid;
    storedFn = invalid; //wrong format!
  }
}
```

Minimal example for jumping to 0 instead of the designated invalid function:
```
pragma solidity ^0.5.7;

contract InvalidTest {

  constructor() public {
    function() internal invalid;
    invalid(); //jumps to 0 instead of designated invalid function!
  }
}
```

Minimal example for wrong-format bug and what happens when it's later read outside a constructor:

```
pragma solidity ^0.5.7;

contract InvalidTest {

  function() internal storedFn;

  constructor() public {
    function() internal invalid;
    storedFn = invalid; //wrong format!
  }

  function run() public {
    storedFn(); //not guaranteed to cause exception!
  }
}
```

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2019-04-15 22:46](https://github.com/ethereum/solidity/issues/6525#issuecomment-483731328):

That's a great find, thanks!

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2019-04-15 22:46](https://github.com/ethereum/solidity/issues/6525#issuecomment-486624927):

```
pragma solidity ^0.5.7;

contract InvalidTest {

  function() internal storedFn;
  uint public x;
    
  constructor() public {
    uint _y1;
    uint _y2;
    uint _y3;
    uint _y4;
    uint _y5;
    uint _y6;
    uint _y7;
    uint _y8;
    uint _y9;
    uint _y10;
    uint _y11;
    uint _y12;
    uint _y13;
    uint _y14;
    
    function() internal invalid;
    storedFn = invalid; //wrong format!
  }

  function run() public {
    storedFn(); //not guaranteed to cause exception!
  }
  
  function z() public {
      x++;
  }
}
```
In this example the invalid tag is the same as `z`'s and a call to `run` does not revert, changing `x` instead.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
