# [\#8353 Issue](https://github.com/ethereum/solidity/issues/8353) `open`: Change the Layout of Storage Variables for Upgradeable Proxy Contracts
**Labels**: `breaking change :warning:`, `feature`, `language design :rage4:`


#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) opened issue at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353):

## Abstract
Upgradegradable proxy contracts that delegate to other contracts (called delegate or logic contracts) for functionality currently have second-class variable storage layout support in Solidity.

The problem is that storage variable declarations must match/line up between a proxy contract and all delegate contracts that are delegated to by the proxy contract.

In the case of a proxy contract with multiple delegate contracts the delegate contract variable declarations must match up with the proxy contract and with all the other delegate contracts that are used.

Alternatively, proxy contracts have used unstructured storage which resorts to manually specifying and manipulating storage with assembly.

Some examples of the proxy contract pattern in use:
1. [ERC1538: Transparent Contract Standard](https://eips.ethereum.org/EIPS/eip-1538)
2. [OpenZepplin's Proxies](https://docs.openzeppelin.com/upgrades/2.7/proxies)
3. [AragonOS](https://github.com/aragon/aragonOS/blob/next/contracts/common/DelegateProxy.sol)
4. [Genosis Proxies](https://blog.gnosis.pm/solidity-delegateproxy-contracts-e09957d0f201)
5. [DecentralLand ](https://github.com/decentraland/land/blob/5154046844f6f94a5074e82abe01381e6fd7c39d/contracts/upgradable/DelegateProxy.sol)
6. [EIP1822: Universal Upgradeable Proxy Standard](https://eips.ethereum.org/EIPS/eip-1822)
7. [EIP1967: Standard Proxy Storage Slots ](https://eips.ethereum.org/EIPS/eip-1967)

The proxy contract pattern is used by people and it would be really great if it had first-class variable storage layout support!

## Motivation

Currently the layout of storage variables is documented here: https://solidity.readthedocs.io/en/v0.6.3/miscellaneous.html

I propose that the layout works the same with one change: Instead of starting at position 0 make a keccak256 hash of the name of the contract and start the storage for the contract at the position given by the hash. This way the storage space is separate for each contract that has a different name.

With this approach a delegate contract can create its own storage variables without clashing with the proxy contract or other delegate contracts.  And delegate contracts no longer need to import and inherit classes it doesn't need in order to maintain storage variable alignment.

A syntactic construct can exist for one delegate contract to access the storage specified by the proxy contract or other delegate contract. I propose that a delegate contract simply imports the code of the delegate/proxy its wants to access and then use the contract name to access the contract's storage like this:

``` Solidity
import "./DelegateA.sol"
// imagine that DelegateA.sol has a storage variable called num2.

contract DelegateB {
  uint256 internal num1;

  // function that returns storage variable data defined within the contract
  function getNum1() external view returns(uint256) {
    return num1; 
  }

  // function that returns storage variable data defined in another delegate contract
  function getNum2() external view returns(uint256) {
    return DelegateA.num2; 
  }
}
```
This is the same syntax that is used by libraries, now being used by contracts.

Perhaps this could be an optional feature that is enabled with a pragma directive. Because this may only be useful for the proxy contract pattern. Also, I am really interested in other possible solutions.

## Backwards Compatibility

This is a not a backwards compatible change. This functionality could be used by new proxy contracts and delegate contracts.  Perhaps, to help with transition, this mode of storage variable layout could have its own pragma directive.

#### <img src="https://avatars.githubusercontent.com/u/20340?v=4" width="50">[axic](https://github.com/axic) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-588867410):

See also #597.

It took me a while to read because the description is quite big, but the actual proposal is in the middle:
> I propose that the layout works the same with one change: Instead of starting at position 0 make a keccak256 hash of the name of the contract and start the storage for the contract at the position given by the hash. This way the storage space is separate for each contract that has a different name.

While this seem like an elegant solution, please note that this addition means either doing an extra round of hashing (and storing the input in the contract) or storing the hash in the contract, both of which are quite expensive.

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-589001846):

Thanks @axic. Perhaps this could be an optional feature that is enabled with a pragma directive. Because this may only be useful for the proxy contract pattern. Also, I am really interested in other possible solutions.

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-589018453):

We have been using the JSON interface for different options and configurations. I think this would be encouraged instead of a pragma directive.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-589077551):

Oh wow, I just wanted to propose the following:

```
struct Data {
  uint a;
  uint b;
}

contract DelegateB {
  function data() internal returns (Data storage r) {
    uint s = keccak256("DelegateB");
    assembly { r_slot := s }
  }

  function doSomething() external view returns(uint256) {
    data().a = 8;
  }
}
```

when I realized that we have no way to assign to storage pointer variables in assembly. Did nobody ever try that?

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-589079661):

Regardless of the above, I would be happier with a more explicit notation like for example
```
struct Data {
  uint a;
  uint b;
}

contract DelegateB {
  Data data internal at keccak256("DelegateB");
  function doSomething() external view returns(uint256) {
    data.a = 8;
  }
}
```

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-589241500):

@chriseth I see, so structs could be used for the storage layout of proxy and/or delegate contracts. I also like explicitness. This is brilliant, I love it!

I love the syntax you gave for defining storage pointers: `Data data internal at keccak256("DelegateB");` How quickly could this be implemented and released?

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-589249774):

@chriseth Could there be a way to specify a struct to use for the storage layout for a contract?  Here is an example:

``` Solidity
struct DataDelegateB {
  uint a;
  uint b;
}

contract DelegateB {
  using storage layout DataDelegateB at keccak256("DataDelegateB");

  function doSomething() external view returns(uint256) {
    a = 8;
    b = 7;
  }
}
```
Notice that I did not need to prepend a struct variable name to access the "a" and "b" members of the struct.

Using the storage pointer definition syntax other delegate contracts can access the storage layout of DelegateB. Here is an example:

``` Solidity
struct DataDelegateB {
  uint a;
  uint b;
}

struct DataDelegateC {
  uint first;
  uint second;
}

contract DelegateC {
  using storage layout DataDelegateC at keccak256("DataDelegateC");
  
  DataDelegateB dataDelegateB internal at keccak256("DataDelegateB");
  
  function getFirst() external view returns(uint256) {
    first = 7;
    second = 10;
    return dataDelegateB.a + first + second
  }
}
```
I think this is pretty clean!

But I think the `at keccak256("DelegateC")` part could be optional so this syntax could be used:
``` Solidity
using storage layout DataDelegateC;
```

If the `at` part is omitted then automatically the keccak256 of the name of the struct is used as the storage pointer value.

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-589476520):

@chriseth I eagerly await your feedback.

I am planning to launch a new and improved proxy contract upgrade standard based on [ERC1538](https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1538.md) and recent solidity features. Having the above storage layout mechanisms we are discussing would make it so much more developer friendly and better.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-590339865):

@mudgen we need more feedback on this, unfortunately. In my opinion, something like `using storage layout DataDelegateC at keccak256("DataDelegateC");` that applies to the whole contract is too dangerous and too easy to miss. Furthermore, it would be very complicated with regards to inheritance.

What I think could find better consensus is a single state variable (which might be of struct type) that is "dislocated". For proxy contracts, wouldn't it even be better if the part needed by the proxy would be dislocated instead of the actual storage? Or is the point that you could move to a different "fresh" location when upgrading?

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-590412649):

Hi @chriseth.

That is a good point about inheritance.

Being able to create a dislocated state variable, like how you showed before: `Data data internal at keccak256("DelegateB");` would be very useful for most proxy contracts.

To answer your question regarding having a dislocated storage variable or having dislocated storage layouts for proxy contracts,  it depends. There are two different use cases:

1. A proxy contract that only ever has one delegate/logic contract that it delegates to at a time, so that any upgrade is simply replacing the one logic contract with a different logic contract would benefit the most by using a dislocated state variable like you suggested.  Each logic contract would use the same storage layout so there would be no need for dislocated storage layouts. 
2. It is different for proxy contracts that use multiple delegate/logic contracts at the same time. In this case the proxy contract will delegate to a logic contract based on what function is being called. In this approach sharing the same storage layout for each logic contract is a burden, because each logic contract handles different things and does not need to access the same state variables. This approach would benefit a lot if each logic contract could use its own dislocated storage layout space, so that each logic contract is separate from each other but still able to access each other's state variables if needed. Check out ERC1538 for this multiple delegate/logic contract approach: https://eips.ethereum.org/EIPS/eip-1538

No matter what having the ability to create a dislocated state variable (such as a struct) is very useful for both approaches. 

The multiple delegate/logic contract proxy approach would benefit from being able to create dislocated state variable like you mentioned.  Each logic contract could use one or more dislocated state variables for its data.

I understand that creating dislocated storage layouts for contracts would require more work and greater consensus. But are you able to move forward now in creating what you have suggested to create dislocated state variable, something like this: `Data data internal at keccak256("DelegateB");` ?
Because I want to start using that in proxy contracts and teaching others about this awesome new capability.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-598184935):

I have some thoughts on all this, but I think this needs a dedicated brainstorming session to get everything right. I currently aim for the online solidity summit to do this.

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-598269261):

Sounds good.

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-598318497):

I just applied to go to the online solidity summit.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-932474378):

> While this seem like an elegant solution, please note that this addition means either doing an extra round of hashing (and storing the input in the contract) or storing the hash in the contract, both of which are quite expensive.

That should no longer be true after #11026. If the key is known at compile-time, the compiler should be able to pre-compute the values, right?

> I propose that the layout works the same with one change: Instead of starting at position 0 make a keccak256 hash of the name of the contract and start the storage for the contract at the position given by the hash. This way the storage space is separate for each contract that has a different name.

This topic was brought up on the chat yesterday and I think it's something users can already do without any new syntax. The example from https://github.com/ethereum/solidity/issues/8353#issuecomment-589249774 could be written like this:
```solidity
contract DelegateB {
    struct Data {
        uint a;
        uint b;
    }

    mapping (string => Data) delegateData;

    function data() internal view returns (Data storage) {
        return delegateData["DelegateB"];
    }

    function doSomething() external {
        data().a = 8;
        data().b = 7;
    }
}
```
You can have multiple delegate contracts, each with its own different `Data` struct. Each delegate has to define the `delegateData` mapping separately as the first storage variable so that these mappings share the same storage location. The thing about mappings is that they reserve a slot in the contract but that slot is unused. Its only purpose is to distinguish between different mappings - it's used as salt when computing storage locations for keys. Each delegate gets a deterministically assigned storage location for its struct (only depends on the key) and as long as the key stays the same, you can upgrade the delegate and have it keep its storage.

The only thing you cannot do without inline assembly is accessing the data of a different delegate - each `Data` struct is different so you can access delegate's key in `delegateData` but it will have wrong type.

If this pattern is enough, then we could consider introducing some syntax sugar for it:
```solidity
contract mapping DelegateB {
    uint a;
    uint b;

    function doSomething() external {
        a = 8;
        b = 7;
    }
}
```
This `contract mapping` would generate the code equivalent to the previous example. The key would be just the name of the contract (maybe qualified with source unit name, though this might be problematic given how different frameworks handle paths differently) and each derived contract would get a separate key and a different `Data` struct. Hiding the mapping and not allowing other "top-level" state variables has the nice benefit of preventing people from adding stuff outside of the `Data` struct and unintentionally breaking the pattern, which can easily happen if you implement this pattern manually.

It's fully backwards-compatible because `contract mapping` would be a new thing, not affecting how `contract`s work.

If we wanted to allow accessing storage of different contracts, we could also go for a different syntax:

```solidity
contract DelegateA {
    zone {
        uint c;
    }

    zone {
        uint a;
        bytes b;
    }

    zone SharedDelegateData {
        uint x;
        bytes4 y;
    }
}

contract DelegateB {
    zone SharedDelegateData {
        uint x;
        bytes4 y;
    }

    zone {
        uint a;
        uint b;
    }

    function doSomething() external returns (bytes4) {
        a = DelegateA.c;
        return y;
    }
}
```
Each unnamed storage "zone" would internally get an index and its mapping key would be a concatenation of contract name and that index. But index would not be necessary to access it. Names across all zones in the same contract would have to be unique so you could access them using contract name.

A named zones would get a key that depends only its name. Such zones could be used to deliberately share storage locations with other contracts. They key could also have some domain-separated suffix to ensure that they never conflict with unnamed zones based on contract names. Named ones give some more flexibility but they will also get out of sync much easier so we could also only allow unnamed zones. 

I would disallow mixing zones and normal state variables, though technically it should be entirely possible to allow that.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-942444712):

We discussed this on the call today and unfortunately we did not reach a decision yet:
- @chriseth thinks that having the location depend only on contract name is a bad idea. The prefix used for hashing should be more explicit.
- @axic mentioned that the upcoming changes related to state expiry might make the feature moot - the likely implementation of this will make access to lower ~100 slots cheaper than to the higher ones so always putting state variables a random location would not be a good default.
- The solution with a mapping is generally not a bad way to solve the problem but it does have some downsides:
    - Structs are packed differently than contracts. We'd need something like #11691 to make it efficient in terms of storage usage.
    - The old code generator does not optimize nested keccak calls with constant values so while it could theoretically be as efficient as putting variables directly in a contract, currently it won't be.
- The biggest problem is really that we have a lot of proposals but not enough feedback on actual use cases to be able to decide.

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-942476743):

@cameel thanks for this update, much appreciated.

> @axic mentioned that the upcoming changes related to state expiry might make the feature moot - the likely implementation of this will make access to lower ~100 slots cheaper than to the higher ones so always putting state variables a random location would not be a good default.

Don't mappings and dynamic arrays use random locations from hashes for storage?  Would that change? It seems to me that the majority of data in contracts are stored in those. It doesn't matter much if the first 100 slots are cheaper because most of the data isn't/won't be stored there.

I wonder how much cheaper the first 100 slots would be.  That would be important to know.

> The biggest problem is really that we have a lot of proposals but not enough feedback on actual use cases to be able to decide.

A number of projects are using Diamond Storage which is the method of storing a struct at a random location in storage.  You can get feedback from users on this approach.  A list of projects using diamonds is here: https://eip2535diamonds.substack.com/p/list-of-projects-using-eip-2535-diamonds

Here is a Solidity library that is based on Diamond Storage to implement various contract functionality:  https://github.com/solidstate-network/solidstate-solidity

If users like this pattern, and its usage is growing, which it is, it might make sense to support the pattern more in the Solidity language.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-942487102):

> Don't mappings and dynamic arrays use random locations from hashes for storage? Would that change? It seems to me that the majority of data in contracts are stored in those.

They do. If you are using them, you will have to pay more so you'll be affected either way. The problem is that some projects can fit in the first slots and will want to.

> I wonder how much cheaper the first 100 slots would be. That would be important to know.

I don't think that's clear yet. At the call even @axic could not give much detail on that.

> If users like this pattern, and its usage is growing, which it is, it might make sense to support the pattern more in the Solidity language.

Well, the pattern does have some downsides as pointed out above so it's not such a no-brainer I thought it was. Doesn't mean we won't implement it but right now at least there's still no consensus that it's the best approach.

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-942490888):

>  The problem is that some projects can fit in the first slots and will want to.

Do we know any projects?   Every project I know of stores most of their data in mappings and/or dynamic arrays.

#### <img src="https://avatars.githubusercontent.com/u/49092?u=e839203b6d7460e1a1907d4d8071a7fe351dce67&v=4" width="50">[mudgen](https://github.com/mudgen) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-942496861):

> Well, the pattern does have some downsides as pointed out above so it's not such a no-brainer I thought it was. Doesn't mean we won't implement it but right now at least there's still no consensus that it's the best approach.

Understood, I personally like the idea of letting the user choose the slot or the data that is hashed to choose the slot. 
For example I like this method of choosing a slot location:
```solidity
bytes32 constant MYSTRUCT_POSITION = 
    keccak256(abi.encodePacked(
      ERC1155.interfaceId, 
      ERC1155.name, 
      address(this)
    ));
```

That would choose a slot location for a struct that contains state variables for an ERC1155 implementation.

@cyotee showed me this.

#### <img src="https://avatars.githubusercontent.com/u/137030?v=4" width="50">[cameel](https://github.com/cameel) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-942518520):

> Do we know any projects? Every project I know of stores most of their data in mappings and/or dynamic arrays.

Someone mentioned one on the call. @hrkrshnn @axic, was that Uniswap?

#### <img src="https://avatars.githubusercontent.com/u/4380063?v=4" width="50">[cyotee](https://github.com/cyotee) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-942580667):

Hi, thanks for the call out @mudgen.

@cameel, If you're interested I can comment on some of the points you've brought up.

STL;DR

Unstructured storage powerful. Language limits exercising power. Give us more power.

TL;DR

Being able to dynamically define storage pointers for variables has expanded my ability to write more flexible contracts. Main issues have come from structs not having a standard way to identify them that is inherent to their code, like `interfaceId` for interfaces. I have to go through a lot of work to write code to ensure it's all being done in a consistent way. Language and compiler support would make this a lot easier, can could standardize the usage.

BUT, still allow for developer defined storage pointers so we can manipulate them as needed. So possible an addition to instantiating structs to let us set the storage pointer at the start.

Promoting structs to a type, somehow, seems like the optimal solution. Baring limitations, as writing compilers is beyond me.

P.S.

All this is really only relevant in the context of dynamic proxies as this whole option facilitates being able to use `delegatecall` dynamically based on mapping transaction properties to target addresses. And that is limited due to the inherent surcharge. So, really, this would be most beneficial if something can be done about the surcharge for using external libraries so we can leverage this further. Which would have the side effect of potentially reducing chain size as we wouldn't need to deploy as much redundant code to get around the surcharge.

Pontification Mode Active /

I agree that a storage pointer standard should rely on an inherent property of the struct to minimize any human errors from having an arbitrary definition. I've settled on using this to calculate my storage slots.

`bytes32 private constant STORAGE_SLOT = keccak256(type(ServiceProxyStorage).creationCode);`

Where the storage library code consists of

```
library ServiceProxyStorage {

  struct Layout {
    mapping(bytes4 => address) serviceForFunctionSelector;
  }

  function _layout(bytes32 slot) pure internal returns (Layout storage layout) {
    assembly{ layout.slot := slot }
  }
  
}
```
The default storage slot is defined and used in a library where I implement the desired CRUD operations for the struct defined in the storage library.

By using the creation code for the struct defining library I can ensure the default slot is intrinsic to the definition of the struct. By including a function that effectively casts a storage pointer to the struct i also provide an option to define a different slot if needed.

I've settled on defining structs in libraries with their `_layout` casting function. Implementing the CRUD functions in a library where I can actually access the creation code of the struct definition library. And then using that CRUD library in an abstract contract to define the "business logic" for a contract as an atomic unit of functionality using only internal functions. Then expose those internal function with a contract that only implements external functions and a constructor if needed.

With that background out of the way, It would be helpful if struct could have implicit properties, like how interfaces have selector properties, or like `type` offers. Then we could have an inherent identifier to use when dynamically defining storage slots. Maybe there's something available in the ABI encoder when it's encoding structs for passing between contracts that could give that inherent identifier?

Also, one point I didn't explain on why I implement my storage usage as I do is that I do have times where I don't want to use the default storage slot. So I made sure to leave the ability for me to reuse my struct definitions, and CRUD libraries with a different storage slot. An example of how I use these libraries in an abstract contract is here.

```
function _setOwner(
    address owner
  ) internal {
    OwnableLib._layout(OwnableLib._getDefaultSlot() )._setOwner(owner);
 }
```

The CRUD library exposes a `_layout` function that passes through to the struct defining library to facilitate loading the right struct without the contract inheriting the abstract having to know anything about the underlying structs or their usage. Which helps maintain a separation of concerns between the presentation layer and the internal logic. So I'm kind fo treating the storage pointer like a database connection, and my struct and CRUD libraries like database driver libraries.

So, it would be nice to also have an option when instantiating a struct to define its storage slot so we don't have to go through all these hoops, and can eliminate the usage of assembly to access the `slot` property of a storage variable.

#### <img src="https://avatars.githubusercontent.com/u/4380063?v=4" width="50">[cyotee](https://github.com/cyotee) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-943104430):

Another matter I've been considering is the potential benefit of including a modifier when calling a function using an interface to designate that a `delegatecall` should be used. I was thinking of 3 options, again, no idea how it might be best to implement. just examples to explain the idea.

`IContract(address).delegate.externalFunction();`

`IContract(address).externalFunction{delegate}();`

or maybe

```
delegate{
  IContract(address).externalFunction();
}
```

Either way, I raise the point here as I see the primary usage of unstructured / dynamically assigned storage being to facilitate using `delegatecall` by resolving the storage slot expectation mismatch problem. So it might be relevant when considering a storage slot standard.

Also, it does work to use external libraries as the implementation for a proxy. It might be appropriate to tie this to the relevant library when it's external. If there is some way to introduce an externally accessible contract metadata system that would work for libraries, then the blockchain could build up a standard set of available data types and logic accessible more easily, and reliably. Like how ERC165 was baked into the compiler.

But all of this might not be very useful since we don't have true polymorhpism and reflection in the EVM. But from some experimentation, I think it would be possible to realize fuller functionality. The reflection could be handled with a contract metadata standard baked into the compiler so developer can more easily expose those properties to other contracts.

And with a standardized set of external libraries, we might be able to leverage more implementations like in ERC1167. Making it possible to store parts of a contracts bytecode, and compose them with external libraries so developers wouldn't need to write as much code. And users could know that proven,, immutable code was being reused from the blockchain. Almost making it possible to treat the blockchain like a byte code file path for loading the relevant libraries. Which might make it possible to implement a declarative framework for Solidity that will handle composing the appropriate bytecode based on deployed external libraries declared like dependencies.

Something like

```
import <ERC20 Mintable Library Address>

@ERC20Mintable{
  name: "Token",
  symbol: "T",
  decimals: 18,
  totalSupply: 1000**decimals
}
```

Then the compiler can check the metadata for the library address, like the interface ID and function selectors, and compile bytecode similar to ERC1167 mapping the library function selectors to the library address in the new proxy. Using a standardized storage slot for the function to library mapping that could be overriden if needed. So, just like being able to drop down to assembly, developers could opt to drop down to base Solidity when they need to do something that isn't already available. But a reverse lookup system could be implemented to make it easier to find available external libraries to import. Treating libraries on chain like packages. And this could then be extended so part of a contract's metadata would be able other contracts that it's dependent upon. So if other libraries need to be included they can be pulled in as more mapped functions in the new proxy. Which means it might be possible to actually find implementations you can import using just the interface id for the functionality you want.

I'm not really capable of writing a compiler, far too rusty. But I was meaning to try implementing a declarative build tool to trial this idea. Initially writing it all as a Hardhat plugin that would start by including my repo as the available libraries, and use my on-chain proxy factory system to identify available modules and simply deploy a proxy using those already deployed contracts. Then see if I can figure out the bytecode to handle it with a more direction insertion by splicing the strings resulting in immutable proxies created at deployment. Then there simply wouldn't be any actual source code. just declarations and bytecode that handles the `delegatecall` to the library for that function.

#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-943109263):

@cyotee Maybe you would be interested in https://github.com/ethereum/solidity/issues/12096

#### <img src="https://avatars.githubusercontent.com/u/23176187?u=3ab83556e51e7e482bb618658fcd44e4b7ea912c&v=4" width="50">[novaknole](https://github.com/novaknole) commented at [2020-02-20 02:09](https://github.com/ethereum/solidity/issues/8353#issuecomment-962080775):

@mudgen @cameel 

Thanks for such a great discussion. I also am arriving on this and love the topic. 

Below is @cameel 's example. 

```js
contract DelegateB {
    struct Data {
        uint a;
        uint b;
    }

    mapping (string => Data) delegateData;

    function data() internal view returns (Data storage) {
        return delegateData["DelegateB"];
    }

    function doSomething() external {
        data().a = 8;
        data().b = 7;
    }
}
```

I don't understand how good this way can be when there's only proxy and only one logic contract at any time. Let's say for the proxy contract, first version was `DelegateB` contract above.   You all know how variables and values will be stored in this case. let's say `a=10, b=20`. Time for the update. So we write new `DelegateC` contract and the main point is I should not be forced to append variables in the struct, if I am forced, then what's the point of this pattern in the first place ? So new `Data` gets changed to

```js
struct Data {
    uint k; // NOTE: we put this in the beginning.
    uint a; 
    uint b;
 }
```

If the key in the mapping stays `DelegateB`, we are screwed as collision happens (`k` gets put in the same slot as `a` ), so we gotta change the key as well to `DelegateC`. Now, we are safe because collisions don't happen, but we got another problem. Since new key appeared in the mapping,  if we do `delegateData[delegateC].a`, we won't get the value of `a`, which I really need otherwise, what's the point of upgrade ? the point of upgrade is i change state variables, add some features, but I also need to know the values of old variables, otherwise state is lost. Okay, so the way of solving this is that in the new upgraded contract `DelegateC`, I should start writing/using `delegateData[delegateB]` alongside `delegateData[delegateC]`.  And the story will go on and on. In the new updated version - `delegateD` contract, I would need to use `delegateData[delegateB], delegateData[delegateC] and delegateData[delegateD]` all three. So changing orders of variables or their types, this pattern doesn't seem to work. 

am I missing something ?


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
