# [\#10480 Issue](https://github.com/ethereum/solidity/issues/10480) `open`: Wrong evaluation order of arguments to parent constructors
**Labels**: `breaking change :warning:`, `language design :rage4:`


#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) opened issue at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480):

## Description

The arguments to parent constructors are evaluated before all parent constructors.

This is a problem when those arguments are (non-pure) function calls, because the function is evaluated before its defining contract has been initialized.

## Steps to Reproduce

```solidity
pragma solidity 0.7.5;

contract Foo {
    event FooConstructor();
    event FooGetValue();
    
    uint value;
    
    constructor (uint _value) {
        emit FooConstructor();
        value = _value;
    }
    
    function getValue() public returns (uint) {
        emit FooGetValue();
        return value;
    }
}

contract Bar {
    event BarConstructor(uint value);
    
    constructor (uint value) {
        emit BarConstructor(value);
    }
}

contract Child is Foo, Bar {
    constructor () Foo(5) Bar(Foo.getValue()) {
    }
}
```

Deploying `Child` currently emits the following sequence of events:

1. `FooGetValue()`
2. `FooConstructor()`
3. <code>BarConstructor(<b>value: 0</b>)</code>

Note: The same thing happens if you add a constraint on linearization such as `Bar is Foo`.

### Expected Behavior

Deploying `Child` should emit the following:

1. `FooConstructor()`
2. `FooGetValue()`
3. <code>BarConstructor(<b>value: 5</b>)</code>

That is, `Foo` is first initialized by calling its constructor, only then `Foo.getValue()` is evaluated, and its value can then be used as argument to `Bar`'s constructor.

### Specification

In general, I would expect the following to happen during contract construction:

Given the linearization of a contract's inheritance `C_0`, ..., `C_n` (from base to most derived), and the argument expressions for each constructor `e_i1`, ..., `e_im` (`m` different for every `C_i`)

- For each `C_i` = `C_0`, ..., `C_n` in order
  - Evaluate all of `e_i1`, ..., `e_im` (I believe this order is not specified, as for function calls in general)
  - Evaluate the constructor `C_i(v_i1, ..., v_im)` (where `v_ik` are the results of the previous step)

Every non-pure contract function call in the expression `e_ik` must be of a function defined in one of `C_0`, ..., `C_(i-1)`, that is only contracts that have been initialized at the point it is evaluated. (Note: this check can be relaxed somewhat but I decided to keep the simpler version.)


#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-738110420):

We had a discussion on Gitter today about that, and I personally think that the current rule makes sense and is actually quite simple. Given the linearized hierarchy:
1) Evaluate all base call arguments in the order (most derived) -> (most base)
2) Construct all contracts (most base) -> (most derived)

This still accepts the possibility that you mentioned, of accessing a contract that hasn't been fully constructed yet. However, all state variables are initialized as 0 before the rules 1) and 2) above. Also, this still wouldn't prevent a contract depending on itself to be constructed:

```
contract Foo {
    event FooConstructor();
    event FooGetValue();
    
    uint value;
    
    constructor (uint _value) {
        emit FooConstructor();
        value = _value;
    }
    
    function getValue() public returns (uint) {
        emit FooGetValue();
        return value;
    }
}

contract Bar is Foo {
    event BarConstructor(uint value);
    uint valueBar;

    constructor (uint value) Foo(value + getValue()) {
        emit BarConstructor(value);
    }

    function g() internal view returns (uint) {
        return valueBar;
    }
}

contract Child is Bar {
    constructor () Bar(Bar.g()) {
    }
}
```
Here you need to evaluate `Foo(value + getValue())` before finishing constructing `Foo`, which then depends on already having visited `Bar(Bar.g())`.

So in my opinion the order is correct.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-738494216):

>However, all state variables are initialized as 0 before the rules 1) and 2) above

Sure, we're not talking about uninitialized memory like on a regular computer program. Of course that would be a more serious issue. What I'm pointing out is an issue of language semantics.

A contract assigns a state variable in the constructor then exposes that state variable through a getter function: would anyone expect the getter to ever return anything other than the value assigned in the constructor? I don't think so. The intuition is that the constructor runs before anything else does. And it goes beyond reading uninitialized values, constructors can establish invariants that are assumed to hold for the execution of other functions in the contract. This evaluation order is violating that assumption, which is why I see this as a pretty serious issue.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-738497777):


> Here you need to evaluate `Foo(value + getValue())` before finishing constructing `Foo`, which then depends on already having visited `Bar(Bar.g())`.

I don't think this code should be allowed. It doesn't make sense to call a function if its defining context hasn't been initialized.

Here's a similar example in JavaScript, which is disallowed:

```js
class A {
  constructor(value) {
    this.value = value;
  }

  getValue() {
    return this.value;
  }
}

class B extends A {
  constructor() {
    super(this.getValue()); // ReferenceError: Must call super constructor
                            // in derived class before accessing 'this'
  }
}

new B();

```

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-738653448):

> I don't think this code should be allowed. It doesn't make sense to call a function if its defining context hasn't been initialized.

Ok, I wasn't sure if that's what you were suggesting. I can agree with that!

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-738685949):

Funnily enough, that exact thing just bit me in https://github.com/ethereum/solidity/pull/10389

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-739871628):

@frangio can you please edit the description and add a summary of the desired behaviour?

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-741872433):

@chriseth I reorganized the issue description and added a specification of the desired behavior.

#### <img src="https://avatars.githubusercontent.com/u/481465?v=4" width="50">[frangio](https://github.com/frangio) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-770867579):

I believe this issue is missing the `bug` label.

#### <img src="https://avatars.githubusercontent.com/u/9073706?v=4" width="50">[chriseth](https://github.com/chriseth) commented at [2020-12-02 19:25](https://github.com/ethereum/solidity/issues/10480#issuecomment-770883737):

It's not a bug, it was a design choice - maybe a bad one :)


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
