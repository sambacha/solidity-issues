# [\#11555 Issue](https://github.com/ethereum/solidity/issues/11555) `closed`: [Docs] Builtin function argument evaluation order is different between legacy and new code gen
**Labels**: `bug :bug:`, `documentation :book:`


#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) opened issue at [2021-06-17 18:49](https://github.com/ethereum/solidity/issues/11555):

We claim in the IR breaking changes docs that function arguments are evaluated the same way by both code gens.

See https://github.com/ethereum/solidity/blob/6e74df8ab6d3a0b24adf8eed50d2dfb1d6982e03/docs/ir/ir-breaking-changes.rst

However, arguments passed to builtin functions (Global variables to be specific, see https://docs.soliditylang.org/en/v0.8.5/cheatsheet.html#global-variables) such as `addmod` and `mulmod` are evaluated inconsistently (left-to-right by Sol->Yul and right-to-left by legacy).

The following test demonstrates this nicely. The functions `test` and `add` are user-defined functions, whose return value is consistent with both code gens evaluating args the same way.

However, the function `f()` that invokes `addmod` returns `5` for legacy code gen and `3` for new code gen. So, the test partially passes (i.e., `test(uint8, uint8)` call passes via both codegens) but fails for the `addmod` invocation.

Perhaps this is a corner case that needs clarification in the breaking changes docs. I will use the documentation label for this issue unless someone thinks otherwise.

```
contract C {
  uint x = 0;
  function g() internal returns (uint) {
    return ++x;
  }
  function f() external returns (uint) {
    return addmod(g(), g(), g() + 17);
  }
  function add(uint8 _a, uint8 _b) public pure returns (uint8) {
    return _a + _b;
  }
  function test(uint8 _a, uint8 _b) public pure returns (uint8) {
    return add(++_a + ++_b, _a + _b);
  }
}
// ====
// compileViaYul: also
// ----
// test(uint8,uint8): 1, 2 -> 10
// f() -> 5
```

#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) commented at [2021-06-17 18:49](https://github.com/ethereum/solidity/issues/11555#issuecomment-863537049):

I am not able to reproduce this via Remix. So this looks like a bug in EVMHost to me. Remix example below produces identical results for both `solFunction()` and `yulFunction()`. My assumption is that the yul function uses Sol->Yul for code gen but this may be wrong as well.

Edit:
  - In Remix, both `solFunction()` and `yulFunction()` seem to be evaluating arguments to `addmod` from *right-to-left*
  - I am unable to force viaYul using a Remix setting, so perhaps this is why the divergence cannot be demonstrated via Remix alone.
  - I may have been wrong about bug being in the EVMHost part. See following comments.

```
pragma solidity >=0.8.5;
contract C {
  uint x = 0;
  function yulFunction() external returns (uint r) {
      assembly {
          function incrementStorageVarInAssembly() -> s {
              sstore(x.slot, add(sload(x.slot), 1))
              s := sload(x.slot)
          }
          sstore(x.slot, 0)
          r := addmod(incrementStorageVarInAssembly(), incrementStorageVarInAssembly(), add(incrementStorageVarInAssembly(), 17))
      }
  }
  function incrementStorageVar() public returns (uint) {
    return ++x;
  }
  function solFunction() external returns (uint) {
      return addmod(incrementStorageVar(), incrementStorageVar(), incrementStorageVar() + 17);
  }
}
```

#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) commented at [2021-06-17 18:49](https://github.com/ethereum/solidity/issues/11555#issuecomment-863542752):

Looking at the Yul code generated for the original example, I get the feeling that this is not an EVMHost issue but a divergence in the legacy and new code generators.

Having said that, I have not yet found a way to trigger this divergence in code that may be compiled via Sol->Yul in the production compiler.

#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) commented at [2021-06-17 18:49](https://github.com/ethereum/solidity/issues/11555#issuecomment-863857685):

Here's a much simpler test case that demonstrates that legacy codegen evaluates arguments to `addmod` (and presumably `mulmod` right-to-left). For user-defined functions, on the other hand, argument evaluation order is left-to-right even by the legacy code gen as shown in the first test case above.

```
pragma solidity >=0.0.0;
contract C {
    function f() pure external returns (uint r1, uint r2) {
        uint x = 3;
        // Expected result: 4
        // Obtained result: 0
        r1 = addmod(++x, ++x, x);
        assembly {
            r2 := addmod(4, 5, 5)
        }
    }
}
```

And a much simpler semantic test compared to the original example

```
contract C {
  function f() external returns (uint) {
    uint x = 3;
    return addmod(++x, ++x, x);
  }
  function add(uint8 _a, uint8 _b) public pure returns (uint8) {
    return _a + _b;
  }
  function test(uint8 _a, uint8 _b) public pure returns (uint8) {
    return add(++_a + ++_b, _a + _b);
  }
}
// ====
// compileViaYul: also
// ----
// test(uint8,uint8): 1, 2 -> 10
// f() -> 0
```

```
$ isoltest -t semanticTests/addMod
...
  Running via Yul:
  Expected result:
  // test(uint8,uint8): 1, 2 -> 10
  // f() -> 0

  Obtained result:
  // test(uint8,uint8): 1, 2 -> 10
  // f() -> 4
  Warning: The call to "f()" returned 
  [0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,4]
  
  Attention: Updates on the test will apply the detected format displayed.
  
  Note that the test passed without Yul.
```

#### <img src="https://avatars.githubusercontent.com/u/2388185?v=4" width="50">[bshastry](https://github.com/bshastry) commented at [2021-06-17 18:49](https://github.com/ethereum/solidity/issues/11555#issuecomment-865168059):

JFYI: The following patch fixes this divergence.

```
diff --git a/libsolidity/codegen/ExpressionCompiler.cpp b/libsolidity/codegen/ExpressionCompiler.cpp
index 5b092363b..db42ca29e 100644
--- a/libsolidity/codegen/ExpressionCompiler.cpp
+++ b/libsolidity/codegen/ExpressionCompiler.cpp
@@ -966,11 +966,12 @@ bool ExpressionCompiler::visit(FunctionCall const& _functionCall)
 		case FunctionType::Kind::AddMod:
 		case FunctionType::Kind::MulMod:
 		{
+			for (unsigned i = 0; i < 2; i ++)
+				acceptAndConvert(*arguments[i], *TypeProvider::uint256());
 			acceptAndConvert(*arguments[2], *TypeProvider::uint256());
 			m_context << Instruction::DUP1 << Instruction::ISZERO;
 			m_context.appendConditionalPanic(util::PanicCode::DivisionByZero);
-			for (unsigned i = 1; i < 3; i ++)
-				acceptAndConvert(*arguments[2 - i], *TypeProvider::uint256());
+
 			if (function.kind() == FunctionType::Kind::AddMod)
 				m_context << Instruction::ADDMOD;
 			else
```


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
