# [\#12271 Issue](https://github.com/ethereum/solidity/issues/12271) `open`: [SMTChecker] Fails to prove a simple assertion with a mapping

#### <img src="https://avatars.githubusercontent.com/u/29802592?v=4" width="50">[olehmisar](https://github.com/olehmisar) opened issue at [2021-11-11 15:14](https://github.com/ethereum/solidity/issues/12271):

## Description

This code emits an SMTChecker warning when compiled, although `balanceOf[account]` can never be greater than `totalSupply`.
<details>
<summary>Code:</summary>

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.10;

contract Token {
    mapping(address => uint256) public balanceOf;
    uint256 public totalSupply;

    constructor(uint256 totalSupply_) {
        balanceOf[msg.sender] = totalSupply_;
        totalSupply = totalSupply_;
    }

    function transfer(address recipient, uint256 amount) public {
        balanceOf[msg.sender] -= amount;
        balanceOf[recipient] += amount;
    }

    function rule_maxBalanceLessThanTotalSupply(address account) external view {
        assert(balanceOf[account] <= totalSupply);
    }
}
```
</details>

<details>
<summary>Warining:</summary>

```
Warning: CHC: Assertion violation might happen here.
  --> contracts/Token.sol:19:9:
   |
19 |         assert(balanceOf[account] <= totalSupply);
   |         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^


Warning: BMC: Assertion violation happens here.
  --> contracts/Token.sol:19:9:
   |
19 |         assert(balanceOf[account] <= totalSupply);
   |         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
Note: Counterexample:
  account = 7719
  balanceOf[account] = 39
  totalSupply = 38

Note: Callstack:
Note:
```

</details>

Looks like SMTChecker blindly assigns any value to a mapping entry:
```
Note: Counterexample:
  account = 7719
  balanceOf[account] = 39
  totalSupply = 38
```

I also created the same token contract but used `balance0` and `balance1` instead of a `mapping`. SMTChecker works as expected in this case. So, I can conclude that it is definitely a problem with `mapping`s.

<details>
<summary>Code:</summary>
Compiles without warnings:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.10;

contract Token2 {
    uint256 public balance0;
    uint256 public balance1;
    uint256 public totalSupply;

    constructor(uint256 totalSupply_) {
        balance0 = totalSupply_;
        totalSupply = totalSupply_;
    }

    function transfer(uint256 amount) public {
        if (msg.sender == address(0)) {
            balance0 -= amount;
            balance1 += amount;
        } else {
            balance1 -= amount;
            balance0 += amount;
        }
    }

    function rule_maxBalanceLessThanTotalSupply() external view {
        assert(balance0 <= totalSupply);
        assert(balance1 <= totalSupply);
        assert(balance0 + balance1 == totalSupply);
    }
}
```

</details>

## Environment

- Compiler version: 0.8.10
- Target EVM version (as per compiler settings): london
- Framework/IDE (e.g. Truffle or Remix): hardhat
- EVM execution environment / backend / blockchain client: hardhat network
- Operating system: macOS 12.0.1

## Steps to Reproduce
```sh
git clone git@github.com:olehmisar/smt-checker-bug-reproduction-mapping.git
yarn install
yarn hardhat compile --force
```

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2021-11-11 15:14](https://github.com/ethereum/solidity/issues/12271#issuecomment-966471760):

tldr: assertion is not as simple as it looks, the underlying SMT and Horn solvers were not able to prove the required nonlinear invariant.

Unfortunately this assertion is logically not as simple as it looks, even though it's a really good property!

The counterexample you see comes from the BMC engine which has lots of false positives. If you use the CHC engine alone (which is what I usually do and recommend) you'll probably see that it says it couldn't prove either way, so an assertion violation `might happen`.

Usually people prove functional correctness of the `transfer` function +- this way:
```
	function transfer(address recipient, uint256 amount) public {
		require(balanceOf[msg.sender] >= amount);
		uint sumPrev = balanceOf[msg.sender] + balanceOf[recipient];
		balanceOf[msg.sender] -= amount;
		balanceOf[recipient] += amount;
		uint sumPost = balanceOf[msg.sender] + balanceOf[recipient];
		assert(sumPrev == sumPost);
	}
```
This version is also proved correct by the CHC engine in the SMTChecker. Notice that this is a local property, that must hold over a single transaction.

Your property is however much much more meaningful because it holds over *infinitely many* transactions, and is much more difficult to prove. In order for the solver to prove this, it would have to find a nonlinear invariant, which is really hard. I tried your code with two different Horn solvers as backend for the SMTChecker, the default one and a different one. Neither could solve it.
When you change it to two variables instead of a mapping the problem indeed becomes much simpler and easier to solve.

#### <img src="https://avatars.githubusercontent.com/u/29802592?v=4" width="50">[olehmisar](https://github.com/olehmisar) commented at [2021-11-11 15:14](https://github.com/ethereum/solidity/issues/12271#issuecomment-966478458):

Thanks for the extensive explanation. I appreciate it.

Sorry if the next question is stupid(I don't know anything about how solvers work). If it is possible to unroll a mapping to two variables, is it possible to unroll a mapping to an infinite number of variables, so the solver can prove correctness of the code?

#### <img src="https://avatars.githubusercontent.com/u/29802592?v=4" width="50">[olehmisar](https://github.com/olehmisar) commented at [2021-11-11 15:14](https://github.com/ethereum/solidity/issues/12271#issuecomment-966481108):

And I want to note that from my perspective(just a smart contract developer), SMTChecker is unfortunately useless right now because mappings are used extensively in smart contracts. I wanted to replace a bunch of unit tests in future projects with formal verification and now I see that it is currently impossible. :(

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2021-11-11 15:14](https://github.com/ethereum/solidity/issues/12271#issuecomment-966486578):

> Sorry if the next question is stupid(I don't know anything about how solvers work). If it is possible to unroll a mapping to two variables, is it possible to unroll a mapping to an infinite number of variables, so the solver can prove correctness of the code?

In a very high level that's what the solver does. A mapping is treated as an infinite array, so it's kind of that.

#### <img src="https://avatars.githubusercontent.com/u/504195?u=ce2facd14af9fd474ebff49f0d44891f56f7500f&v=4" width="50">[leonardoalt](https://github.com/leonardoalt) commented at [2021-11-11 15:14](https://github.com/ethereum/solidity/issues/12271#issuecomment-966496224):

> And I want to note that from my perspective(just a smart contract developer), SMTChecker is unfortunately useless right now because mappings are used extensively in smart contracts. I wanted to replace a bunch of unit tests in future projects with formal verification and now I see that it is currently impossible. :(

Thanks for the feedback!
That's not always the case though. Mappings are not always that problematic, note that the main problem here is the nonlinear math (variable * variable) over multiple transactions, and not the mapping itself. Tools like the SMTChecker cannot handle this type of math very well, and this includes hevm and other symbolic execution tools. For example, even a simplified AMM code like https://github.com/d-xo/symtest-examples/blob/main/src/AMM.sol , that doesn't use mappings at all, won't be supported. We also noticed recently that the solver that the SMTChecker uses by default doesn't solve many problems that another solver we recently discovered does, because of the way that the SMTChecker encodes the contracts. Unfortunately it's still some work to be able to use the second solver.
Just as an example, we recently managed to verify the Open Zeppelin's ERC777 implementation without changes, and those contracts use a lot of mappings, even nested mappings, and other complicated features, but this required us to use this new solver which is not fully integrated and shipped with the compiler.

I think you're on the right path with the properties it looks like you're writing, hopefully the tool will be able to cope with some of them.
I'd suggest that you also use the `modelChecker.contracts` option to choose exactly which contract you're verifying, this helps the solver a lot, and also increase the `modelChecker.timeout`.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
