# [\#12476 Issue](https://github.com/ethereum/solidity/issues/12476) `closed`: Possible bug with memory arrays write access

#### <img src="https://avatars.githubusercontent.com/u/28758736?u=1b6dd0d74cf88927c0642389a7b897d75c918530&v=4" width="50">[Nielsbishere](https://github.com/Nielsbishere) opened issue at [2022-01-02 20:23](https://github.com/ethereum/solidity/issues/12476):

## Description

When you pass arrays to a library or function, it doesn't always correctly modify the array. Is there a reason for this or is this a bug?
My guess is that instead of passing it as a reference, it copies the memory. This seems like it would have an impact on performance and prevent things that I am trying to do.

The example is in reproduce, what I do is the following (and if they produce the expected result):
- Send array wrapped in struct to function and resize or fill it. Size changes ðŸ‘ Data changes ðŸ‘
- Pass array to function and resize or fill it. Size changes ðŸ‘Ž Data changes ðŸ‘
- Pass array to library function and resize or fill it. Size changes ðŸ‘Ž Data changes ðŸ‘Ž
- Pass array wrapped in struct to library function and resize or fill it. Size changes ðŸ‘Ž Data changes ðŸ‘Ž

## Environment

- Compiler version: 0.8.11
- Target EVM version (as per compiler settings): ^
- Framework/IDE (e.g. Truffle or Remix): Remix
- EVM execution environment / backend / blockchain client: ^
- Operating system: N/A.

## Steps to Reproduce

```solidity
// SPDX-License-Identifier: MIT
pragma solidity >=0.8.11;

struct IHaveArray {
    uint128[] arr;
}

library IModifyArray {

    function modifyArray(IHaveArray memory arr) external pure {
        arr.arr = new uint128[](8);
        arr.arr[0] = 1;
    }

    function modifyArray1(IHaveArray memory arr) external pure {
        arr.arr[0] = 1;
    }

    function modifyArray2(uint128[] memory arr) external pure {
        arr = new uint128[](8);
        arr[0] = 1;
    }

    function modifyArray3(uint128[] memory arr) external pure {
        arr[0] = 1;
    }

}

contract WeirdBug {

    function modifyArray0(IHaveArray memory arr) private pure {
        arr.arr = new uint128[](8);
        arr.arr[0] = 1;
    }

    function modifyArray1(IHaveArray memory arr) private pure {
        arr.arr[0] = 1;
    }

    function modifyArray2(uint128[] memory arr) private pure {
        arr = new uint128[](8);
        arr[0] = 1;
    }

    function modifyArray3(uint128[] memory arr) private pure {
        arr[0] = 1;
    }

    function modifyArrays() external pure returns(
        uint256,
        uint128,

        uint256,
        uint128,

        uint256,
        uint128,
        
        uint256,
        uint128
    ) {

        IHaveArray memory arr0;
        arr0.arr = new uint128[](0);
        modifyArray0(arr0);

        IHaveArray memory arr1;
        arr1.arr = new uint128[](8);
        modifyArray1(arr1);

        uint128[] memory arr2 = new uint128[](0);
        modifyArray2(arr2);

        uint128[] memory arr3 = new uint128[](8);
        modifyArray3(arr3);

        IHaveArray memory arr4;
        arr4.arr = new uint128[](0);
        IModifyArray.modifyArray(arr4);

        IHaveArray memory arr5;
        arr5.arr = new uint128[](8);
        IModifyArray.modifyArray1(arr5);

        uint128[] memory arr6 = new uint128[](0);
        IModifyArray.modifyArray2(arr6);

        uint128[] memory arr7 = new uint128[](8);
        IModifyArray.modifyArray3(arr3);

        return(
            arr0.arr.length,
            arr1.arr[0],
            arr2.length,
            arr3[0],
            arr4.arr.length,
            arr5.arr[0],
            arr6.length,
            arr7[0]
        );
    }
}
```

## Output
```
[ 8, 1, 0, 1, 0, 0, 0, 0 ]
```
## Expected output
```
[ 8, 1, 8, 1, 8, 1, 8, 1 ]
```

#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2022-01-02 20:23](https://github.com/ethereum/solidity/issues/12476#issuecomment-1003880121):

Didn't check the example carefully, but it should be the following: external library functions work differently to internal library functions. Internal ones are attached to the contract, while external ones would be deployed and delegatecalled. That should explain the differences that you are noticing.

Closing this for now. Feel free to reply if you have any questions.

#### <img src="https://avatars.githubusercontent.com/u/28758736?u=1b6dd0d74cf88927c0642389a7b897d75c918530&v=4" width="50">[Nielsbishere](https://github.com/Nielsbishere) commented at [2022-01-02 20:23](https://github.com/ethereum/solidity/issues/12476#issuecomment-1004116966):

@hrkrshnn I understand that a delegate call is different from a function call, but why does it always have to copy memory, as opposed to allowing to pass a reference? One of the examples isn't related to external library functions; if you pass a memory array to a local function and re-initialize it to resize, it won't be applied in the calling scope. This does however work if this array is contained in a struct.

#### <img src="https://avatars.githubusercontent.com/u/13174375?u=52d702cb6bec53b561afa293cf9cd53ef7a63924&v=4" width="50">[hrkrshnn](https://github.com/hrkrshnn) commented at [2022-01-02 20:23](https://github.com/ethereum/solidity/issues/12476#issuecomment-1004137134):

> but why does it always have to copy memory, as opposed to allowing to pass a reference?

This is not easily achieved with the EVM currently. Memory is contextual to the current call's scope.

> One of the examples isn't related to external library functions; 

Could you please specify which one is this?

#### <img src="https://avatars.githubusercontent.com/u/28758736?u=1b6dd0d74cf88927c0642389a7b897d75c918530&v=4" width="50">[Nielsbishere](https://github.com/Nielsbishere) commented at [2022-01-02 20:23](https://github.com/ethereum/solidity/issues/12476#issuecomment-1004158342):

@hrkrshnn ah so that explains the cost of external libraries then and why I was getting issues. modifyArray2 doesn't seem to modify array size even though it should.


-------------------------------------------------------------------------------



[Export of Github issue for [ethereum/solidity](https://github.com/ethereum/solidity). Generated on 2022.05.23 at 03:51:38.]
